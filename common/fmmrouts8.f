c**************************************************************************
c--------------------------------------------------------------------------
c
c     initialization module
c
c--------------------------------------------------------------------------
c**************************************************************************

c**************************************************************************
c     the following subroutine precomputes all of the maps that depend on 
c     the specified precision.
c
c     input:
c
c     iprec is a parameter that determines the precision (and in turn the
c           number of terms in the multipole and exponential expansions)
c
c     map is a real array that is blank
c
c     lenmaps is the length of the array map
c
c     output:
c   
c     map contains all of the maps that depend on precision.
c         when map is passed to the fmmstart6 routine, it
c         is divided up in a similar manner.
c
c**************************************************************************
      subroutine computemaps8(map, iprec, lenmaps)
      implicit none
c-----global variables
      integer *4  iprec, lenmaps
      real *8  map(1)
c-----local variables
      integer *4  nterms, nnodes, itot
      integer *4  lvolmaps, lsidemaps, lwint
      integer *4  mmapsouth, mmapnorth, mmapeast, mmapwest
      integer *4  medbletop, medbleside, mesngletop, mesngleside
      integer *4  mwdbletop, mwdbleside, mwsngletop, mwsngleside
      integer *4  mndbletop, mndbleside, mnsngletop, mnsngleside
      integer *4  msdbletop, msdbleside, mssngletop, mssngleside
      integer *4  mwint

      if(iprec .eq. 0)then
        nterms = 8
        nnodes = 8
      elseif(iprec .eq. 1)then
        nterms = 16
        nnodes = 16
      elseif(iprec .eq. 2)then
        nterms = 24
        nnodes = 24
      elseif(iprec .eq. 3)then
        nterms = 40
        nnodes = 40
      endif

      lvolmaps = 2*(nnodes + 1) * 36
      lsidemaps = 2*(nnodes + 1) * 8
      lwint = 2*(nterms + 1) * 8 * 8

      mmapsouth   =  1
      mmapnorth   =  mmapsouth   + lvolmaps
      mmapeast    =  mmapnorth   + lvolmaps
      mmapwest    =  mmapeast    + lvolmaps
      mwint       =  mmapwest    + lvolmaps
      medbletop   =  mwint       + lwint
      medbleside  =  medbletop   + lsidemaps
      mesngletop  =  medbleside  + lsidemaps
      mesngleside =  mesngletop  + lsidemaps
      mwdbletop   =  mesngleside + lsidemaps
      mwdbleside  =  mwdbletop   + lsidemaps
      mwsngletop  =  mwdbleside  + lsidemaps
      mwsngleside =  mwsngletop  + lsidemaps
      mndbletop   =  mwsngleside + lsidemaps
      mndbleside  =  mndbletop   + lsidemaps
      mnsngletop  =  mndbleside  + lsidemaps
      mnsngleside =  mnsngletop  + lsidemaps
      msdbletop   =  mnsngleside + lsidemaps
      msdbleside  =  msdbletop   + lsidemaps
      mssngletop  =  msdbleside  + lsidemaps
      mssngleside =  mssngletop  + lsidemaps

      itot = mssngleside + lsidemaps

      if(itot .gt. lenmaps)then
        write(*,*)'there is not enough workspace needed to'
        write(*,*)'form the maps.'
        write(*,*)'you have set the length of the map'
        write(*,*)'array to be:',lenmaps
        write(*,*)'the workspace needed to form maps is:',itot
        write(*,*)'i am stopping.'
        stop
      endif


      call getmaps8(map(mmapsouth), map(mmapnorth),
     1     map(mmapeast), map(mmapwest), map(medbletop),
     2     map(medbleside), map(mesngletop), map(mesngleside),
     3     map(mwdbletop), map(mwdbleside), map(mwsngletop),
     4     map(mwsngleside), map(mndbletop), map(mndbleside),
     5     map(mnsngletop), map(mnsngleside), map(msdbletop),
     6     map(msdbleside), map(mssngletop),
     7     map(mssngleside), map(mwint), nnodes, nterms)

      return
      end


c**************************************************************************
c     the following subroutine precomputes the maps that are needed in the
c     big to small far case (layer and volume) as well as the weights that
c     map from the polynomial basis coefficients to the multipole terms.
c
c     input:
c     
c     nterms is the number of terms in the multipole expansions
c     
c     nnodes is the number of terms in the exponential expansions
c
c     output:
c
c     mapsouth is the map for getting the big to small south exponential
c
c     mapnorth is the map for getting the big to small north exponential
c
c     mapeast  is the map for getting the big to small east  exponential
c
c     mapwest  is the map for getting the big to small west  exponential
c
c     wint represents the weights that pass from the coefficients to the
c          multipole terms
c
c     edbletop is the east going layer map for the double layer on the top
c              (the other matrices are defined using the same convention)
c
c**************************************************************************
      subroutine getmaps8(mapsouth, mapnorth, mapeast, mapwest,
     1     edbletop, edbleside, esngletop, esngleside,
     2     wdbletop, wdbleside, wsngletop, wsngleside,
     3     ndbletop, ndbleside, nsngletop, nsngleside,
     5     sdbletop, sdbleside, ssngletop, ssngleside,
     6     wint, nnodes, nterms)
      implicit none
c-----global variables
      integer *4   nterms, nnodes
      complex *16  mapnorth(0:nnodes,36), mapsouth(0:nnodes,36)
      complex *16  mapeast(0:nnodes,36),  mapwest(0:nnodes,36)
      complex *16  edbletop(0:nnodes,0:7),  edbleside(0:nnodes,0:7)
      complex *16  esngletop(0:nnodes,0:7), esngleside(0:nnodes,0:7)
      complex *16  wdbletop(0:nnodes,0:7),  wdbleside(0:nnodes,0:7)
      complex *16  wsngletop(0:nnodes,0:7), wsngleside(0:nnodes,0:7)
      complex *16  ndbletop(0:nnodes,0:7),  ndbleside(0:nnodes,0:7)
      complex *16  nsngletop(0:nnodes,0:7), nsngleside(0:nnodes,0:7)
      complex *16  sdbletop(0:nnodes,0:7),  sdbleside(0:nnodes,0:7)
      complex *16  ssngletop(0:nnodes,0:7), ssngleside(0:nnodes,0:7)
      complex *16  wint(0:nterms,0:7,0:7)

      call genweightsmult8(wint,nterms)

      call genbtosvolmat8(mapeast,mapwest,mapsouth,mapnorth,nnodes)

      call genbtoslaymat8(esngletop, wsngletop, nsngletop, 
     1       ssngletop, esngleside, wsngleside, nsngleside, 
     2       ssngleside, edbletop, wdbletop, ndbletop,
     3       sdbletop, edbleside, wdbleside, ndbleside,
     4       sdbleside, nnodes)

      return
      end


c**************************************************************************
c     the following subroutine precomputes the weights that pass from 
c     the polynomial coefficients to the multipole terms.
c
c     input:
c     
c     p is the number of terms in the multipole expansions
c
c     output:
c
c     w represents the weights that pass from the coefficients to the
c          multipole terms
c
c**************************************************************************
      subroutine genweightsmult8(w, p)
      implicit none
c-----global variables
      integer *4  p
      complex *16  w(0:p,0:7,0:7)
c-----local variables
      integer *4  i, j, k
      integer *4  kpts, kind, m, l
      real *8  alpha, beta, endpts(2), b(30)
      real *8  t(30), wnode(30)
      real *8  cheb
      complex *16  sum, imag
      data imag/(0.0,1.0)/

      alpha = 0.0d0
      beta = 0.0d0
      kpts = 0
      endpts(1) = 0.0d0
      endpts(2) = 0.0d0
      kind = 1

      call gaussq(kind, 30, alpha, beta, kpts, endpts, b, t, wnode)

      do k = 0,p
       do i = 0,7
         do j = 0,7-i


          sum = 0.0d0
          do m = 1,30
            do l = 1,30
             sum = sum + wnode(l)*wnode(m)*cheb(t(m),j)*cheb(t(l),i)
     1                  * (t(l) + imag*t(m))**k
            end do
          end do

         w(k,i,j) = sum / 4.0d0

        end do
       end do
      end do

      return
      end


c**************************************************************************
c     the following subroutine precomputes the matrices that map from the
c     coefficients values (that approximating the charge densities) to
c     the representative exponential expansions needed in the big to small
c     far case.
c
c     input:
c     
c     nnodes is the number of terms in the exponential expansions
c
c     output:
c
c     the matrices are defined as direction, single or double, top or side.
c     so eastsngletop, would represent the map from the single layer
c     coefficients on the top of the box to the expansion going in the 
c     east direction.
c
c**************************************************************************
      subroutine genbtoslaymat8(eastsngletop, westsngletop,
     1    northsngletop, southsngletop, eastsngleside,
     2    westsngleside, northsngleside, southsngleside, 
     3    eastdbletop, westdbletop, northdbletop,
     4    southdbletop, eastdbleside, westdbleside, 
     5    northdbleside, southdbleside, nnodes)
      implicit none
c-----global variables
      integer *4  nnodes
      complex *16  eastsngletop(0:nnodes,0:7)
      complex *16  westsngletop(0:nnodes,0:7)
      complex *16  northsngletop(0:nnodes,0:7)
      complex *16  southsngletop(0:nnodes,0:7)
      complex *16  eastsngleside(0:nnodes,0:7)
      complex *16  westsngleside(0:nnodes,0:7)
      complex *16  northsngleside(0:nnodes,0:7)
      complex *16  southsngleside(0:nnodes,0:7)
      complex *16  eastdbletop(0:nnodes,0:7)
      complex *16  westdbletop(0:nnodes,0:7)
      complex *16  northdbletop(0:nnodes,0:7)
      complex *16  southdbletop(0:nnodes,0:7)
      complex *16  eastdbleside(0:nnodes,0:7)
      complex *16  westdbleside(0:nnodes,0:7)
      complex *16  northdbleside(0:nnodes,0:7)
      complex *16  southdbleside(0:nnodes,0:7)
c-----local variables
      integer *4  i, j
      real *8  wnodes(40), xnodes(40)
      real *8  temp1, temp2, temp5
      real *8  tx1d
      complex *16  temp3, temp4
      complex *16  ty1d
      complex *16  tempnorthtop, tempsouthtop
      complex *16  tempeasttop, tempwesttop
      complex *16  tempnorthside, tempsouthside
      complex *16  tempeastside, tempwestside
      complex *16  zsouthtop, znorthtop
      complex *16  zwesttop, zeasttop
      complex *16  zsouthside, znorthside
      complex *16  zwestside, zeastside
      complex *16  zshiftbig
      complex *16  imag
      data imag/(0.0d0,1.0d0)/

      call pwts4(xnodes,wnodes,nnodes)

      temp1 = -2.0d0 / 3.0d0
      temp2 = (16.0d0/5.0d0 - 16.0d0/3.0d0 + 2.0d0)
      temp5 = (64.0d0/7.0d0 - 96.0d0/5.0d0 + 10.0d0)
      eastsngletop(0,0) = 2.0d0
      eastsngletop(0,1) = 0.0d0
      eastsngletop(0,2) = temp1
      eastsngletop(0,3) = 0.0d0
      eastsngletop(0,4) = temp2
      eastsngletop(0,5) = 0.0d0
      eastsngletop(0,6) = temp5
      eastsngletop(0,7) = 0.0d0

      westsngletop(0,0) = 2.0d0
      westsngletop(0,1) = 0.0d0
      westsngletop(0,2) = temp1
      westsngletop(0,3) = 0.0d0
      westsngletop(0,4) = temp2
      westsngletop(0,5) = 0.0d0
      westsngletop(0,6) = temp5
      westsngletop(0,7) = 0.0d0

      northsngletop(0,0) = 2.0d0
      northsngletop(0,1) = 0.0d0
      northsngletop(0,2) = temp1
      northsngletop(0,3) = 0.0d0
      northsngletop(0,4) = temp2
      northsngletop(0,5) = 0.0d0
      northsngletop(0,6) = temp5
      northsngletop(0,7) = 0.0d0

      southsngletop(0,0) = 2.0d0
      southsngletop(0,1) = 0.0d0
      southsngletop(0,2) = temp1
      southsngletop(0,3) = 0.0d0
      southsngletop(0,4) = temp2
      southsngletop(0,5) = 0.0d0
      southsngletop(0,6) = temp5
      southsngletop(0,7) = 0.0d0

      eastsngleside(0,0) = 2.0d0
      eastsngleside(0,1) = 0.0d0
      eastsngleside(0,2) = temp1
      eastsngleside(0,3) = 0.0d0
      eastsngleside(0,4) = temp2
      eastsngleside(0,5) = 0.0d0
      eastsngleside(0,6) = temp5
      eastsngleside(0,7) = 0.0d0

      westsngleside(0,0) = 2.0d0
      westsngleside(0,1) = 0.0d0
      westsngleside(0,2) = temp1
      westsngleside(0,3) = 0.0d0
      westsngleside(0,4) = temp2
      westsngleside(0,5) = 0.0d0
      westsngleside(0,6) = temp5
      westsngleside(0,7) = 0.0d0

      northsngleside(0,0) = 2.0d0
      northsngleside(0,1) = 0.0d0
      northsngleside(0,2) = temp1
      northsngleside(0,3) = 0.0d0
      northsngleside(0,4) = temp2
      northsngleside(0,5) = 0.0d0
      northsngleside(0,6) = temp5
      northsngleside(0,7) = 0.0d0

      southsngleside(0,0) = 2.0d0
      southsngleside(0,1) = 0.0d0
      southsngleside(0,2) = temp1
      southsngleside(0,3) = 0.0d0
      southsngleside(0,4) = temp2
      southsngleside(0,5) = 0.0d0
      southsngleside(0,6) = temp5
      southsngleside(0,7) = 0.0d0

      eastdbletop(0,0) = 0.0d0
      eastdbletop(0,1) = 0.0d0
      eastdbletop(0,2) = 0.0d0 
      eastdbletop(0,3) = 0.0d0
      eastdbletop(0,4) = 0.0d0
      eastdbletop(0,5) = 0.0d0
      eastdbletop(0,6) = 0.0d0
      eastdbletop(0,7) = 0.0d0

      westdbletop(0,0) = 0.0d0
      westdbletop(0,1) = 0.0d0
      westdbletop(0,2) = 0.0d0
      westdbletop(0,3) = 0.0d0
      westdbletop(0,4) = 0.0d0
      westdbletop(0,5) = 0.0d0
      westdbletop(0,6) = 0.0d0
      westdbletop(0,7) = 0.0d0

      northdbletop(0,0) = 0.0d0
      northdbletop(0,1) = 0.0d0
      northdbletop(0,2) = 0.0d0 
      northdbletop(0,3) = 0.0d0
      northdbletop(0,4) = 0.0d0
      northdbletop(0,5) = 0.0d0
      northdbletop(0,6) = 0.0d0
      northdbletop(0,7) = 0.0d0

      southdbletop(0,0) = 0.0d0
      southdbletop(0,1) = 0.0d0
      southdbletop(0,2) = 0.0d0
      southdbletop(0,3) = 0.0d0
      southdbletop(0,4) = 0.0d0
      southdbletop(0,5) = 0.0d0
      southdbletop(0,6) = 0.0d0
      southdbletop(0,7) = 0.0d0

      eastdbleside(0,0) = 0.0d0
      eastdbleside(0,1) = 0.0d0
      eastdbleside(0,2) = 0.0d0
      eastdbleside(0,3) = 0.0d0
      eastdbleside(0,4) = 0.0d0
      eastdbleside(0,5) = 0.0d0
      eastdbleside(0,6) = 0.0d0
      eastdbleside(0,7) = 0.0d0

      westdbleside(0,0) = 0.0d0
      westdbleside(0,1) = 0.0d0
      westdbleside(0,2) = 0.0d0
      westdbleside(0,3) = 0.0d0
      westdbleside(0,4) = 0.0d0
      westdbleside(0,5) = 0.0d0
      westdbleside(0,6) = 0.0d0
      westdbleside(0,7) = 0.0d0

      northdbleside(0,0) = 0.0d0
      northdbleside(0,1) = 0.0d0
      northdbleside(0,2) = 0.0d0
      northdbleside(0,3) = 0.0d0
      northdbleside(0,4) = 0.0d0
      northdbleside(0,5) = 0.0d0
      northdbleside(0,6) = 0.0d0
      northdbleside(0,7) = 0.0d0

      southdbleside(0,0) = 0.0d0
      southdbleside(0,1) = 0.0d0
      southdbleside(0,2) = 0.0d0
      southdbleside(0,3) = 0.0d0
      southdbleside(0,4) = 0.0d0
      southdbleside(0,5) = 0.0d0
      southdbleside(0,6) = 0.0d0
      southdbleside(0,7) = 0.0d0

      zshiftbig =  -0.5d0 - imag * 1.5d0
      zeasttop  =      -zshiftbig
      zwesttop  =       zshiftbig
      znorthtop =  imag*zshiftbig
      zsouthtop = -imag*zshiftbig

      zshiftbig =  -1.5d0 - imag * 0.5d0
      zeastside  =      -zshiftbig
      zwestside  =       zshiftbig
      znorthside =  imag*zshiftbig
      zsouthside = -imag*zshiftbig

      do i = 1, nnodes
      temp1 =  wnodes(i) / xnodes(i)
      temp2 =  wnodes(i)
      temp3 =  temp2 * imag

      tempeasttop  = cdexp(zeasttop*xnodes(i))
      tempwesttop  = cdexp(zwesttop*xnodes(i))
      tempnorthtop = cdexp(znorthtop*xnodes(i))
      tempsouthtop = cdexp(zsouthtop*xnodes(i))

      tempeastside  = cdexp(zeastside*xnodes(i))
      tempwestside  = cdexp(zwestside*xnodes(i))
      tempnorthside = cdexp(znorthside*xnodes(i))
      tempsouthside = cdexp(zsouthside*xnodes(i))

       do j = 0, 7 

         call getintsx(xnodes(i), tx1d, j)

         temp4 = tx1d * tempeasttop
         eastsngletop(i,j) =  temp4 * temp1
         eastdbletop(i,j)  = -temp4 * temp3
             

         temp4 = tx1d * tempnorthside
         northsngleside(i,j) = temp4 * temp1
         northdbleside(i,j)  = temp4 * temp3



         call getintsx(-xnodes(i), tx1d, j)

         temp4 = tx1d * tempwesttop
         westsngletop(i,j) = temp4 * temp1
         westdbletop(i,j)  = temp4 * temp3

         temp4 = tx1d * tempsouthside
         southsngleside(i,j) =  temp4 * temp1
         southdbleside(i,j)  = -temp4 * temp3



         call getintsy(xnodes(i), ty1d, j)

         temp4 = ty1d * tempsouthtop
         southsngletop(i,j) =  temp4 * temp1
         southdbletop(i,j)  =  temp4 * temp2

         temp4 = ty1d * tempeastside
         eastsngleside(i,j) =  temp4 * temp1
         eastdbleside(i,j)  = -temp4 * temp2



         call getintsy(-xnodes(i), ty1d, j)

         temp4 = ty1d * tempnorthtop
         northsngletop(i,j) =  temp4 * temp1
         northdbletop(i,j)  = -temp4 * temp2

         temp4 = ty1d * tempwestside
         westsngleside(i,j) =  temp4 * temp1
         westdbleside(i,j)  =  temp4 * temp2

        end do
      end do
      return
      end


c**************************************************************************
c     the following subroutine precomputes the matrices that map from the
c     coefficients values, representing the right hand side of the poisson
c     equation to the representative exponential expansions needed in the
c     big to small far case.
c
c     input:
c
c     nnodes is the number of terms in the exponential expansions
c
c     output:
c
c     mapeast  is the map for east  going expansions
c
c     mapwest  is the map for west  going expansions
c
c     mapsouth is the map for south going expansions
c
c     mapnorth is the map for north going expansions
c
c**************************************************************************
      subroutine genbtosvolmat8(mapeast, mapwest, 
     1                mapsouth, mapnorth, nnodes)
      implicit none
c-----global variables
      integer *4  nnodes
      complex *16  mapeast(0:nnodes,36)
      complex *16  mapwest(0:nnodes,36)
      complex *16  mapnorth(0:nnodes,36)
      complex *16  mapsouth(0:nnodes,36)
c-----local variables
      integer *4  i
      real *8  pi2
      real *8  wnodes(40), xnodes(40)
      real *8  txint0, txint1
      real *8  txint2, txint3
      real *8  txint4, txint5
      real *8  txint6, txint7
      complex *16  tyint0, tyint1
      complex *16  tyint2, tyint3
      complex *16  tyint4, tyint5
      complex *16  tyint6, tyint7
      complex *16  zeast, zwest, znorth, zsouth
      complex *16  temp1, temp2
      complex *16  temp3, temp4
      complex *16  temp5, temp6
      complex *16  imag 
      data imag/(0.0d0,1.0d0)/

      pi2 = 2.0d0*dacos(-1.0d0)

      call pwts4(xnodes,wnodes,nnodes)
  
      zeast  =  0.5d0 + imag * 0.5d0
      zwest  = -0.5d0 - imag * 0.5d0
      znorth =  0.5d0 - imag * 0.5d0
      zsouth = -0.5d0 + imag * 0.5d0

      temp1 =  1.0d0 / pi2
      temp2 = -1.0d0 / (3.0d0*pi2)
      temp3 = -1.0d0 / (15.0d0*pi2)
      temp4 =  1.0d0 / (9.0d0*pi2)
      temp5 = -1.0d0 / (35.0d0*pi2)
      temp6 =  1.0d0 / (45.0d0*pi2)
      mapeast(0,1) = temp1
      mapeast(0,2) = 0.0d0
      mapeast(0,3) = 0.0d0
      mapeast(0,4) = temp2
      mapeast(0,5) = 0.0d0
      mapeast(0,6) = temp2
      mapeast(0,7) = 0.0d0
      mapeast(0,8) = 0.0d0
      mapeast(0,9) = 0.0d0
      mapeast(0,10)= 0.0d0
      mapeast(0,11)= temp3
      mapeast(0,12)= 0.0d0
      mapeast(0,13)= temp4
      mapeast(0,14)= 0.0d0
      mapeast(0,15)= temp3
      mapeast(0,16)= 0.0d0
      mapeast(0,17)= 0.0d0
      mapeast(0,18)= 0.0d0
      mapeast(0,19)= 0.0d0
      mapeast(0,20)= 0.0d0
      mapeast(0,21)= 0.0d0
      mapeast(0,22)= temp5
      mapeast(0,23)= 0.0d0
      mapeast(0,24)= temp6
      mapeast(0,25)= 0.0d0
      mapeast(0,26)= temp6
      mapeast(0,27)= 0.0d0
      mapeast(0,28)= temp5
      mapeast(0,29)= 0.0d0
      mapeast(0,30)= 0.0d0
      mapeast(0,31)= 0.0d0
      mapeast(0,32)= 0.0d0
      mapeast(0,33)= 0.0d0
      mapeast(0,34)= 0.0d0
      mapeast(0,35)= 0.0d0
      mapeast(0,36)= 0.0d0

      mapwest(0,1) = temp1
      mapwest(0,2) = 0.0d0
      mapwest(0,3) = 0.0d0
      mapwest(0,4) = temp2
      mapwest(0,5) = 0.0d0
      mapwest(0,6) = temp2
      mapwest(0,7) = 0.0d0
      mapwest(0,8) = 0.0d0
      mapwest(0,9) = 0.0d0
      mapwest(0,10)= 0.0d0
      mapwest(0,11)= temp3
      mapwest(0,12)= 0.0d0
      mapwest(0,13)= temp4
      mapwest(0,14)= 0.0d0
      mapwest(0,15)= temp3
      mapwest(0,16)= 0.0d0
      mapwest(0,17)= 0.0d0
      mapwest(0,18)= 0.0d0
      mapwest(0,19)= 0.0d0
      mapwest(0,20)= 0.0d0
      mapwest(0,21)= 0.0d0
      mapwest(0,22)= temp5
      mapwest(0,23)= 0.0d0
      mapwest(0,24)= temp6
      mapwest(0,25)= 0.0d0
      mapwest(0,26)= temp6
      mapwest(0,27)= 0.0d0
      mapwest(0,28)= temp5
      mapwest(0,29)= 0.0d0
      mapwest(0,30)= 0.0d0
      mapwest(0,31)= 0.0d0
      mapwest(0,32)= 0.0d0
      mapwest(0,33)= 0.0d0
      mapwest(0,34)= 0.0d0
      mapwest(0,35)= 0.0d0
      mapwest(0,36)= 0.0d0

      mapnorth(0,1) = temp1
      mapnorth(0,2) = 0.0d0
      mapnorth(0,3) = 0.0d0
      mapnorth(0,4) = temp2
      mapnorth(0,5) = 0.0d0
      mapnorth(0,6) = temp2
      mapnorth(0,7) = 0.0d0
      mapnorth(0,8) = 0.0d0
      mapnorth(0,9) = 0.0d0
      mapnorth(0,10)= 0.0d0
      mapnorth(0,11)= temp3
      mapnorth(0,12)= 0.0d0
      mapnorth(0,13)= temp4
      mapnorth(0,14)= 0.0d0
      mapnorth(0,15)= temp3
      mapnorth(0,16)= 0.0d0
      mapnorth(0,17)= 0.0d0
      mapnorth(0,18)= 0.0d0
      mapnorth(0,19)= 0.0d0
      mapnorth(0,20)= 0.0d0
      mapnorth(0,21)= 0.0d0
      mapnorth(0,22)= temp5
      mapnorth(0,23)= 0.0d0
      mapnorth(0,24)= temp6
      mapnorth(0,25)= 0.0d0
      mapnorth(0,26)= temp6
      mapnorth(0,27)= 0.0d0
      mapnorth(0,28)= temp5
      mapnorth(0,29)= 0.0d0
      mapnorth(0,30)= 0.0d0
      mapnorth(0,31)= 0.0d0
      mapnorth(0,32)= 0.0d0
      mapnorth(0,33)= 0.0d0
      mapnorth(0,34)= 0.0d0
      mapnorth(0,35)= 0.0d0
      mapnorth(0,36)= 0.0d0


      mapsouth(0,1) = temp1
      mapsouth(0,2) = 0.0d0
      mapsouth(0,3) = 0.0d0
      mapsouth(0,4) = temp2
      mapsouth(0,5) = 0.0d0
      mapsouth(0,6) = temp2
      mapsouth(0,7) = 0.0d0
      mapsouth(0,8) = 0.0d0
      mapsouth(0,9) = 0.0d0
      mapsouth(0,10)= 0.0d0
      mapsouth(0,11)= temp3
      mapsouth(0,12)= 0.0d0
      mapsouth(0,13)= temp4
      mapsouth(0,14)= 0.0d0
      mapsouth(0,15)= temp3
      mapsouth(0,16)= 0.0d0
      mapsouth(0,17)= 0.0d0
      mapsouth(0,18)= 0.0d0
      mapsouth(0,19)= 0.0d0
      mapsouth(0,20)= 0.0d0
      mapsouth(0,21)= 0.0d0
      mapsouth(0,22)= temp5
      mapsouth(0,23)= 0.0d0
      mapsouth(0,24)= temp6
      mapsouth(0,25)= 0.0d0
      mapsouth(0,26)= temp6
      mapsouth(0,27)= 0.0d0
      mapsouth(0,28)= temp5
      mapsouth(0,29)= 0.0d0
      mapsouth(0,30)= 0.0d0
      mapsouth(0,31)= 0.0d0
      mapsouth(0,32)= 0.0d0
      mapsouth(0,33)= 0.0d0
      mapsouth(0,34)= 0.0d0
      mapsouth(0,35)= 0.0d0
      mapsouth(0,36)= 0.0d0



      do i = 1, nnodes
        temp1 = wnodes(i) / (4.0d0*pi2 * xnodes(i)) 

         call getintsx(xnodes(i), txint0, 0)
         call getintsx(xnodes(i), txint1, 1)
         call getintsx(xnodes(i), txint2, 2)
         call getintsx(xnodes(i), txint3, 3)
         call getintsx(xnodes(i), txint4, 4)
         call getintsx(xnodes(i), txint5, 5)
         call getintsx(xnodes(i), txint6, 6)
         call getintsx(xnodes(i), txint7, 7)

         call getintsy(xnodes(i), tyint0, 0)
         call getintsy(xnodes(i), tyint1, 1)
         call getintsy(xnodes(i), tyint2, 2)
         call getintsy(xnodes(i), tyint3, 3)
         call getintsy(xnodes(i), tyint4, 4)
         call getintsy(xnodes(i), tyint5, 5)
         call getintsy(xnodes(i), tyint6, 6)
         call getintsy(xnodes(i), tyint7, 7)

        temp2 = temp1 * cdexp(zeast*xnodes(i)) 

         mapeast(i,1) = txint0 * tyint0 * temp2
         mapeast(i,2) = txint1 * tyint0 * temp2
         mapeast(i,3) = txint0 * tyint1 * temp2
         mapeast(i,4) = txint2 * tyint0 * temp2
         mapeast(i,5) = txint1 * tyint1 * temp2
         mapeast(i,6) = txint0 * tyint2 * temp2
         mapeast(i,7) = txint3 * tyint0 * temp2
         mapeast(i,8) = txint2 * tyint1 * temp2
         mapeast(i,9) = txint1 * tyint2 * temp2
         mapeast(i,10)= txint0 * tyint3 * temp2
         mapeast(i,11)= txint4 * tyint0 * temp2
         mapeast(i,12)= txint3 * tyint1 * temp2
         mapeast(i,13)= txint2 * tyint2 * temp2
         mapeast(i,14)= txint1 * tyint3 * temp2
         mapeast(i,15)= txint0 * tyint4 * temp2
         mapeast(i,16)= txint5 * tyint0 * temp2
         mapeast(i,17)= txint4 * tyint1 * temp2
         mapeast(i,18)= txint3 * tyint2 * temp2
         mapeast(i,19)= txint2 * tyint3 * temp2
         mapeast(i,20)= txint1 * tyint4 * temp2
         mapeast(i,21)= txint0 * tyint5 * temp2
         mapeast(i,22)= txint6 * tyint0 * temp2
         mapeast(i,23)= txint5 * tyint1 * temp2
         mapeast(i,24)= txint4 * tyint2 * temp2
         mapeast(i,25)= txint3 * tyint3 * temp2
         mapeast(i,26)= txint2 * tyint4 * temp2
         mapeast(i,27)= txint1 * tyint5 * temp2
         mapeast(i,28)= txint0 * tyint6 * temp2
         mapeast(i,29)= txint7 * tyint0 * temp2
         mapeast(i,30)= txint6 * tyint1 * temp2
         mapeast(i,31)= txint5 * tyint2 * temp2
         mapeast(i,32)= txint4 * tyint3 * temp2
         mapeast(i,33)= txint3 * tyint4 * temp2
         mapeast(i,34)= txint2 * tyint5 * temp2
         mapeast(i,35)= txint1 * tyint6 * temp2
         mapeast(i,36)= txint0 * tyint7 * temp2


        temp2 = temp1 * cdexp(zwest*xnodes(i))

         mapwest(i,1) =  txint0 * tyint0 * temp2
         mapwest(i,2) = -txint1 * tyint0 * temp2
         mapwest(i,3) = -txint0 * tyint1 * temp2
         mapwest(i,4) =  txint2 * tyint0 * temp2
         mapwest(i,5) =  txint1 * tyint1 * temp2
         mapwest(i,6) =  txint0 * tyint2 * temp2
         mapwest(i,7) = -txint3 * tyint0 * temp2
         mapwest(i,8) = -txint2 * tyint1 * temp2
         mapwest(i,9) = -txint1 * tyint2 * temp2
         mapwest(i,10)= -txint0 * tyint3 * temp2
         mapwest(i,11)=  txint4 * tyint0 * temp2
         mapwest(i,12)=  txint3 * tyint1 * temp2
         mapwest(i,13)=  txint2 * tyint2 * temp2
         mapwest(i,14)=  txint1 * tyint3 * temp2
         mapwest(i,15)=  txint0 * tyint4 * temp2
         mapwest(i,16)= -txint5 * tyint0 * temp2
         mapwest(i,17)= -txint4 * tyint1 * temp2
         mapwest(i,18)= -txint3 * tyint2 * temp2
         mapwest(i,19)= -txint2 * tyint3 * temp2
         mapwest(i,20)= -txint1 * tyint4 * temp2
         mapwest(i,21)= -txint0 * tyint5 * temp2
         mapwest(i,22)=  txint6 * tyint0 * temp2
         mapwest(i,23)=  txint5 * tyint1 * temp2
         mapwest(i,24)=  txint4 * tyint2 * temp2
         mapwest(i,25)=  txint3 * tyint3 * temp2
         mapwest(i,26)=  txint2 * tyint4 * temp2
         mapwest(i,27)=  txint1 * tyint5 * temp2
         mapwest(i,28)=  txint0 * tyint6 * temp2
         mapwest(i,29)= -txint7 * tyint0 * temp2
         mapwest(i,30)= -txint6 * tyint1 * temp2
         mapwest(i,31)= -txint5 * tyint2 * temp2
         mapwest(i,32)= -txint4 * tyint3 * temp2
         mapwest(i,33)= -txint3 * tyint4 * temp2
         mapwest(i,34)= -txint2 * tyint5 * temp2
         mapwest(i,35)= -txint1 * tyint6 * temp2
         mapwest(i,36)= -txint0 * tyint7 * temp2


        temp2 = temp1 * cdexp(znorth*xnodes(i)) 

         mapnorth(i,1) =  txint0 * tyint0 * temp2
         mapnorth(i,2) = -txint0 * tyint1 * temp2
         mapnorth(i,3) =  txint1 * tyint0 * temp2
         mapnorth(i,4) =  txint0 * tyint2 * temp2
         mapnorth(i,5) = -txint1 * tyint1 * temp2
         mapnorth(i,6) =  txint2 * tyint0 * temp2
         mapnorth(i,7) = -txint0 * tyint3 * temp2
         mapnorth(i,8) =  txint1 * tyint2 * temp2
         mapnorth(i,9) = -txint2 * tyint1 * temp2
         mapnorth(i,10)=  txint3 * tyint0 * temp2
         mapnorth(i,11)=  txint0 * tyint4 * temp2
         mapnorth(i,12)= -txint1 * tyint3 * temp2
         mapnorth(i,13)=  txint2 * tyint2 * temp2
         mapnorth(i,14)= -txint3 * tyint1 * temp2
         mapnorth(i,15)=  txint4 * tyint0 * temp2
         mapnorth(i,16)= -txint0 * tyint5 * temp2
         mapnorth(i,17)=  txint1 * tyint4 * temp2
         mapnorth(i,18)= -txint2 * tyint3 * temp2
         mapnorth(i,19)=  txint3 * tyint2 * temp2
         mapnorth(i,20)= -txint4 * tyint1 * temp2
         mapnorth(i,21)=  txint5 * tyint0 * temp2
         mapnorth(i,22)=  txint0 * tyint6 * temp2
         mapnorth(i,23)= -txint1 * tyint5 * temp2
         mapnorth(i,24)=  txint2 * tyint4 * temp2
         mapnorth(i,25)= -txint3 * tyint3 * temp2
         mapnorth(i,26)=  txint4 * tyint2 * temp2
         mapnorth(i,27)= -txint5 * tyint1 * temp2
         mapnorth(i,28)=  txint6 * tyint0 * temp2
         mapnorth(i,29)= -txint0 * tyint7 * temp2
         mapnorth(i,30)=  txint1 * tyint6 * temp2
         mapnorth(i,31)= -txint2 * tyint5 * temp2
         mapnorth(i,32)=  txint3 * tyint4 * temp2
         mapnorth(i,33)= -txint4 * tyint3 * temp2
         mapnorth(i,34)=  txint5 * tyint2 * temp2
         mapnorth(i,35)= -txint6 * tyint1 * temp2
         mapnorth(i,36)=  txint7 * tyint0 * temp2


        temp2 = temp1 * cdexp(zsouth*xnodes(i))

         mapsouth(i,1) =  txint0 * tyint0 * temp2
         mapsouth(i,2) =  txint0 * tyint1 * temp2
         mapsouth(i,3) = -txint1 * tyint0 * temp2
         mapsouth(i,4) =  txint0 * tyint2 * temp2
         mapsouth(i,5) = -txint1 * tyint1 * temp2
         mapsouth(i,6) =  txint2 * tyint0 * temp2
         mapsouth(i,7) =  txint0 * tyint3 * temp2
         mapsouth(i,8) = -txint1 * tyint2 * temp2
         mapsouth(i,9) =  txint2 * tyint1 * temp2
         mapsouth(i,10)= -txint3 * tyint0 * temp2
         mapsouth(i,11)=  txint0 * tyint4 * temp2
         mapsouth(i,12)= -txint1 * tyint3 * temp2
         mapsouth(i,13)=  txint2 * tyint2 * temp2
         mapsouth(i,14)= -txint3 * tyint1 * temp2
         mapsouth(i,15)=  txint4 * tyint0 * temp2
         mapsouth(i,16)=  txint0 * tyint5 * temp2
         mapsouth(i,17)= -txint1 * tyint4 * temp2
         mapsouth(i,18)=  txint2 * tyint3 * temp2
         mapsouth(i,19)= -txint3 * tyint2 * temp2
         mapsouth(i,20)=  txint4 * tyint1 * temp2
         mapsouth(i,21)= -txint5 * tyint0 * temp2
         mapsouth(i,22)=  txint0 * tyint6 * temp2
         mapsouth(i,23)= -txint1 * tyint5 * temp2
         mapsouth(i,24)=  txint2 * tyint4 * temp2
         mapsouth(i,25)= -txint3 * tyint3 * temp2
         mapsouth(i,26)=  txint4 * tyint2 * temp2
         mapsouth(i,27)= -txint5 * tyint1 * temp2
         mapsouth(i,28)=  txint6 * tyint0 * temp2
         mapsouth(i,29)=  txint0 * tyint7 * temp2
         mapsouth(i,30)= -txint1 * tyint6 * temp2
         mapsouth(i,31)=  txint2 * tyint5 * temp2
         mapsouth(i,32)= -txint3 * tyint4 * temp2
         mapsouth(i,33)=  txint4 * tyint3 * temp2
         mapsouth(i,34)= -txint5 * tyint2 * temp2
         mapsouth(i,35)=  txint6 * tyint1 * temp2
         mapsouth(i,36)= -txint7 * tyint0 * temp2

      end do
      return
      end


c**************************************************************************
c     the following subroutine computes an integral needed when
c     forming the big to small maps for the volume part
c
c     input:
c
c     xnode is the value of xnodes(i) here
c
c     xpow is as power that x/2 is raised to
c
c     output:
c
c     xint is set equal to int -1 < x < 1 (x/2)^xpow * exp(xnode*x) dx
c     note that xint must be real here
c
c**************************************************************************
      subroutine getintsx(xnode, xint, xpow)
      implicit none
c-----global variables
      integer *4  xpow
      real *8  xnode
      real *8  xint
c-----local variables
      integer *4  kpts, kind, m
      real *8  alpha, beta, endpts(2), b(60), cheb
      real *8  t(60), wnode(60)

      alpha = 0.0d0
      beta = 0.0d0
      kpts = 0
      endpts(1) = 0.0d0
      endpts(2) = 0.0d0
      kind = 1

      call gaussq(kind, 60, alpha, beta, kpts, endpts, b, t, wnode)

        xint = 0.0d0
        do m = 1,60
         xint = xint + wnode(m) * cheb(t(m),xpow) * dexp(xnode*t(m))
       end do
      return
      end



c**************************************************************************
c     the following subroutine computes an integral needed when
c     forming the big to small maps for the volume part
c
c     input:
c
c     xnode is the value of xnodes(i) here
c
c     ypow is as power that x/2 is raised to
c
c     output:
c
c     xint is set equal to int -1 < x < 1 (x/2)^ypow * exp(i*xnode*x) dx
c     note that xint can be complex here
c
c**************************************************************************
      subroutine getintsy(xnode, xint, ypow)
      implicit none
c-----global variables
      integer *4  ypow
      real *8  xnode
      complex *16  xint
c-----local variables
      integer *4  kpts, kind, l
      real *8  alpha, beta, endpts(2), b(30), cheb
      real *8  t(30), wnode(30)
      complex *16  imag
      data imag/(0.0,1.0)/

      alpha = 0.0d0
      beta = 0.0d0
      kpts = 0
      endpts(1) = 0.0d0
      endpts(2) = 0.0d0
      kind = 1

      call gaussq(kind, 30, alpha, beta, kpts, endpts, b, t, wnode)

        xint = 0.0d0
        do l = 1,30
           xint = xint + wnode(l) * cheb(t(l),ypow) 
     1                            * cdexp(xnode*imag*t(l))
        end do
      return
      end


c**************************************************************************
c     this subroutine generates four exponential expansions given
c     one multipole expansion as input.
c
c     input:
c
c     nnodes is the order of the plane wave expansions
c
c     mpole is the array or multipole coefficients
c
c     nterms is the order of the multipole expansions
c
c     comp is a precomputed term involving the ratio of
c          the weights and factorial terms
c
c     wnodes is the array of exponential weights
c
c     xnodes is the array of exponential nodes
c
c     sum is a precomputed real term
c
c     temp(i) is set equal to wnodes(i) / xnodes(i)
c
c     output:
c
c     betae is set to the east exponential coefficients
c
c     betaw is set to the west exponential coefficients
c
c     betan is set to the north exponential coefficients
c
c     betas is set to the south exponential coefficients
c
c**************************************************************************
      subroutine expcoeff(betae, betaw, betan, betas,
     1                    nnodes, mpole, nterms, comp,
     2                    wnodes, sum, temp)
      implicit none
c-----global variables
      integer *4  nnodes, nterms
      real *8  comp(nnodes,0:nterms)
      real *8  wnodes(nnodes)
      real *8  temp(nnodes)
      real *8  sum
      complex *16  betae(0:nnodes), betaw(0:nnodes)
      complex *16  betan(0:nnodes), betas(0:nnodes)
      complex *16  mpole(0:nterms)
c-----local variables
      integer *4  i, j
      complex *16  imag

      complex *16 sum1, sum2, sum5, sum6
      complex *16 sum7, sum8
      complex *16 temp1

      data imag/(0.0d0, 1.0d0)/

      temp1 = mpole(0)*sum

      betae(0) = temp1
      betaw(0) = temp1
      betas(0) = temp1
      betan(0) = temp1

      do i = 1, nnodes

         sum1 = 0.0d0
         sum2 = 0.0d0
         sum5 = 0.0d0
         sum6 = 0.0d0

         do j = 1, nterms-3, 4
            sum1 = sum1 - comp(i,j)   * mpole(j+1)
            sum2 = sum2 - comp(i,j-1) * mpole(j)
            sum5 = sum5 - comp(i,j+1) * mpole(j+2)
            sum6 = sum6 - comp(i,j+2) * mpole(j+3)
         end do

         sum8 = imag*(sum2 - sum5)
         sum7 = sum6 - sum1
         sum1 = sum1 + sum6
         sum2 = sum2 + sum5
         betae(i) = wnodes(i) * (sum1 + sum2)
         betaw(i) = wnodes(i) * (sum1 - sum2)
         betas(i) = wnodes(i) * (sum7 + sum8)
         betan(i) = wnodes(i) * (sum7 - sum8)

      end do

c     now let's readjust the east coefficients to account for the
c     difference in the log term
      do i = 1, nnodes
       temp1 =  mpole(0)*temp(i)
       betae(i) = betae(i) + temp1
       betaw(i) = betaw(i) + temp1
       betas(i) = betas(i) + temp1
       betan(i) = betan(i) + temp1
      end do
      return
      end


      subroutine gaussq(kind, n, alpha, beta, kpts, endpts, b, t, w)
c
c           this set of routines computes the nodes t(j) and weights
c        w(j) for gaussian-type quadrature rules with pre-assigned
c        nodes.  these are used when one wishes to approximate
c
c                 integral (from a to b)  f(x) w(x) dx
c
c                              n
c        by                   sum w  f(t )
c                             j=1  j    j
c
c        (note w(x) and w(j) have no connection with each other.)
c        here w(x) is one of six possible non-negative weight
c        functions (listed below), and f(x) is the
c        function to be integrated.  gaussian quadrature is particularly
c        useful on infinite intervals (with appropriate weight
c        functions), since then other techniques often fail.
c
c           associated with each weight function w(x) is a set of
c        orthogonal polynomials.  the nodes t(j) are just the zeroes
c        of the proper n-th degree polynomial.
c
c     input parameters (all real numbers are in real*8)
c
c        kind     an integer between 1 and 6 giving the type of
c                 quadrature rule:
c
c        kind = 1:  legendre quadrature, w(x) = 1 on (-1, 1)
c        kind = 2:  chebyshev quadrature of the first kind
c                   w(x) = 1/sqrt(1 - x*x) on (-1, +1)
c        kind = 3:  chebyshev quadrature of the second kind
c                   w(x) = sqrt(1 - x*x) on (-1, 1)
c        kind = 4:  hermite quadrature, w(x) = exp(-x*x) on
c                   (-infinity, +infinity)
c        kind = 5:  jacobi quadrature, w(x) = (1-x)**alpha * (1+x)**
c                   beta on (-1, 1), alpha, beta .gt. -1.
c                   note: kind=2 and 3 are a special case of this.
c        kind = 6:  generalized laguerre quadrature, w(x) = exp(-x)*
c                   x**alpha on (0, +infinity), alpha .gt. -1
c
c        n        the number of points used for the quadrature rule
c        alpha    real parameter used only for gauss-jacobi and gauss-
c                 laguerre quadrature (otherwise use 0.d0).
c        beta     real parameter used only for gauss-jacobi quadrature--
c                 (otherwise use 0.d0)
c        kpts     (integer) normally 0, unless the left or right end-
c                 point (or both) of the interval is required to be a
c                 node (this is called gauss-radau or gauss-lobatto
c                 quadrature).  then kpts is the number of fixed
c                 endpoints (1 or 2).
c        endpts   real array of length 2.  contains the values of
c                 any fixed endpoints, if kpts = 1 or 2.
c        b        real scratch array of length n
c
c     output parameters (both real*8 arrays of length n)
c
c        t        will contain the desired nodes.
c        w        will contain the desired weights w(j).
c
c     subroutines required
c
c        solve, class, and imtql2 are provided.  underflow may sometimes
c        occur, but it is harmless if the underflow interrupts are
c        turned off.  to do this, the first call of the main program
c        should be
c                  call traps (0, 0, 10000, 0, 0)    in watfiv
c        or
c                  call init                         in fortran g or h.
c
c     accuracy
c
c        the routine was tested up to n = 512 for legendre quadrature,
c        up to n = 136 for hermite, up to n = 68 for laguerre, and up
c        to n = 10 or 20 in other cases.  in all but two instances,
c        comparison with tables in ref. 3 showed 12 or more significant
c        digits of accuracy.  the two exceptions were the weights for
c        hermite and laguerre quadrature, where underflow caused some
c        very small weights to be set to zero.  this is, of course,
c        completely harmless.
c
c     method
c
c           the coefficients of the three-term recurrence relation
c        for the corresponding set of orthogonal polynomials are
c        used to form a symmetric tridiagonal matrix, whose
c        eigenvalues (determined by the implicit ql-method with
c        shifts) are just the desired nodes.  the first components of
c        the orthonormalized eigenvectors, when properly scaled,
c        yield the weights.  this technique is much faster than using a
c        root-finder to locate the zeroes of the orthogonal polynomial.
c        for further details, see ref. 1.  ref. 2 contains details of
c        gauss-radau and gauss-lobatto quadrature only.
c
c     references
c
c        1.  golub, g. h., and welsch, j. h., "calculation of gaussian
c            quadrature rules," mathematics of computation 23 (april,
c            1969), pp. 221-230.
c        2.  golub, g. h., "some modified matrix eigenvalue problems,"
c            siam review 15 (april, 1973), pp. 318-334 (section 7).
c        3.  stroud and secrest, gaussian quadrature formulas, prentice-
c            hall, englewood cliffs, n.j., 1966.
c
c     ..................................................................
c
      real *8 b(n), t(n), w(n), endpts(2), muzero, t1,
     x gam, solve, dsqrt, alpha, beta
c
      call class (kind, n, alpha, beta, b, t, muzero)
c
c           the matrix of coefficients is assumed to be symmetric.
c           the array t contains the diagonal elements, the array
c           b the off-diagonal elements.
c           make appropriate changes in the lower right 2 by 2
c           submatrix.
c
      if (kpts.eq.0)  go to 100
      if (kpts.eq.2)  go to  50
c
c           if kpts=1, only t(n) must be changed
c
      t(n) = solve(endpts(1), n, t, b)*b(n-1)**2 + endpts(1)
      go to 100
c
c           if kpts=2, t(n) and b(n-1) must be recomputed
c
   50 gam = solve(endpts(1), n, t, b)
      t1 = ((endpts(1) - endpts(2))/(solve(endpts(2), n, t, b) - gam))
      b(n-1) = dsqrt(t1)
      t(n) = endpts(1) + gam*t1
c
c           note that the indices of the elements of b run from 1 to n-1
c           and thus the value of b(n) is arbitrary.
c           now compute the eigenvalues of the symmetric tridiagonal
c           matrix, which has been modified as necessary.
c           the method used is a ql-type method with origin shifting
c
  100 w(1) = 1.0d0
      do 105 i = 2, n
  105    w(i) = 0.0d0
c
      call imtql2 (n, t, b, w, ierr)
      do 110 i = 1, n
  110    w(i) = muzero * w(i) * w(i)
c
      return
      end
c
c
c
      real*8 function solve(shift, n, a, b)
c
c       this procedure performs elimination to solve for the
c       n-th component of the solution delta to the equation
c
c             (jn - shift*identity) * delta  = en,
c
c       where en is the vector of all zeroes except for 1 in
c       the n-th position.
c
c       the matrix jn is symmetric tridiagonal, with diagonal
c       elements a(i), off-diagonal elements b(i).  this equation
c       must be solved to obtain the appropriate changes in the lower
c       2 by 2 submatrix of coefficients for orthogonal polynomials.
c
c
      real*8 shift, a(n), b(n), alpha
c
      alpha = a(1) - shift
      nm1 = n - 1
      do 10 i = 2, nm1
   10    alpha = a(i) - shift - b(i-1)**2/alpha
      solve = 1.0d0/alpha
      return
      end
c
c
c
      subroutine class(kind, n, alpha, beta, b, a, muzero)
c
c           this procedure supplies the coefficients a(j), b(j) of the
c        recurrence relation
c
c             b p (x) = (x - a ) p   (x) - b   p   (x)
c              j j            j   j-1       j-1 j-2
c
c        for the various classical (normalized) orthogonal polynomials,
c        and the zero-th moment
c
c             muzero = integral w(x) dx
c
c        of the given polynomial's weight function w(x).  since the
c        polynomials are orthonormalized, the tridiagonal matrix is
c        guaranteed to be symmetric.
c
c           the input parameter alpha is used only for laguerre and
c        jacobi polynomials, and the parameter beta is used only for
c        jacobi polynomials.  the laguerre and jacobi polynomials
c        require the gamma function.
c
c     ..................................................................
c
      real*8 a(n), b(n), muzero, alpha, beta
      real*8 abi, a2b2, pi, dsqrt, ab
      data pi / 3.141592653589793d0/
c
      nm1 = n - 1
      go to (10, 20, 30, 40, 50, 60), kind
c
c              kind = 1:  legendre polynomials p(x)
c              on (-1, +1), w(x) = 1.
c
   10 muzero = 2.0d0
      do 11 i = 1, nm1
         a(i) = 0.0d0
         abi = i
   11    b(i) = abi/dsqrt(4*abi*abi - 1.0d0)
      a(n) = 0.0d0
      return
c
c              kind = 2:  chebyshev polynomials of the first kind t(x)
c              on (-1, +1), w(x) = 1 / sqrt(1 - x*x)
c
   20 muzero = pi
      do 21 i = 1, nm1
         a(i) = 0.0d0
   21    b(i) = 0.5d0
      b(1) = dsqrt(0.5d0)
      a(n) = 0.0d0
      return
c
c              kind = 3:  chebyshev polynomials of the second kind u(x)
c              on (-1, +1), w(x) = sqrt(1 - x*x)
c
   30 muzero = pi/2.0d0
      do 31 i = 1, nm1
         a(i) = 0.0d0
   31    b(i) = 0.5d0
      a(n) = 0.0d0
      return
c
c              kind = 4:  hermite polynomials h(x) on (-infinity,
c              +infinity), w(x) = exp(-x**2)
c
   40 muzero = dsqrt(pi)
      do 41 i = 1, nm1
         a(i) = 0.0d0
   41    b(i) = dsqrt(i/2.0d0)
      a(n) = 0.0d0
      return
c
c              kind = 5:  jacobi polynomials p(alpha, beta)(x) on
c              (-1, +1), w(x) = (1-x)**alpha + (1+x)**beta, alpha and
c              beta greater than -1
c
   50 ab = alpha + beta
      abi = 2.0d0 + ab
      muzero = 2.0d0 ** (ab + 1.0d0)
      a(1) = (beta - alpha)/abi
      b(1) = dsqrt(4.0d0*(1.0d0 + alpha)*(1.0d0 + beta)/((abi + 1.0d0)*
     1  abi*abi))
      a2b2 = beta*beta - alpha*alpha
      do 51 i = 2, nm1
         abi = 2.0d0*i + ab
         a(i) = a2b2/((abi - 2.0d0)*abi)
   51    b(i) = dsqrt (4.0d0*i*(i + alpha)*(i + beta)*(i + ab)/
     1   ((abi*abi - 1)*abi*abi))
      abi = 2.0d0*n + ab
      a(n) = a2b2/((abi - 2.0d0)*abi)
      return
c
c              kind = 6:  laguerre polynomials l(alpha)(x) on
c              (0, +infinity), w(x) = exp(-x) * x**alpha, alpha greater
c              than -1.
c
   60 muzero = 1.0d0
      do 61 i = 1, nm1
         a(i) = 2.0d0*i - 1.0d0 + alpha
   61    b(i) = dsqrt(i*(i + alpha))
      a(n) = 2.0d0*n - 1 + alpha
      return
      end
 
 
      subroutine imtql2(n, d, e, z, ierr)
c
c     this subroutine is a translation of the algol procedure imtql2,
c     num. math. 12, 377-383(1968) by martin and wilkinson,
c     as modified in num. math. 15, 450(1970) by dubrulle.
c     handbook for auto. comp., vol.ii-linear algebra, 241-248(1971).
c     this is a modified version of the 'eispack' routine imtql2.
c
c     this subroutine finds the eigenvalues and first components of the
c     eigenvectors of a symmetric tridiagonal matrix by the implicit ql
c     method.
c
c     on input:
c
c        n is the order of the matrix;
c
c        d contains the diagonal elements of the input matrix;
c
c        e contains the subdiagonal elements of the input matrix
c          in its first n-1 positions.  e(n) is arbitrary;
c
c        z contains the first row of the identity matrix.
c
c      on output:
c
c        d contains the eigenvalues in ascending order.  if an
c          error exit is made, the eigenvalues are correct but
c          unordered for indices 1, 2, ..., ierr-1;
c
c        e has been destroyed;
c
c        z contains the first components of the orthonormal eigenvectors
c          of the symmetric tridiagonal matrix.  if an error exit is
c          made, z contains the eigenvectors associated with the stored
c          eigenvalues;
c
c        ierr is set to
c          zero       for normal return,
c          j          if the j-th eigenvalue has not been
c                     determined after 30 iterations.
c
c     ------------------------------------------------------------------
c
      integer i, j, k, l, m, n, ii, mml, ierr
      real*8 d(n), e(n), z(n), b, c, f, g, p, r, s, machep
      real*8 dsqrt, dabs, dsign
c
c     :::::::::: machep is a machine dependent parameter specifying
c                the relative precision of floating point arithmetic.
c                machep = 16.0d0**(-13) for long form arithmetic
c                on s360 ::::::::::
cccc  data machep/z3410000000000000/
      data machep/1.0d-14/
c
      ierr = 0
      if (n .eq. 1) go to 1001
c
      e(n) = 0.0d0
      do 240 l = 1, n
         j = 0
c     :::::::::: look for small sub-diagonal element ::::::::::
  105    do 110 m = l, n
            if (m .eq. n) go to 120
            if (dabs(e(m)) .le. machep * (dabs(d(m)) + dabs(d(m+1))))
     x         go to 120
  110    continue
c
  120    p = d(l)
         if (m .eq. l) go to 240
         if (j .eq. 30) go to 1000
         j = j + 1
c     :::::::::: form shift ::::::::::
         g = (d(l+1) - p) / (2.0d0 * e(l))
         r = dsqrt(g*g+1.0d0)
         g = d(m) - p + e(l) / (g + dsign(r, g))
         s = 1.0d0
         c = 1.0d0
         p = 0.0d0
         mml = m - l
c
c     :::::::::: for i=m-1 step -1 until l do -- ::::::::::
         do 200 ii = 1, mml
            i = m - ii
            f = s * e(i)
            b = c * e(i)
            if (dabs(f) .lt. dabs(g)) go to 150
            c = g / f
            r = dsqrt(c*c+1.0d0)
            e(i+1) = f * r
            s = 1.0d0 / r
            c = c * s
            go to 160
  150       s = f / g
            r = dsqrt(s*s+1.0d0)
            e(i+1) = g * r
            c = 1.0d0 / r
            s = s * c
  160       g = d(i+1) - p
            r = (d(i) - g) * s + 2.0d0 * c * b
            p = s * r
            d(i+1) = g + p
            g = c * r - b
c     :::::::::: form first component of vector ::::::::::
            f = z(i+1)
            z(i+1) = s * z(i) + c * f
  200       z(i) = c * z(i) - s * f
c
         d(l) = d(l) - p
         e(l) = g
         e(m) = 0.0d0
         go to 105
  240 continue
c
c     :::::::::: order eigenvalues and eigenvectors ::::::::::
      do 300 ii = 2, n
         i = ii - 1
         k = i
         p = d(i)
c
         do 260 j = ii, n
            if (d(j) .ge. p) go to 260
            k = j
            p = d(j)
  260    continue
c
         if (k .eq. i) go to 300
         d(k) = d(i)
         d(i) = p
         p = z(i)
         z(i) = z(k)
         z(k) = p
  300 continue
c
      go to 1001
c     :::::::::: set error -- no convergence to an
c                eigenvalue after 30 iterations ::::::::::
 1000 ierr = l
 1001 return
      end






c**************************************************************************
c--------------------------------------------------------------------------
c     core FMM module
c--------------------------------------------------------------------------
c**************************************************************************
c     the following subroutine defines the array that represents the 
c     right hand side of the poisson equation. 
c     in each childless box, there are 16 cell centered points 
c     where the right hand side values (and later
c     the output values) are defined.  
c
c     input:
c
c     levelbox is an array determining the level of each box
c
c     nboxes is the total number of boxes
c
c     icolbox denotes the column of each box
c
c     irowbox denotes the row of each box
c
c     ichildbox denotes the four children of each box
c
c     nlev is the finest level
c
c     iboxlev is the array in which the boxes are arranged
c
c     nblevel is the total number of boxes per level
c
c     istartlev is the pointer to where each level begins in the
c               iboxlev array
c
c     h is the real function that is the right hand side
c       of the poisson equation
c
c     output:
c
c     fright is the right hand side defined on the tree
c
c***********************************************************************
      subroutine setf8(fright,
     1       icolbox, irowbox, ichildbox,nlev,
     2       nblevel, iboxlev, istartlev, h,t)
      implicit none
c-----global variables
      integer *4  icolbox(1), irowbox(1), nlev
      integer *4  ichildbox(4,1)
      integer *4  nblevel(0:1), iboxlev(1), istartlev(0:1)
      real *8  fright(64,1), xf(64), yf(64)
c-----local variables
      integer *4  i, ibox, j, k, l
      real *8  temp1, pi16
      real *8  xstart
      real *8  xshift, xx(8)
      real *8  xscale(8)
      real *8  yshift
c-----external functions
      real *8  h
      real *8  t

      pi16 = dacos(-1.0d0) / 16.0d0
      xx(1) = dcos(15.0d0*pi16) / 2.0d0
      xx(2) = dcos(13.0d0*pi16) / 2.0d0
      xx(3) = dcos(11.0d0*pi16) / 2.0d0
      xx(4) = dcos( 9.0d0*pi16) / 2.0d0
      xx(5) = dcos( 7.0d0*pi16) / 2.0d0
      xx(6) = dcos( 5.0d0*pi16) / 2.0d0
      xx(7) = dcos( 3.0d0*pi16) / 2.0d0
      xx(8) = dcos( 1.0d0*pi16) / 2.0d0

      temp1 = 1.0d0
      do k = 0, nlev
      xstart = (1.0d0 - temp1) / 2.0d0

      xscale(1) = xx(1) * temp1 - xstart
      xscale(2) = xx(2) * temp1 - xstart
      xscale(3) = xx(3) * temp1 - xstart
      xscale(4) = xx(4) * temp1 - xstart
      xscale(5) = xx(5) * temp1 - xstart
      xscale(6) = xx(6) * temp1 - xstart
      xscale(7) = xx(7) * temp1 - xstart
      xscale(8) = xx(8) * temp1 - xstart


      do 100 i = istartlev(k), istartlev(k) + nblevel(k) - 1
        ibox = iboxlev(i)
        if(ichildbox(1,ibox) .gt. 0)goto 100

        xshift  =  dble(icolbox(ibox) - 1) * temp1
        yshift  =  dble(irowbox(ibox) - 1) * temp1

        do j = 1, 8
          do l = 1, 8
            xf(8*(l-1)+j) = xscale(j) + xshift
            yf(8*(j-1)+l) = xscale(j) + yshift
          end do
        end do
       
        do j = 1, 64
          fright(j,ibox) = h(xf(j),yf(j),t)
        end do
100   continue
      temp1 = temp1 / 2.0d0
      end do
      return
      end



c***********************************************************************
c     the following subroutine defines the array coeffs that contains
c     the polynomial coefficients for the polynomial that approximates
c     the right hand side of the poisson equation.
c
c     input:
c
c     fright is the right hand side defined on the old grid
c
c     levelbox is an array determining the level of each box
c
c     nboxes is the total number of boxes
c
c     nlev is the finest level
c
c     ichildbox denotes the four children of each box
c
c     nblevel is the total number of boxes per level
c
c     iboxlev is the array in which the boxes are arranged
c
c     istartlev is the pointer to where each level begins
c               in the iboxlev array
c
c     iperiod denotes which of the boundary cases we are in
c
c     a is the matrix that maps from 36 function
c          values to 21 polynomial coefficients
c
c     output:
c  
c     coeffs is the array of coefficients for the basis functions
c
c***********************************************************************
      subroutine mkcoeffs8(coeffs,fright,
     1         nlev, ichildbox, nblevel, 
     2         iboxlev, istartlev)
      implicit none
c-----global variables
      integer *4  nlev
      integer *4  ichildbox(4,1)
      integer *4  nblevel(0:1), iboxlev(1), istartlev(0:1)
      real *8  coeffs(0:7,0:7,1), fright(64,1)
c-----local variables
      integer *4  i, ibox, j, l, ii
      real *8  ftemp(8,8)
      real *8  coefftemp(8,8)
      real *8  wsave2(1000)

      do l = 0, nlev
       do i = istartlev(l), istartlev(l) + nblevel(l) - 1
       ibox = iboxlev(i)
        if (ichildbox(1,ibox) .lt.0)then


c       initialize wsave2 array (needed by getcoeff).
        call chxcin(8,wsave2)

        do j = 1, 8
          do ii = 1, 8
            ftemp(j,ii) = fright(8*(ii-1)+j,ibox)
          end do
        end do


c       compute chebyshev transforms
        call getcoeff(8,8,ftemp,coefftemp,wsave2)

c       now set these values to out coefficients
        do j = 0, 7
          do ii = 0, 7
            coeffs(j,ii,ibox) = coefftemp(j+1,ii+1)
          end do
        end do
        endif
       end do
      end do
      return
      end




      subroutine getcoeff1(fdat,coeff,wsave2)
      implicit real *8 (a-h,o-z)
      real *8 fdat(8,1),coeff(8,1)
      real *8 wsave2(1000)
      real *8 work(1000)
      real *8 f(1000),texp(1000)
c
c     transform rows
c
         do i = 1,8
            f(i) = fdat(i,1)
         enddo
         call chexfc(f,8,texp,wsave2,work)
         do i = 1,8
            coeff(i,1) = texp(i)
         enddo
      return
      end


c**********************************************************************
      subroutine addexp(b,a,nterms)
c**********************************************************************
c     input :     multipole expansions  a and b of length nterms
c     output:     a is over written by (a+b).
c***********************************************************************
      implicit none
      integer *4  nterms
      complex *16 b(0:1),a(0:1)
      integer *4 i
c --------------------------------------------------------------------
c
      do i = 0,nterms
         a(i) = a(i) + b(i)
      end do
      return
      end

 



c**********************************************************************
c     this subroutine is designed to merge the multipole expansions
c     of four child boxes together to form the multipole expansion
c     of the parent.  
c     the algorithm works by passing through the loop mod 4 and
c     rescaling the new coefficients so that they are on the parent's
c     level.
c
c     input:
c 
c     mpole1 is the multipole coefficients for the child box in the
c            upper left corner
c 
c     mpole2 is the multipole coefficients for the child box in the
c            upper right corner
c 
c     mpole3 is the multipole coefficients for the child box in the
c            lower right corner
c 
c     mpole4 is the multipole coefficients for the child box in the
c            lower left corner
c 
c     nterms is the order of the multipole expansions
c 
c     c is the array that the binomial coefficients are stored in
c
c     output:
c 
c     mpolepar is the multipole coefficients of the parent box
c
c**********************************************************************
      subroutine childpar_ps(mpolepar, mpole1, mpole2, mpole3,
     1                    mpole4, nterms, c)
      implicit none
c-----global variables
      integer *4 nterms
      real *8 c(2*nterms, 1)
      complex *16 mpole1(0:nterms), mpole2(0:nterms)
      complex *16 mpole3(0:nterms), mpole4(0:nterms)
      complex *16 mpolepar(0:nterms)
c-----local variables
      integer *4  i, m, k, m1, m2, m3, k1, k2, k3
      real *8  xntemp, xntemp1
      complex *16  atemp(0:60), atemp2(0:60), atemp3(0:60), atemp4(0:60)
      complex *16 b1(60)
      complex *16 z00, z0p(0:60), z0p2(0:60)
      complex *16 cd, cdd, z0, imag
      complex *16 temp1, temp2, temp3, temp4
      data imag/(0.0d0,1.0d0)/

      z0 = -.5d0 + .5d0*imag
      z00=z0
      cd=1.0d0 / z0
      cdd=cd
      z0p(0) =1.0d0
      z0p2(0)=1.0d0
      do i=1,nterms
         z0p(i)  = z00
         z0p2(i) = cdd
         cdd     = cdd*cd
         z00     = z00*z0
      end do

      do i=1,nterms
         b1(i)    = 0.0d0
      end do

      mpolepar(0) = mpolepar(0) + mpole1(0)
     1      + mpole2(0) + mpole3(0) + mpole4(0)

      do m = 1, nterms
       temp1 = mpole1(m) + mpole3(m)
       temp2 = mpole1(m) - mpole3(m)
       temp3 = mpole2(m) + mpole4(m)
       temp4 = imag*(mpole2(m) - mpole4(m))

       atemp(m) =  (temp1 + temp3)*z0p2(m)
       atemp2(m) = (temp1 - temp3)*z0p2(m)
       atemp3(m) = (temp2 - temp4)*z0p2(m)
       atemp4(m) = (temp2 + temp4)*z0p2(m)
      end do

      xntemp = 2.0d0
      do m=1,nterms-3,4
      m1 = m + 1
      m2 = m + 2
      m3 = m + 3
      do k=1,nterms-3,4
        k1 = k + 1
        k2 = k + 2
        k3 = k + 3

        b1(m)=b1(m) + atemp(k)*c(m,k)
     1   + atemp4(k1)*c(m,k1)
     2   + atemp2(k2)*c(m,k2)
     3   + atemp3(k3)*c(m,k3)

        b1(m1)=b1(m1) + atemp3(k)*c(m1,k)
     1   + atemp(k1)*c(m1,k1)
     2   + atemp4(k2)*c(m1,k2)
     3   + atemp2(k3)*c(m1,k3)

        b1(m2)=b1(m2) + atemp2(k)*c(m2,k)
     1   + atemp3(k1)*c(m2,k1)
     2   + atemp(k2)*c(m2,k2)
     3   + atemp4(k3)*c(m2,k3)

        b1(m3)=b1(m3) + atemp4(k)*c(m3,k)
     1   + atemp2(k1)*c(m3,k1)
     2   + atemp3(k2)*c(m3,k2)
     3   + atemp(k3)*c(m3,k3)
      end do

       temp1 = mpole1(0) - mpole3(0)
       temp2 = imag*(mpole2(0) - mpole4(0))
       temp3 = mpole1(0) + mpole3(0)
       temp4 = mpole2(0) + mpole4(0)

       xntemp1 = xntemp

       mpolepar(m) = mpolepar(m) + z0p(m)*(b1(m)
     1     +(temp2 - temp1)/dble(m))/ xntemp1

       xntemp1 = 2.0d0*xntemp1
       mpolepar(m1) = mpolepar(m1) + z0p(m1)*(b1(m1)
     1    + (temp4 - temp3)/dble(m1))/ xntemp1

       xntemp1 = 2.0d0*xntemp1
       mpolepar(m2) = mpolepar(m2) + z0p(m2)*(b1(m2)
     1 -(temp1 + temp2)/dble(m2))/ xntemp1

       xntemp1 = 2.0d0*xntemp1
       mpolepar(m3) = mpolepar(m3) + z0p(m3)*(b1(m3)
     1  -(temp3 + temp4)/dble(m3))/ xntemp1
    
      xntemp = 16.0d0*xntemp

      end do
      return
      end

c**********************************************************************
c     this subroutine is designed to shift the local expansions of
c     a parent box to its four children.  this is used in the downward
c     pass. 
c
c     input:
c 
c     betahatpar is the multipole coefficients of the parent box
c
c     nterms is the order of the multipole expansions
c
c     c is the array that the binomial coefficients are stored in
c
c     output:
c
c     betahat1 is the multipole coefficients for the child box in the
c            upper left corner
c
c     betahat2 is the multipole coefficients for the child box in the
c            upper right corner
c
c     betahat3 is the multipole coefficients for the child box in the
c            lower right corner
c
c     betahat4 is the multipole coefficients for the child box in the
c            lower left corner
c
c**********************************************************************
      subroutine parentchild_ps(betahatpar, 
     1      beta1hat, beta2hat, beta3hat,
     2      beta4hat, nterms, c, iswitch)
      implicit none
c-----global variables
      integer *4 nterms, iswitch
      real *8 c(2*nterms, 1)
      complex *16 beta1hat(0:nterms), beta2hat(0:nterms)
      complex *16 beta3hat(0:nterms), beta4hat(0:nterms)
      complex *16 betahatpar(0:nterms)
c-----local variables
      integer *4 i, m, k
      integer *4 k1,k2,k3,k4
      integer *4 m1,m2,m3
      complex *16 temp1, temp2, temp3, temp4, temp5
      complex *16 temp6, temp7, temp8, temp9, temp10
      complex *16 z0,z00,z0p(0:60),z0p2(0:60), anew(0:60)
      complex *16 cd,cdd,imag

      data imag/(0.0d0, 1.0d0)/

      if(iswitch .eq. 0)then
        return
      endif

c     now let's initialize an array containing
c     powers of the shift vector (-.25 + i*.25)
c     set z0p(i) equal to z0^i and
c     z0p2(i) equal to 1 / z0^i:
      z0 = -.25d0 + imag*.25d0
      z00= z0
      z0p(0)=1.0d0
      cd = 1.0d0 /(z0*2.0d0)
      cdd = cd
      z0p2(0) = 1.0d0

      do  i=1,nterms
         z0p(i)=z00
         z00= z00*z0
         z0p2(i) = cdd
         cdd = cdd*cd
      end do

c     initialize all local expansions to 
c     zero and set up the array anew:
      do i=0, nterms
          beta1hat(i) = 0.0d0
          beta2hat(i) = 0.0d0
          beta3hat(i) = 0.0d0
          beta4hat(i) = 0.0d0
          anew(i) = betahatpar(i)*z0p(i)
      end do

c     now go through a mod 4 loop in which the
c     expansions are shifted:
      do k=0, nterms-3, 4
      k1 = k + 1
      k2 = k + 2
      k3 = k + 3
      k4 = k + 4
       do m=0, k
       m1 = m + 1
        temp5 = anew(k)*c(k1,m1)
        temp6 = anew(k1)*c(k2,m1)
        temp7 = anew(k2)*c(k3,m1)
        temp8 = anew(k3)*c(k4,m1)

        temp1 = temp5 + temp7
        temp2 = temp5 - temp7
        temp3 = temp6 + temp8
        temp4 = imag*(temp6 - temp8)

        beta1hat(m) = beta1hat(m) + temp1 + temp3
        beta2hat(m) = beta2hat(m) + temp2 - temp4
        beta3hat(m) = beta3hat(m) + temp1 - temp3
        beta4hat(m) = beta4hat(m) + temp2 + temp4
       end do


        temp7 = anew(k2)*c(k3,k2)
        temp8 = anew(k3)*c(k4,k2)
        temp9 = imag*(anew(k1) - temp8)
        temp10 = anew(k1) + temp8
        beta1hat(k1) = beta1hat(k1) + temp7 + temp10
        beta2hat(k1) = beta2hat(k1) - temp9 - temp7
        beta3hat(k1) = beta3hat(k1) - temp10 + temp7
        beta4hat(k1) = beta4hat(k1) + temp9  - temp7


        temp8 = anew(k3)*c(k4,k3)
        temp9 = imag*temp8
        beta1hat(k2) = beta1hat(k2) + anew(k2) + temp8
        beta2hat(k2) = beta2hat(k2) - anew(k2) + temp9
        beta3hat(k2) = beta3hat(k2) + anew(k2) - temp8
        beta4hat(k2) = beta4hat(k2) - anew(k2) - temp9


        temp8 = imag*anew(k3)
        beta1hat(k3) = beta1hat(k3) + anew(k3)
        beta2hat(k3) = beta2hat(k3) + temp8
        beta3hat(k3) = beta3hat(k3) - anew(k3)
        beta4hat(k3) = beta4hat(k3) - temp8
      end do

      do m = 1, nterms-3, 4
      m1 = m + 1
      m2 = m + 2
      m3 = m + 3
         temp1 = z0p2(m)*imag
         temp2 = z0p2(m2)*imag

         beta1hat(m)  = beta1hat(m)*z0p2(m)
         beta1hat(m1) = beta1hat(m1)*z0p2(m1)
         beta1hat(m2) = beta1hat(m2)*z0p2(m2)
         beta1hat(m3) = beta1hat(m3)*z0p2(m3)

         beta2hat(m)  =  beta2hat(m)*temp1
         beta2hat(m1) = -beta2hat(m1)*z0p2(m1)
         beta2hat(m2) = -beta2hat(m2)*temp2
         beta2hat(m3) =  beta2hat(m3)*z0p2(m3)

         beta3hat(m)  = -beta3hat(m)*z0p2(m)
         beta3hat(m1) =  beta3hat(m1)*z0p2(m1)
         beta3hat(m2) = -beta3hat(m2)*z0p2(m2)
         beta3hat(m3) =  beta3hat(m3)*z0p2(m3)

         beta4hat(m)  = -beta4hat(m)*temp1
         beta4hat(m1) = -beta4hat(m1)*z0p2(m1)
         beta4hat(m2) =  beta4hat(m2)*temp2
         beta4hat(m3) =  beta4hat(m3)*z0p2(m3)
      end do
      return
      end


c**************************************************************************
c     the following subroutine computes several things and arrays that
c     are needed for later use in the code.  
c
c     input:
c 
c     everything is blank on input
c
c     output:
c 
c     comp is an array that is a combination of factorial and nodal terms
c 
c     nterms is the order of the multipole expansions
c 
c     nnodes is the order of the plane wave expansions
c 
c     xnodes is a blank array that is set to
c            the nodes in the plane wave expansions
c 
c     wnodes is a blank array that is set to
c            the weights in the plane wave expansions
c 
c     temp  is a blank array that is set to
c            the ratio of the weights and nodes
c 
c     choose is the array that the binomial coefficients are stored in
c 
c     sum is a precomputed terms needed in the exponential expansions
c
c**************************************************************************
      subroutine precompute(comp, nterms, 
     1         nnodes, xnodes, wnodes, temp,
     2         choose, sum)
      implicit none
c-----global variables
      integer *4  nnodes, nterms
      real *8  choose(2*nterms, 2*nterms)
      real *8  wnodes(1), xnodes(1), temp(1)
      real *8  comp(nnodes,0:nterms), sum
c-----local variables
      integer *4  i, j
      real *8  fact(0:100), x

c     set up the factorial array:
      fact(0) = 1.0d0
      do  i = 1, 2*nterms
       fact(i) = dble(i)
        do  j = i-1, 2, -1
          fact(i) = fact(i) * dble(j)
       end do
      end do
c     fact(i) is now equal to i!

c     now that we have set up the factorial
c     array, let's set up the choose array.
      do i = 1, 2*nterms
       do j = 1, i
         choose(i,j) = fact(i-1)/(fact(i-j)*fact(j-1))
       end do
      end do
c     choose(i,j) is now equal to (i-1) choose (j-1).

c     call a subroutine that returns precomputed nodes that will
c     accurately approximate the integral representing 1/z, note
c     that these nodes will only give accurate results for certain
c     specific values of z (see comments in the pwts4 subroutine):

      do i = 1, nnodes
       temp(i) = wnodes(i) / xnodes(i)
      end do


c     now let's precompute the array of terms comp(i,j).
      do i = 1, nnodes
        x = 1.0d0
        do j = 0, nterms
          comp(i,j) = x / fact(j)
          x = x * xnodes(i)
        end do
      end do
c     comp(i,j) is now set equal to xnodes(i)^j / j!
c     this is used later on in the parent to child and
c     child to parent subroutines.

      sum = 0.0d0
      do i = 1, nnodes
       sum = sum + dexp(-xnodes(i))*wnodes(i)/xnodes(i)
      end do
      return
      end


c*************************************************************************
c     the following subroutine takes four multipole expansions (from four
c     different directions) as input after they have been shifted to a 
c     common target point.  the four expansions are joined into one 
c     common local expansion.  
c     none of the input parameters are altered.
c     the expansions are merged by counting through the loop mod 4.
c
c     input:
c
c     p is the order of the multipole expansions
c
c     nnodes is the order of the plane wave expansions
c
c     betaw are the incoming west exponential coefficients
c
c     betas are the incoming south exponential coefficients
c
c     betae are the incoming east exponential coefficients
c
c     betan are the incoming north exponential coefficients
c
c     comp is a precomputed term involving the ratio of
c          the weights and factorial terms
c
c     output:
c
c     betahat is the local coefficients of the parent box
c
c*************************************************************************
      subroutine exp4local(betahat, p, nnodes, betaw,
     1                     betas, betae, betan, comp)
      implicit none
c-----global variables
      integer *4 nnodes, p
      real *8 comp(nnodes,0:p)
      complex *16 betahat(0:p)
      complex *16 betan(0:nnodes), betas(0:nnodes)
      complex *16 betae(0:nnodes), betaw(0:nnodes)
c-----local variables
      integer *4 i, j
      complex *16 bsum1, bsum2, bsum3, bsum4
      complex *16 bs14, bs3m2, bs1m4, bs32
      complex *16 imag

      data imag/(0.0d0, 1.0d0)/

c     the local coefficients are computed below.
c     they are obtained just by taking the exponential
c     expansions and performing a taylor expansion of
c     each term.  these taylor expansions are then combined
c     to generate one local expansion.
c
c     upon input, the exponential expansions are in the 
c     form sum(i 0 to nnodes)beta(i)*exp(spin*xnodes(i)*z).
c     upon output, the local expansion is of the form
c     sum(i 0 to p) betahat(i)*z^i.

      betahat(0) = 0.0d0
      do i = 1, nnodes
        betahat(0) = betahat(0) - (betaw(i) + betae(i) +
     1          betas(i) + betan(i))
      end do

      do i = 1, nnodes
         bsum1 = imag*(betan(i) - betas(i))
         bsum2 = betae(i) + betaw(i)
         bsum3 = betan(i) + betas(i)
         bsum4 = betaw(i) - betae(i)
	 bs14  = bsum4 + bsum1
	 bs3m2 = bsum3 - bsum2
	 bs1m4 = bsum1 - bsum4
	 bs32  = bsum3 + bsum2

c        initialize all terms to zero the
c        first time through the loop.
         if(i .eq. 1)then
            do j = 1, nnodes
              betahat(j) = 0.0d0
            end do
         endif

         do j = 1, nnodes-3, 4
           betahat(j)   = betahat(j)   - comp(i,j)*bs14
           betahat(j+1) = betahat(j+1) + comp(i,j+1)*bs3m2
           betahat(j+2) = betahat(j+2) + comp(i,j+2)*bs1m4
           betahat(j+3) = betahat(j+3) - comp(i,j+3)*bs32
         end do
      end do
      betahat(0) = betahat(0) + betaw(0) + betae(0)
     1           + betas(0) + betan(0)
      return
      end


c*************************************************************************
c
c     the following subroutine is used to evaluate the 'big'
c     expansions.  these are the expansions that result from the
c     stobfar interactions.
c
c     iflageast, iflagwest, iflagnorth, and iflagsouth are flags that
c     indicate whether or not a given expansion needs to be evaluated.
c
c     tempnorth, tempsouth, tempwest, and tempeast are precomputed arrays 
c     that may be needed on the evaluation.
c
c     expw, exps, expe, and expn are the exponential expansions that
c     represent the incoming stobfar interactions.  pot is the potential
c     of the box in question and nnodes is the number of terms in the 
c     exponential expansion.
c
c*************************************************************************
      subroutine expbig4eval(pot,nnodes,
     1      expw, exps, expe, expn,
     2      iflageast,iflagwest,
     3      iflagnorth,iflagsouth, tempeast, 
     4      tempnorth, tempwest, tempsouth)
      implicit none
c-----global variables
      integer *4  nnodes
      integer *4  iflageast, iflagnorth
      integer *4  iflagwest, iflagsouth
      real *8  pot(64)
      complex *16  expn(0:nnodes), exps(0:nnodes)
      complex *16  expe(0:nnodes), expw(0:nnodes)
      complex *16  tempeast(64,1), tempnorth(64,1)
      complex *16  tempwest(64,1), tempsouth(64,1)
c-----local variables
      integer *4  i, j

        if(iflageast  .eq. 0 .and.  iflagwest .eq. 0 .and.
     1     iflagnorth .eq. 0 .and. iflagsouth .eq. 0)then
              return
        endif


        if(iflagwest .eq. 1)then
         do j = 1, 64
          pot(j) = pot(j) + dreal(expw(0))
          do i = 1, nnodes
           pot(j) = pot(j) + dreal(expw(i)*tempwest(j,i))
          end do
         end do
        endif

        if(iflageast .eq. 1)then
         do j = 1, 64
          pot(j) = pot(j) + dreal(expe(0))
          do i = 1, nnodes
           pot(j) = pot(j) + dreal(expe(i)*tempeast(j,i))
          end do
         end do
        endif

        if(iflagnorth .eq. 1)then
         do j = 1, 64
          pot(j) = pot(j) + dreal(expn(0))
          do i = 1, nnodes
           pot(j) = pot(j) + dreal(expn(i)*tempnorth(j,i))
          end do
         end do
        endif

        if(iflagsouth .eq. 1)then
         do j = 1, 64
          pot(j) = pot(j) + dreal(exps(0))
          do i = 1, nnodes
           pot(j) = pot(j) + dreal(exps(i)*tempsouth(j,i))
          end do
         end do
        endif

      return
      end


c***********************************************************************
c     this file contains all of the expansion creation routines for 
c     a parent box from its four children.
c
c     mkexps creates the table of shifting coefficients in the physical
c     domain e^{lambda z} for a given lambda discretization.
c
c     mknsexp creates all north and south expansions centered at child 1
c
c     mkewexp creates all east and west expansions centered at child 1
c
c***********************************************************************
      subroutine mkshifts2d_ps(xnodes,nnodes,zs,
     1    zseast,zswest,zsnorth,zssouth)
      implicit none
      complex *16 zs(-3:3,-3:3,0:nnodes)
      complex *16 zseast(-3:3,-3:3,0:nnodes)
      complex *16 zswest(-3:3,-3:3,0:nnodes)
      complex *16 zsnorth(-3:3,-3:3,0:nnodes)
      complex *16 zssouth(-3:3,-3:3,0:nnodes)
      complex *16 tempexp
      real *8     xnodes(nnodes)
      integer *4  nnodes, k, m, n
c     loop over each lambda value 
      do k = 1,nnodes
         do n = -3,3
            do m = -3,3
               zs(n,m,k)=cdexp(-xnodes(k)*dcmplx(dble(n),dble(m)))
            enddo
         enddo
      enddo
      do n = -3,3
         do m = -3,3
            zs(n,m,0)=1.0d0
         enddo
      enddo

      do k = 1,nnodes
         tempexp = cdexp(-xnodes(k)*dcmplx(0.5d0,0.5d0))
         do n = -3,3
            do m = -3,3
               zseast(n,m,k)=zs(n,m,k) * tempexp
            enddo
         enddo
      enddo
      do n = -3,3
         do m = -3,3
            zseast(n,m,0)=1.0d0
         enddo
      enddo

      do k = 1,nnodes
         tempexp = cdexp(-xnodes(k)*dcmplx(-0.5d0,-0.5d0))
         do n = -3,3
            do m = -3,3
               zswest(n,m,k)=zs(n,m,k) * tempexp
            enddo
         enddo
      enddo
      do n = -3,3
         do m = -3,3
            zswest(n,m,0)=1.0d0
         enddo
      enddo

      do k = 1,nnodes
         tempexp = cdexp(-xnodes(k)*dcmplx(0.5d0,-0.5d0))
         do n = -3,3
            do m = -3,3
               zsnorth(n,m,k)=zs(n,m,k) * tempexp
            enddo
         enddo
      enddo
      do n = -3,3
         do m = -3,3
            zsnorth(n,m,0)=1.0d0
         enddo
      enddo

      do k = 1,nnodes
         tempexp = cdexp(-xnodes(k)*dcmplx(-0.5d0,0.5d0))
         do n = -3,3
            do m = -3,3
               zssouth(n,m,k)=zs(n,m,k) * tempexp
            enddo
         enddo
      enddo
      do n = -3,3
         do m = -3,3
            zssouth(n,m,0)=1.0d0
         enddo
      enddo

      return
      end

c***********************************************************************
      subroutine mkexp2d_ps(ibox,nterms,mpole,
     1           nnodes,mexnall,mexn12,mexsall,mexs34,
     2           mexeall,mexe13,mexe1,mexe3,mexwall,mexw24,mexw2,mexw4,
     3           zs,comp,wnodes,scale, sum, temp,
     4           ichildbox)
c***********************************************************************
      implicit none
      integer *4  ibox
      integer *4  nnodes,ic
      integer *4  ichild(4)
      integer *4  ichildbox(4,1)
      integer *4  nterms, jj
      real *8     xlength
      complex *16  mpole(0:nterms,*)
      complex *16  zs(-3:3,-3:3,0:nnodes)
      complex *16  expn(0:50),exps(0:50),expe(0:50),expw(0:50)
      complex *16  mexnall(0:nnodes),mexn12(0:nnodes)
      complex *16  mexsall(0:nnodes),mexs34(0:nnodes)
      complex *16  mexeall(0:nnodes),mexe13(0:nnodes)
      complex *16  mexe1(0:nnodes),mexe3(0:nnodes)
      complex *16  mexwall(0:nnodes),mexw24(0:nnodes)
      complex *16  mexw2(0:nnodes),mexw4(0:nnodes)
      real *8  comp(nnodes,0:nterms)
      real *8  wnodes(nnodes),scale, sum, sum1
      real *8  temp(nnodes)
c***********************************************************************
c
c     this subroutine creates the north (+y)  and south (-y) exponential 
c     expansions for a parent box due to all four children. 
c
c     some intelligence is used in the order of summation. 
c
c***********************************************************************

      xlength = 1.0d0/scale
      sum1 = sum + dlog(xlength)

      ichild(1) = ichildbox(1,ibox)
      ichild(2) = ichildbox(2,ibox)
      ichild(3) = ichildbox(3,ibox)
      ichild(4) = ichildbox(4,ibox)

c
c     include contributions from child 1
c

      ic = ichild(4)

      call expcoeff(expe,expw,expn,exps,
     1                    nnodes, mpole(0,ic), nterms, comp,
     2                    wnodes, sum1, temp)

      do jj = 0,nnodes
         mexn12(jj) = expn(jj)
         mexsall(jj) = exps(jj)
      enddo
      do jj = 0,nnodes
         mexe1(jj) = expe(jj)
         mexwall(jj) = expw(jj)
      enddo

c
c     include contributions from child 2
c
      ic = ichild(3)


      call expcoeff(expe,expw,expn,exps,
     1                    nnodes, mpole(0,ic), nterms, comp,
     2                    wnodes, sum1, temp)


      do jj = 0,nnodes
         expn(jj) = expn(jj)*zs(0,1,jj)
         mexn12(jj) = mexn12(jj) + expn(jj)
         exps(jj) = exps(jj)*zs(0,-1,jj)
         mexsall(jj) = mexsall(jj) + exps(jj)
      enddo

      do jj = 0,nnodes
         mexeall(jj) = expe(jj)*zs(-1,0,jj)
         mexw2(jj) = expw(jj)*zs(1,0,jj)
      enddo
c
c     include contributions from child 3
c
      ic = ichild(1)


      call expcoeff(expe,expw,expn,exps,
     1                    nnodes, mpole(0,ic), nterms, comp,
     2                    wnodes, sum1, temp)


      do jj = 0,nnodes
         expn(jj) = expn(jj)*zs(-1,0,jj)
         mexnall(jj) = expn(jj) + mexn12(jj)
         exps(jj) = exps(jj)*zs(1,0,jj)
         mexs34(jj) = exps(jj)
      enddo

      do jj = 0,nnodes
         mexe3(jj) = expe(jj)*zs(0,-1,jj)
         mexe13(jj) = mexe1(jj) + mexe3(jj)
         mexeall(jj) = mexeall(jj) + mexe13(jj)
         expw(jj) = expw(jj)*zs(0,1,jj)
         mexwall(jj) = mexwall(jj) + expw(jj)
      enddo
c
c     include contributions from child 4
c
      ic = ichild(2)

      call expcoeff(expe,expw,expn,exps,
     1                    nnodes, mpole(0,ic), nterms, comp,
     2                    wnodes, sum1, temp)



      do jj = 0,nnodes
         expn(jj) = expn(jj)*zs(-1,1,jj)
         mexnall(jj) = mexnall(jj) + expn(jj)
         exps(jj) = exps(jj)*zs(1,-1,jj)
         mexs34(jj) = mexs34(jj) + exps(jj)
         mexsall(jj) = mexsall(jj) + mexs34(jj)
      enddo

      do jj = 0,nnodes
         expe(jj) = expe(jj)*zs(-1,-1,jj)
         mexeall(jj) = mexeall(jj) + expe(jj)
         mexw4(jj) = expw(jj)*zs(1,1,jj)
         mexw24(jj) = mexw2(jj) + mexw4(jj)
         mexwall(jj) = mexwall(jj) + mexw24(jj)
      enddo
      return
      end


c***********************************************************************
c     this subroutine is set up to compute all of the north, south,
c     east, and west interaction lists.  because this is for the
c     adaptive case, all of these lists have to be computed in one
c     loop.  north and south take precedence over the east and
c     west and this is reflected  in the else statements within
c     the routine.  processing is done at the parent level.
c     in the periodic case, the procedure is basically 
c     the same, but if a column or row number lies outside the center
c     box, it is readjusted to account for the periodicity.
c     when the lists are actually processed, there is no difference
c     between free space and periodic case.
c    
c     input:
c
c     ibox denotes the box being considered
c
c     level is the level of ibox
c
c     icolleagbox, irowbox, and icolbox define the tree
c
c     iperiod denotes whether or not the case is periodic or free space
c
c
c     output:
c
c     the naming convention for the lists is is as follows:
c
c     inall is an array that denotes the boxes in the
c     north all list.  
c
c     nnall is the number of boxes in the north all
c     list.  
c
c     iynall represents the corresponding offsets of the boxes
c     in the north all list.
c
c     the same convention is used for the south all, east all, west all,
c     north12, south34, east13, west24, west2, west4, east1,
c     and east3 lists.
c
c***********************************************************************
      subroutine mklists_ps(ibox,inall,nnall,iynall, in12,nn12,iy12,
     1    isall,nsall,iysall,is34,ns34,iy34,ieall,neall,
     2    iyeall,ie13,ne13,iy13,iwall,nwall,iywall,
     3    iw24,nw24,iy24,iw2,iy2,nw2,iw4,iy4,nw4,
     4    ie1,iy1,ne1,ie3,iy3,ne3,
     5    inbig12,isbig34,iebig13,
     6    iwbig24,iebig1, iwbig2, 
     7    iebig3, iwbig4, icolleagbox,ichildbox,
     8    icolbox, irowbox, iperiod,iflageast, iflagwest, 
     9    iflagnorth,iflagsouth, localonoff)
      implicit none
c-----global variables
      integer *4  inall(6),nnall,iynall(6)
      integer *4  isall(6),nsall,iysall(6)
      integer *4  ieall(4),neall,iyeall(4)
      integer *4  iwall(4),nwall,iywall(4)
      integer *4  in12(2),nn12,iy12(2)
      integer *4  is34(2),ns34,iy34(2)
      integer *4  iw24(2),nw24,iy24(2)
      integer *4  ie13(2),ne13,iy13(2)
      integer *4  iw4(1),iy4(1)
      integer *4  nw4
      integer *4  iw2(1),iy2(1)
      integer *4  nw2
      integer *4  ie1(1),iy1(1)
      integer *4  ne1
      integer *4  ie3(1),iy3(1)
      integer *4  ne3
      integer *4  localonoff(1)
      integer *4  ibox
      integer *4  inbig12(3), isbig34(3)
      integer *4  iebig13(1), iwbig24(1)
      integer *4  iebig1(1), iwbig2(1)
      integer *4  iebig3(1), iwbig4(1)
      integer *4  icolleagbox(9,1), ichildbox(4,1)
      integer *4  icolbox(1), irowbox(1)
      integer *4  iperiod
      integer *4  iflageast(1), iflagnorth(1)
      integer *4  iflagwest(1), iflagsouth(1)
c-----local variables
      integer *4  i, j, iout
      integer *4  ichild, ncntr, scntr, ecntr, wcntr
      integer *4  n12cntr, s34cntr
      integer *4  w24cntr, e13cntr
      integer *4  w4cntr, w2cntr
      integer *4  e1cntr, e3cntr
      integer *4  icoltest, irowtest
      integer *4  icol, irow

c     initially, set all list entries
c     and offsets to zero.
      do j = 1, 6
        inall(j)  = 0
        iynall(j) = 0
        isall(j)  = 0
        iysall(j) = 0
      end do

      do j = 1, 4
        ieall(j)  = 0
        iyeall(j) = 0
        iwall(j)  = 0
        iywall(j) = 0
      end do

      do j = 1, 4
        isbig34(j) = -1
        inbig12(j) = -1
      end do

      do j = 1, 2
        in12(j) = 0
        iy12(j) = 0
        ie13(j) = 0
        iy13(j) = 0
        iw24(j) = 0
        iy24(j) = 0
        is34(j) = 0
        iy34(j) = 0
      end do

      ie1(1) = 0
      iy1(1) = 0
      iw2(1) = 0
      iy2(1) = 0
      ie3(1) = 0
      iy3(1) = 0
      iw4(1) = 0
      iy4(1) = 0

      iebig13(1) = -1
      iwbig24(1) = -1
      iebig1(1)  = -1
      iebig3(1)  = -1
      iwbig2(1)  = -1
      iwbig4(1)  = -1


c     all of the offsets are set based from the
c     box in the lower left corner (child 4 in
c     out ordering convention).
c     icol and irow are the rows and columns of
c     the box whose list we are trying to generate.

      icol = icolbox(ichildbox(4,ibox))
      irow = irowbox(ichildbox(4,ibox))


c     first do the free space case:
      if(iperiod .eq. 0)then
c     set all of the counters to 1
      ncntr   =  1
      scntr   =  1
      ecntr   =  1
      wcntr   =  1
      n12cntr =  1
      s34cntr =  1
      w24cntr =  1
      e13cntr =  1
      w4cntr  =  1
      w2cntr  =  1
      e1cntr  =  1
      e3cntr  =  1
c     first scan through all nine of the boxes colleagues
      do 100 i = 1, 9
      iout = icolleagbox(i,ibox)
c     test to see if this colleague doesn't exist or is
c     childless, if so, skip it.
      if(iout .lt. 0)goto 100
      if(ichildbox(1,iout) .gt. 0)then
c       scan all four of the colleagues children.
        do j = 1, 4
c       icoltest and irowtest represent the row and column
c       of the box being checked.
        ichild = ichildbox(j,iout)
        icoltest = icolbox(ichild)
        irowtest = irowbox(ichild)


        if(irowtest .eq. irow+3)then
           inall(ncntr) = ichild
           iynall(ncntr) = icoltest - icol
           ncntr = ncntr + 1
        elseif(irowtest .eq. irow-2)then
           isall(scntr) = ichild
           iysall(scntr) = icoltest - icol
           scntr = scntr + 1
        elseif(icoltest .eq. icol-2)then
           iwall(wcntr) = ichild
           iywall(wcntr) = irowtest - irow
           wcntr = wcntr + 1
        elseif(icoltest .eq. icol+3)then
           ieall(ecntr) = ichild
           iyeall(ecntr) = irowtest - irow
           ecntr = ecntr + 1
        elseif(irowtest .eq. irow+2)then
           in12(n12cntr) = ichild
           iy12(n12cntr) = icoltest - icol
           n12cntr = n12cntr + 1
            if(icoltest .eq. icol-1)then
                iw4(w4cntr) = ichild
                iy4(w4cntr) = irowtest - irow
                w4cntr = w4cntr + 1
            endif
            if(icoltest .eq. icol+2)then
                ie3(e3cntr) = ichild
                iy3(e3cntr) = irowtest - irow
                e3cntr = e3cntr + 1
            endif
        elseif(irowtest .eq. irow-1)then
           is34(s34cntr) = ichild
           iy34(s34cntr) = icoltest - icol
           s34cntr = s34cntr + 1
            if(icoltest .eq. icol-1)then
                iw2(w2cntr) = ichild
                iy2(w2cntr) = irowtest - irow
                w2cntr = w2cntr + 1
            endif
            if(icoltest .eq. icol+2)then
                ie1(e1cntr) = ichild
                iy1(e1cntr) = irowtest - irow
                e1cntr = e1cntr + 1
            endif
        elseif(icoltest .eq. icol-1)then
           iw24(w24cntr) = ichild
           iy24(w24cntr) = irowtest - irow
           w24cntr = w24cntr + 1
        elseif(icoltest .eq. icol+2)then
           ie13(e13cntr) = ichild
           iy13(e13cntr) = irowtest - irow
           e13cntr = e13cntr + 1
        endif
        enddo
      elseif(ichildbox(1,iout) .lt. 0)then
        if(i .eq. 1)then
          isbig34(1) = iout
          iwbig2(1) = iout

          iflagsouth(iout) = 1
          iflagwest(iout) = 1
        elseif(i .eq. 2)then
          isbig34(2) = iout

          iflagsouth(iout) = 1
        elseif(i .eq. 3)then
          isbig34(3) = iout
          iebig1(1) = iout

          iflageast(iout) = 1
          iflagsouth(iout) = 1
        elseif(i .eq. 4)then
          iwbig24(1) = iout

          iflagwest(iout) = 1
        elseif(i .eq. 6)then
          iebig13(1) = iout

          iflageast(iout) = 1
        elseif(i .eq. 7)then
          inbig12(1) = iout
          iwbig4(1) = iout

          iflagnorth(iout) = 1
          iflagwest(iout) = 1
        elseif(i .eq. 8)then
          inbig12(2) = iout

          iflagnorth(iout) = 1
        elseif(i .eq. 9)then
          inbig12(3) = iout
          iebig3(1) = iout

          iflagnorth(iout) = 1
          iflageast(iout) = 1
        endif
      endif
100   continue
      nnall = ncntr   -  1
      nsall = scntr   -  1
      neall = ecntr   -  1
      nwall = wcntr   -  1
      nn12  = n12cntr -  1
      ns34  = s34cntr -  1
      nw24  = w24cntr -  1
      ne13  = e13cntr -  1
      nw4   = w4cntr  -  1
      nw2   = w2cntr  -  1
      ne1   = e1cntr  -  1
      ne3   = e3cntr  -  1

c     now let's do the periodic case:
      elseif(iperiod .eq. 1 .or. iperiod .eq. 2)then
c     set all of the counters to 1
      ncntr   =  1
      scntr   =  1
      ecntr   =  1
      wcntr   =  1
      n12cntr =  1
      s34cntr =  1
      w24cntr =  1
      e13cntr =  1
      w4cntr  =  1
      w2cntr  =  1
      e1cntr  =  1
      e3cntr  =  1
c     first scan through all nine of the boxes colleagues
      do 200 i = 1, 9
      iout = icolleagbox(i,ibox)
c     test to see if this colleague doesn't exist or is
c     childless, if so, skip it.
      if(iout .lt. 0)goto 200
      if(ichildbox(1,iout) .gt. 0)then
c       scan all four of the colleagues children.
        do j = 1, 4
         if(localonoff(ichildbox(j,iout)) .eq. 1)then
          if(i .eq. 7)then
             if(j .eq. 1)then
               inall(ncntr)  = ichildbox(j,iout)
               iynall(ncntr) = -2
               ncntr = ncntr + 1
             elseif(j .eq. 2)then 
               inall(ncntr)  = ichildbox(j,iout)
               iynall(ncntr) = -1
               ncntr = ncntr + 1
             elseif(j .eq. 4)then
               iwall(wcntr) = ichildbox(j,iout)
               iywall(wcntr) = 2
               wcntr = wcntr + 1
             elseif(j .eq. 3)then 
               in12(n12cntr) = ichildbox(j,iout)
               iy12(n12cntr) = -1
               n12cntr = n12cntr + 1
               iw4(w4cntr) = ichildbox(j,iout)
               iy4(w4cntr) = 2
               w4cntr = w4cntr + 1
             endif
          elseif(i .eq. 8)then
             if(j .eq. 1)then
               inall(ncntr)  = ichildbox(j,iout)
               iynall(ncntr) = 0
               ncntr = ncntr + 1
             elseif(j .eq. 2)then
               inall(ncntr)  = ichildbox(j,iout)
               iynall(ncntr) = 1
               ncntr = ncntr + 1
             elseif(j .eq. 3)then 
               in12(n12cntr) = ichildbox(j,iout)
               iy12(n12cntr) = 1
               n12cntr = n12cntr + 1
             elseif(j .eq. 4)then 
               in12(n12cntr) = ichildbox(j,iout)
               iy12(n12cntr) = 0
               n12cntr = n12cntr + 1
             endif
          elseif(i .eq. 9)then
             if(j .eq. 1)then
               inall(ncntr)  = ichildbox(j,iout)
               iynall(ncntr) = 2
               ncntr = ncntr + 1
             elseif(j .eq. 2)then
               inall(ncntr)  = ichildbox(j,iout)
               iynall(ncntr) = 3
               ncntr = ncntr + 1
             elseif(j .eq. 3)then
               ieall(ecntr) = ichildbox(j,iout)
               iyeall(ecntr) = 2
               ecntr = ecntr + 1
             elseif(j .eq. 4)then 
               in12(n12cntr) = ichildbox(j,iout)
               iy12(n12cntr) = 2
               n12cntr = n12cntr + 1
               ie3(e3cntr) = ichildbox(j,iout)
               iy3(e3cntr) = 2
               e3cntr = e3cntr + 1
             endif
          elseif(i .eq. 1)then
             if(j .eq. 4)then
               isall(scntr) = ichildbox(j,iout)
               iysall(scntr) = -2
               scntr = scntr + 1
             elseif(j .eq. 3)then
               isall(scntr) = ichildbox(j,iout)
               iysall(scntr) = -1
               scntr = scntr + 1
             elseif(j .eq. 1)then
               iwall(wcntr) = ichildbox(j,iout)
               iywall(wcntr) = -1
               wcntr = wcntr + 1
             elseif(j .eq. 2)then
               is34(s34cntr) = ichildbox(j,iout)
               iy34(s34cntr) = -1
               s34cntr = s34cntr + 1
               iw2(w2cntr) = ichildbox(j,iout)
               iy2(w2cntr) = -1
               w2cntr = w2cntr + 1
             endif
          elseif(i .eq. 2)then
             if(j .eq. 4)then
               isall(scntr) = ichildbox(j,iout)
               iysall(scntr) = 0
               scntr = scntr + 1
             elseif(j .eq. 3)then
               isall(scntr) = ichildbox(j,iout)
               iysall(scntr) = 1
               scntr = scntr + 1
             elseif(j .eq. 2)then
               is34(s34cntr) = ichildbox(j,iout)
               iy34(s34cntr) = 1
               s34cntr = s34cntr + 1
             elseif(j .eq. 1)then
               is34(s34cntr) = ichildbox(j,iout)
               iy34(s34cntr) = 0
               s34cntr = s34cntr + 1
             endif
          elseif(i .eq. 3)then
             if(j .eq. 4)then
               isall(scntr) = ichildbox(j,iout)
               iysall(scntr) = 2
               scntr = scntr + 1
             elseif(j .eq. 3)then
               isall(scntr) = ichildbox(j,iout)
               iysall(scntr) = 3
               scntr = scntr + 1
             elseif(j .eq. 2)then
               ieall(ecntr) = ichildbox(j,iout)
               iyeall(ecntr) = -1
               ecntr = ecntr + 1
             elseif(j .eq. 1)then
               is34(s34cntr) = ichildbox(j,iout)
               iy34(s34cntr) = 2
               s34cntr = s34cntr + 1
               ie1(e1cntr) = ichildbox(j,iout)
               iy1(e1cntr) = -1
               e1cntr = e1cntr + 1
             endif
          elseif(i .eq. 4)then
             if(j .eq. 1)then
               iwall(wcntr) = ichildbox(j,iout)
               iywall(wcntr) = 1
               wcntr = wcntr + 1
             elseif(j .eq. 4)then
               iwall(wcntr) = ichildbox(j,iout)
               iywall(wcntr) = 0
               wcntr = wcntr + 1
             elseif(j .eq. 2)then
               iw24(w24cntr) = ichildbox(j,iout)
               iy24(w24cntr) = 1
               w24cntr = w24cntr + 1
             elseif(j .eq. 3)then
               iw24(w24cntr) = ichildbox(j,iout)
               iy24(w24cntr) = 0
               w24cntr = w24cntr + 1
             endif
          elseif(i .eq. 6)then
             if(j .eq. 3)then
               ieall(ecntr) = ichildbox(j,iout)
               iyeall(ecntr) = 0
               ecntr = ecntr + 1
             elseif(j .eq. 2)then
               ieall(ecntr) = ichildbox(j,iout)
               iyeall(ecntr) = 1
               ecntr = ecntr + 1
             elseif(j .eq. 4)then
               ie13(e13cntr) = ichildbox(j,iout)
               iy13(e13cntr) = 0
               e13cntr = e13cntr + 1
             elseif(j .eq. 1)then
               ie13(e13cntr) = ichildbox(j,iout)
               iy13(e13cntr) = 1
               e13cntr = e13cntr + 1
             endif
           endif
          endif
        enddo
      elseif(ichildbox(1,iout) .lt. 0 .and.
     1       localonoff(iout) .eq. 1)then
        if(i .eq. 1)then
          isbig34(1) = iout
          iwbig2(1) = iout

          iflagsouth(iout) = 1
          iflagwest(iout) = 1
        elseif(i .eq. 2)then
          isbig34(2) = iout

          iflagsouth(iout) = 1
        elseif(i .eq. 3)then
          isbig34(3) = iout
          iebig1(1) = iout

          iflageast(iout) = 1
          iflagsouth(iout) = 1
        elseif(i .eq. 4)then
          iwbig24(1) = iout

          iflagwest(iout) = 1
        elseif(i .eq. 6)then
          iebig13(1) = iout

          iflageast(iout) = 1
        elseif(i .eq. 7)then
          inbig12(1) = iout
          iwbig4(1) = iout

          iflagnorth(iout) = 1
          iflagwest(iout) = 1
        elseif(i .eq. 8)then
          inbig12(2) = iout

          iflagnorth(iout) = 1
        elseif(i .eq. 9)then
          inbig12(3) = iout
          iebig3(1) = iout

          iflagnorth(iout) = 1
          iflageast(iout) = 1
        endif
      endif
200   continue
      nnall = ncntr   -  1
      nsall = scntr   -  1
      neall = ecntr   -  1
      nwall = wcntr   -  1
      nn12  = n12cntr -  1
      ns34  = s34cntr -  1
      nw24  = w24cntr -  1
      ne13  = e13cntr -  1
      nw4   = w4cntr  -  1
      nw2   = w2cntr  -  1
      ne1   = e1cntr  -  1
      ne3   = e3cntr  -  1
      endif
      return
      end 


c***********************************************************************
c
c     this subroutine processes the north interaction lists.
c
c     input:
c
c     inall(nnall), iynall(nnall) are the boxes 
c          receiving all child box data and the x and y offsets from
c          child 1, respectively.
c     the other north lists are similarly defined (see mknolist).
c
c     mexnall is the exponential expansion for all eight children, etc.
c     zs are the shift coefficients computed by subroutine
c          mkexps.
c
c     output:
c
c     lexp1, which contains the local north expansion information for
c            all boxes, is incremented for each box in the 
c            interaction lists.
c
c
c***********************************************************************
      subroutine processno_ps(lexp1,inall,nnall,iynall,
     1           in12,nn12,iy12,mexnall,mexn12,zs,nnodes,
     2           inbig12,expnbig,zsnorth,localonoff)
      implicit none
      integer *4  inall(nnall),nnall,iynall(nnall)
      integer *4  in12(nn12),nn12,iy12(nn12)
      integer *4  i, j, localonoff(1)
      integer *4  nnodes
      integer *4  inbig12(3)
      complex *16  lexp1(0:nnodes,1)
      complex *16  expnbig(0:nnodes,1)
      complex *16  mexnall(0:nnodes)
      complex *16  mexn12(0:nnodes)
      complex *16  zs(-3:3,-3:3,0:nnodes)
      complex *16  zsnorth(-3:3,-3:3,0:nnodes)
      complex *16  zmul
c
      do i = 1,nnall
         if(localonoff(inall(i)) .eq. 1)then
          do j = 0,nnodes
            zmul = zs(3,-iynall(i),j)
            lexp1(j,inall(i)) = lexp1(j,inall(i)) +
     1      mexnall(j)*zmul
          enddo
         endif
      enddo
c
      do i = 1,nn12
         if(localonoff(in12(i)) .eq. 1)then
          do j = 0,nnodes
             zmul = zs(2,-iy12(i),j)
             lexp1(j,in12(i)) = lexp1(j,in12(i)) +
     1            mexn12(j)*zmul
          enddo
         endif
      enddo

      do i = 1, 3
       if(localonoff(inbig12(i)) .eq. 1 .and.
     1    inbig12(i) .gt. 0)then
         j = 0
         zmul = zsnorth(2,-2*(i-2),j) 
         expnbig(j,inbig12(i)) = expnbig(j,inbig12(i)) +
     1          mexn12(j)*zmul

         do j = 1, nnodes
           zmul = zsnorth(2,-2*(i-2),j)
           expnbig(j,inbig12(i)) = expnbig(j,inbig12(i))
     1                         + mexn12(j)*zmul
         enddo
       endif
      enddo
      return
      end


c***********************************************************************
c
c     this subroutine processes the south interaction lists.
c
c     input:
c
c     isall(nsall), iysall(nsall) are the boxes 
c          receiving all child box data and the x and y offsets from
c          child 1, respectively.
c     the other south lists are similarly defined (see mksolist).
c
c     mexsall is the exponential expansion for all eight children, etc.
c     zs are the shift coefficients computed by subroutine
c          mkexps.
c
c     output:
c
c     lexp2, which contains the local north expansion information for
c            all boxes, is incremented for each box in the 
c            interaction lists.
c
c
c***********************************************************************
      subroutine processso_ps(lexp2,isall,nsall,iysall,
     1           is34,ns34,iy34,mexsall,mexs34,zs,nnodes,
     2           isbig34,expsbig,zssouth,localonoff)
      implicit none
      integer *4  isall(nsall),nsall,iysall(nsall)
      integer *4  is34(ns34),ns34,iy34(ns34)
      integer *4  i, j,localonoff(1)
      integer *4  nnodes
      integer *4  isbig34(3)
      complex *16  lexp2(0:nnodes,1)
      complex *16  mexsall(0:nnodes)
      complex *16  mexs34(0:nnodes)
      complex *16  zs(-3:3,-3:3,0:nnodes)
      complex *16  zssouth(-3:3,-3:3,0:nnodes)
      complex *16  zmul
      complex *16  expsbig(0:nnodes,1)

c
      do i = 1,nsall
        if(localonoff(isall(i)) .eq. 1)then
         do j = 0,nnodes
            zmul = zs(2,iysall(i),j)
            lexp2(j,isall(i)) = lexp2(j,isall(i)) +
     1            mexsall(j)*zmul
         enddo
        endif
      enddo
c
      do i = 1,ns34
        if(localonoff(is34(i)) .eq. 1)then
         do j = 0,nnodes
            zmul = zs(1,iy34(i),j)
            lexp2(j,is34(i)) = lexp2(j,is34(i)) +
     1            mexs34(j)*zmul
         enddo
        endif
      enddo

      do i = 1, 3
       if(isbig34(i) .gt. 0 .and.
     1    localonoff(isbig34(i)) .eq. 1)then
        j = 0
         zmul = zssouth(2,2*(i-2),j)
          expsbig(j,isbig34(i)) = expsbig(j,isbig34(i)) +
     1          mexs34(j)*zmul

        do j = 1, nnodes
         zmul = zssouth(2,2*(i-2),j)
          expsbig(j,isbig34(i)) = expsbig(j,isbig34(i))
     1                          + mexs34(j)*zmul
        enddo
      endif
      enddo
      return
      end

c***********************************************************************
c
c     this subroutine processes the east interaction lists.
c
c     input:
c
c     ieall(neall), iyeall(neall) are the boxes 
c          receiving all child box data and the x and y offsets from
c          child 1, respectively.
c     the other east lists are similarly defined (see mkealist).
c
c     mexeall is the exponential expansion for all eight children, etc.
c     zs are the shift coefficients computed by subroutine
c          mkexps.
c
c     output:
c
c     lexp1, which contains the local east expansion information for
c            all boxes, is incremented for each box in the 
c            interaction lists.
c
c
c***********************************************************************
      subroutine processea_ps(lexp1,ieall,neall,iyeall,
     1           ie13,ne13,iy13,ie1,ne1,iy1,ie3,ne3,iy3,
     2           mexeall,mexe13,mexe1,mexe3,zs,nnodes,
     3           iebig13,expebig,iebig1,iebig3,zseast,
     4           localonoff)
      implicit none
      integer *4  ieall(neall),neall,iyeall(neall)
      integer *4  ie13(ne13),ne13,iy13(ne13)
      integer *4  ie1(ne1),ne1,iy1(ne1)
      integer *4  ie3(ne3),ne3,iy3(ne3)
      integer *4  iebig13(1)
      integer *4  iebig1(1)
      integer *4  iebig3(1)
      integer *4  i, j, localonoff(1)
      integer *4  nnodes
      complex *16  lexp1(0:nnodes,1)
      complex *16  mexeall(0:nnodes)
      complex *16  mexe13(0:nnodes)
      complex *16  mexe1(0:nnodes)
      complex *16  mexe3(0:nnodes)
      complex *16  zs(-3:3,-3:3,0:nnodes)
      complex *16  zseast(-3:3,-3:3,0:nnodes)
      complex *16  expebig(0:nnodes,1)
      complex *16  zmul
c
      do i = 1,neall
        if(localonoff(ieall(i)) .eq. 1)then
         do j = 0,nnodes
            zmul = zs(3,iyeall(i),j)
            lexp1(j,ieall(i)) = lexp1(j,ieall(i)) +
     1            mexeall(j)*zmul
         enddo
        endif
      enddo
c
      do i = 1,ne13
        if(localonoff(ie13(i)) .eq. 1)then
         do j = 0,nnodes
            zmul = zs(2,iy13(i),j)
            lexp1(j,ie13(i)) = lexp1(j,ie13(i)) +
     1            mexe13(j)*zmul
         enddo
        endif
      enddo
c
      do i = 1,ne1
        if(localonoff(ie1(i)) .eq. 1)then
         do j = 0,nnodes
            zmul = zs(2,iy1(i),j)
            lexp1(j,ie1(i)) = lexp1(j,ie1(i)) +
     1            mexe1(j)*zmul
         enddo
        endif
      enddo
c
      do i = 1,ne3
        if(localonoff(ie3(i)) .eq. 1)then
         do j = 0,nnodes
            zmul = zs(2,iy3(i),j)
            lexp1(j,ie3(i)) = lexp1(j,ie3(i)) +
     1            mexe3(j)*zmul
         enddo
        endif
      enddo

      i = 1
      if(iebig13(i) .gt. 0 .and.
     1   localonoff(iebig13(i)) .eq. 1)then
        j = 0
         zmul = zseast(2,0,j)
          expebig(j,iebig13(i)) = expebig(j,iebig13(i)) +
     1          mexe13(j)*zmul

        do j = 1, nnodes
         zmul = zseast(2,0,j)
          expebig(j,iebig13(i)) = expebig(j,iebig13(i)) +
     1          mexe13(j)*zmul
        enddo
      endif

      i = 1
      if(iebig3(i) .gt. 0 .and.
     1   localonoff(iebig3(i)) .eq. 1)then
        j = 0
         zmul = zseast(2,2,j)
          expebig(j,iebig3(i)) = expebig(j,iebig3(i)) +
     1          mexe3(j)*zmul

        do j = 1, nnodes
         zmul = zseast(2,2,j)
          expebig(j,iebig3(i)) = expebig(j,iebig3(i)) +
     1          mexe3(j)*zmul
        enddo
      endif

      i = 1
      if(iebig1(i) .gt. 0 .and.
     1   localonoff(iebig1(i)).eq. 1)then
        j = 0
         zmul = zseast(2,-2,j)
          expebig(j,iebig1(i)) = expebig(j,iebig1(i)) +
     1          mexe1(j)*zmul

        do j = 1, nnodes
         zmul = zseast(2,-2,j)
         expebig(j,iebig1(i)) = expebig(j,iebig1(i)) +
     1          mexe1(j)*zmul
        enddo
      endif
      return
      end


c***********************************************************************
c
c     this subroutine processes the west interaction lists.
c
c     input:
c
c     iwall(nwall), iywall(nwall) are the boxes 
c          receiving all child box data and the x and y offsets from
c          child 1, respectively.
c     the other west lists are similarly defined (see mkwelist).
c
c     mexeall is the exponential expansion for all eight children, etc.
c     zs are the shift coefficients computed by subroutine
c          mkexps.
c
c     output:
c
c     lexp1, which contains the local west expansion information for
c            all boxes, is incremented for each box in the 
c            interaction lists.
c
c
c***********************************************************************
      subroutine processwe_ps(lexp2,iwall,nwall,iywall,
     1           iw24,nw24,iy24,iw2,nw2,iy2,iw4,nw4,iy4,
     2           mexwall,mexw24,mexw2,mexw4,zs,nnodes,
     3           iwbig24,expwbig,iwbig2,iwbig4,zswest,
     4           localonoff)
      implicit none
      integer *4  iwall(nwall),nwall,iywall(nwall)
      integer *4  iw24(nw24),nw24,iy24(nw24)
      integer *4  iw2(nw2),nw2,iy2(nw2)
      integer *4  iw4(nw4),nw4,iy4(nw4)
      integer *4  i, j, localonoff(1)
      integer *4  nnodes
      integer *4  iwbig24(1)
      integer *4  iwbig2(1)
      integer *4  iwbig4(1)
      complex *16  lexp2(0:nnodes,1)
      complex *16  mexwall(0:nnodes)
      complex *16  mexw24(0:nnodes)
      complex *16  mexw2(0:nnodes)
      complex *16  mexw4(0:nnodes)
      complex *16  zs(-3:3,-3:3,0:nnodes)
      complex *16  zswest(-3:3,-3:3,0:nnodes)
      complex *16  expwbig(0:nnodes,1)
      complex *16  zmul
c
      do i = 1,nwall
        if(localonoff(iwall(i)) .eq. 1)then
         do j = 0,nnodes
            zmul = zs(2,-iywall(i),j)
            lexp2(j,iwall(i)) = lexp2(j,iwall(i)) +
     1            mexwall(j)*zmul
         enddo
        endif
      enddo
c
      do i = 1,nw24
        if(localonoff(iw24(i)) .eq. 1)then
         do j = 0,nnodes
            zmul = zs(1,-iy24(i),j)
            lexp2(j,iw24(i)) = lexp2(j,iw24(i)) +
     1            mexw24(j)*zmul
         enddo
        endif
      enddo
c
      do i = 1,nw2
        if(localonoff(iw2(i)) .eq. 1)then
         do j = 0,nnodes
            zmul = zs(1,-iy2(i),j)
            lexp2(j,iw2(i)) = lexp2(j,iw2(i)) +
     1            mexw2(j)*zmul
         enddo
        endif
      enddo
c
      do i = 1,nw4
        if(localonoff(iw4(i)) .eq. 1)then
         do j = 0,nnodes
            zmul = zs(1,-iy4(i),j)
            lexp2(j,iw4(i)) = lexp2(j,iw4(i)) +
     1            mexw4(j)*zmul
         enddo
        endif
      enddo

      i = 1
      if(iwbig24(i) .gt. 0 .and. 
     1   localonoff(iwbig24(i)) .eq. 1)then
        j = 0
         zmul = zswest(2,0,j)
          expwbig(j,iwbig24(i)) = expwbig(j,iwbig24(i)) +
     1          mexw24(j)*zmul

        do j = 1, nnodes
         zmul = zswest(2,0,j)
          expwbig(j,iwbig24(i)) = expwbig(j,iwbig24(i)) +
     1          mexw24(j)*zmul
        enddo
      endif

      i = 1
      if(iwbig2(i) .gt. 0 .and.
     1   localonoff(iwbig2(i)) .eq. 1)then
        j = 0
         zmul = zswest(2,2,j)
          expwbig(j,iwbig2(i)) = expwbig(j,iwbig2(i)) +
     1          mexw2(j)*zmul

        do j = 1, nnodes
         zmul = zswest(2,2,j)
          expwbig(j,iwbig2(i)) = expwbig(j,iwbig2(i)) +
     1          mexw2(j)*zmul
        enddo
      endif


      i = 1
      if(iwbig4(i) .gt. 0 .and.
     1   localonoff(iwbig4(i)) .eq. 1)then
        j = 0
         zmul = zswest(2,-2,j)
          expwbig(j,iwbig4(i)) = expwbig(j,iwbig4(i)) +
     1          mexw4(j)*zmul

        do j = 1, nnodes
         zmul = zswest(2,-2,j)
          expwbig(j,iwbig4(i)) = expwbig(j,iwbig4(i)) +
     1          mexw4(j)*zmul
        enddo
      endif
      return
      end




c************************************************************************
c     the following subroutine calculates the multipole
c     coefficients for a given set of polynomial coefficients.
c
c     input:
c
c     coeffs is the array of coefficients for the basis functions
c
c     ndeg is the degree of the approximating polynomial
c
c     nterms is the number order of the multipole expansion
c
c     xlength is the length of a side of the box
c
c     w are the basis function to multipole weights.
c
c     output:
c
c     mpole is the array of multipole coefficients for the box
c
c************************************************************************
      subroutine multipole(coeff, ndeg, nterms, xlength, mpole, w)
      implicit none
c-----global variables
      integer *4 ndeg, nterms
      real *8 coeff(0:ndeg,0:ndeg), xlength
      complex *16 mpole(0:nterms), w(0:nterms,0:ndeg,0:ndeg)
c-----local variables
      integer *4 i, j, k
      real *8 coeffsc(0:7,0:7), y
      real *8 pi2
      complex *16 sum

c     w contains a set of precomputed integrals for each of the
c     basis functions being considered.  w(i,j,k) represents the
c     integral of x^i * y^j /(x + imag*y)^k over the square 
c     -1 < x < 1 and -1 < y < 1.
c     these integrals can be rescaled as needed.
c     the multipole coefficients are obtained by summing over
c     all of the basis functions and multiplying the above
c     rescaled integral by the corresponding polynomial
c     coefficient.

 
      pi2 = 2.0d0*dacos(-1.0d0)

      do i = 0, ndeg
        do j = 0, ndeg - i
          coeffsc(i,j) = coeff(i,j) * xlength**2
        end do
      end do

      sum = 0.0d0
      do i = 0, ndeg
        do j = 0, ndeg-i
         sum = sum + coeffsc(i,j) * w(0,i,j)
        end do
      end do
      mpole(0) = sum/pi2


      y =  -1.0d0 / (pi2*2.0d0)
      do k = 1, nterms
       sum = 0.0d0
       do i = 0, ndeg
         do j = 0, ndeg-i
           sum = sum  + coeffsc(i,j) * w(k,i,j)
         end do
       end do
       mpole(k) = y * sum / dble(k)
       y = y / 2.0d0
      end do
      return
      end


c**************************************************************************
c     the following subroutine does the local work (just outgoing)
c     between two boxes that are of the same size.  this routine does
c     exactly the same type of work as is doe for the local work in the
c     uniform case.
c
c     input:
c
c     coeffs is the array of  basis functions coefficients for the box
c                 being considered
c
c     ndeg is the degree of the approximating polynomial
c
c     nb denotes the colleague number being considered
c
c     t is a set of precomputed scalings (set in the adapfmm6 routine)
c
c     w is the table of weights needed to determine the local contributions
c
c     output:
c
c     pot is altered to account for the local contribution
c
c**************************************************************************
      subroutine colloc8(pot,coeffs,ndeg,nb,t,tlog,w,iswitch)
      implicit none
c-----global variables
      integer *4 ndeg, nb, iswitch
      real *8 pot(64), coeffs(0:ndeg,0:ndeg)
      real *8 t(3), w(9,64,36)
      real *8 tlog(36)
c-----local variables
      integer *4 i, j
      real *8 c(36), clog(36)

      if(iswitch .eq. 0)then
        return
      endif
  
       c(1)  = coeffs(0,0) / t(1)
       c(2)  = coeffs(1,0) / t(1)
       c(3)  = coeffs(0,1) / t(1)
       c(4)  = coeffs(2,0) / t(1)
       c(5)  = coeffs(1,1) / t(1)
       c(6)  = coeffs(0,2) / t(1)
       c(7)  = coeffs(3,0) / t(1)
       c(8)  = coeffs(2,1) / t(1)
       c(9)  = coeffs(1,2) / t(1)
       c(10) = coeffs(0,3) / t(1)
       c(11) = coeffs(4,0) / t(1)
       c(12) = coeffs(3,1) / t(1)
       c(13) = coeffs(2,2) / t(1)
       c(14) = coeffs(1,3) / t(1)
       c(15) = coeffs(0,4) / t(1)
       c(16) = coeffs(5,0) / t(1)
       c(17) = coeffs(4,1) / t(1)
       c(18) = coeffs(3,2) / t(1)
       c(19) = coeffs(2,3) / t(1)
       c(20) = coeffs(1,4) / t(1)
       c(21) = coeffs(0,5) / t(1)
       c(22) = coeffs(6,0) / t(1)
       c(23) = coeffs(5,1) / t(1)
       c(24) = coeffs(4,2) / t(1)
       c(25) = coeffs(3,3) / t(1)
       c(26) = coeffs(2,4) / t(1)
       c(27) = coeffs(1,5) / t(1)
       c(28) = coeffs(0,6) / t(1)
       c(29) = coeffs(7,0) / t(1)
       c(30) = coeffs(6,1) / t(1)
       c(31) = coeffs(5,2) / t(1)
       c(32) = coeffs(4,3) / t(1)
       c(33) = coeffs(3,4) / t(1)
       c(34) = coeffs(2,5) / t(1)
       c(35) = coeffs(1,6) / t(1)
       c(36) = coeffs(0,7) / t(1)

 
       clog(1)  = c(1) * tlog(1)
       clog(2)  = 0.0d0
       clog(3)  = 0.0d0
       clog(4)  = c(4) * tlog(4)
       clog(5)  = 0.0d0
       clog(6)  = c(6) * tlog(6)
       clog(7)  = 0.0d0
       clog(8)  = 0.0d0
       clog(9)  = 0.0d0
       clog(10) = 0.0d0
       clog(11) = c(11) * tlog(11)
       clog(12) = 0.0d0
       clog(13) = c(13) * tlog(13)
       clog(14) = 0.0d0
       clog(15) = c(15) * tlog(15) 
       clog(16) = 0.0d0
       clog(17) = 0.0d0
       clog(18) = 0.0d0
       clog(19) = 0.0d0
       clog(20) = 0.0d0
       clog(21) = 0.0d0
       clog(22) = c(22) * tlog(22)
       clog(23) = 0.0d0
       clog(24) = c(24) * tlog(24)
       clog(25) = 0.0d0
       clog(26) = c(26) * tlog(26)
       clog(27) = 0.0d0
       clog(28) = c(28) * tlog(28)
       clog(29) = 0.0d0
       clog(30) = 0.0d0
       clog(31) = 0.0d0
       clog(32) = 0.0d0
       clog(33) = 0.0d0
       clog(34) = 0.0d0
       clog(35) = 0.0d0
       clog(36) = 0.0d0
 

       do i = 1,64
        do j = 1,36
          pot(i) = pot(i) + c(j)*w(nb,i,j) + clog(j)
        enddo
       enddo
       return
       end


c***************************************************************************
c     the following subroutine does the local work that figures the
c     potential in a bigger box due to a small box that is touching it.
c     the boxes are only allowed to be one level apart.
c
c     input:
c
c     coeffs is the array of  basis functions coefficients for the box
c                 being considered
c
c     ndeg is the degree of the approximating polynomial
c
c     iflag denotes the colleague number being considered
c
c     t is a set of precomputed scalings (set in the adapfmm6 routine)
c
c     w is the table of weights needed to determine the local contributions
c
c     output:
c
c     pot is altered to account for the local contribution
c
c***************************************************************************
      subroutine stobloc8(pot,coeffs,ndeg,iflag,t,tlog,w,iswitch)
      implicit none
c-----global variables
      integer *4 ndeg, iflag, iswitch
      real *8 pot(36), coeffs(0:ndeg,0:ndeg)
      real *8 t(3), w(12,64,36)
      real *8 tlog(36)
c-----local variables
      integer *4 i, j
      real *8 c(36), clog(36)

      if(iswitch .eq. 0)then
        return
      endif

       c(1)  = coeffs(0,0) / t(3)
       c(2)  = coeffs(1,0) / t(3)
       c(3)  = coeffs(0,1) / t(3)
       c(4)  = coeffs(2,0) / t(3)
       c(5)  = coeffs(1,1) / t(3)
       c(6)  = coeffs(0,2) / t(3)
       c(7)  = coeffs(3,0) / t(3)
       c(8)  = coeffs(2,1) / t(3)
       c(9)  = coeffs(1,2) / t(3)
       c(10) = coeffs(0,3) / t(3)
       c(11) = coeffs(4,0) / t(3)
       c(12) = coeffs(3,1) / t(3)
       c(13) = coeffs(2,2) / t(3)
       c(14) = coeffs(1,3) / t(3)
       c(15) = coeffs(0,4) / t(3)
       c(16) = coeffs(5,0) / t(3)
       c(17) = coeffs(4,1) / t(3)
       c(18) = coeffs(3,2) / t(3)
       c(19) = coeffs(2,3) / t(3)
       c(20) = coeffs(1,4) / t(3)
       c(21) = coeffs(0,5) / t(3)
       c(22) = coeffs(6,0) / t(3)
       c(23) = coeffs(5,1) / t(3)
       c(24) = coeffs(4,2) / t(3)
       c(25) = coeffs(3,3) / t(3)
       c(26) = coeffs(2,4) / t(3)
       c(27) = coeffs(1,5) / t(3)
       c(28) = coeffs(0,6) / t(3)
       c(29) = coeffs(7,0) / t(3)
       c(30) = coeffs(6,1) / t(3)
       c(31) = coeffs(5,2) / t(3)
       c(32) = coeffs(4,3) / t(3)
       c(33) = coeffs(3,4) / t(3)
       c(34) = coeffs(2,5) / t(3)
       c(35) = coeffs(1,6) / t(3)
       c(36) = coeffs(0,7) / t(3)

       clog(1)  = c(1) * tlog(1)
       clog(2)  = 0.0d0
       clog(3)  = 0.0d0
       clog(4)  = c(4) * tlog(4)
       clog(5)  = 0.0d0
       clog(6)  = c(6) * tlog(6)
       clog(7)  = 0.0d0
       clog(8)  = 0.0d0
       clog(9)  = 0.0d0
       clog(10) = 0.0d0
       clog(11) = c(11) * tlog(11)
       clog(12) = 0.0d0
       clog(13) = c(13) * tlog(13)
       clog(14) = 0.0d0
       clog(15) = c(15) * tlog(15)
       clog(16) = 0.0d0
       clog(17) = 0.0d0
       clog(18) = 0.0d0
       clog(19) = 0.0d0
       clog(20) = 0.0d0
       clog(21) = 0.0d0
       clog(22) = c(22) * tlog(22)
       clog(23) = 0.0d0
       clog(24) = c(24) * tlog(24)
       clog(25) = 0.0d0
       clog(26) = c(26) * tlog(26)
       clog(27) = 0.0d0
       clog(28) = c(28) * tlog(28)
       clog(29) = 0.0d0
       clog(30) = 0.0d0
       clog(31) = 0.0d0
       clog(32) = 0.0d0
       clog(33) = 0.0d0
       clog(34) = 0.0d0
       clog(35) = 0.0d0
       clog(36) = 0.0d0

       do i = 1, 64
         do j = 1, 36
           pot(i) = pot(i) + c(j)*w(iflag,i,j) + clog(j)
         end do
       end do
       return
       end


c***************************************************************************
c     the following subroutine does the local work that figures the
c     potential in a smaller box due to a big box that is touching it.
c     the boxes are only allowed to be one level apart.
c
c     input:
c
c     coeffs is the array of  basis functions coefficients for the box
c                 being considered
c
c     ndeg is the degree of the approximating polynomial
c
c     iflag denotes the colleague number being considered
c
c     t is a set of precomputed scalings (set in the adapfmm6 routine)
c
c     w is the table of weights needed to determine the local contributions
c
c     output:
c
c     pot is altered to account for the local contribution
c
c***************************************************************************
      subroutine btosloc8(pot,coeffs,ndeg,iflag,t,tlog,w,iswitch)
      implicit none
c-----global variables
      integer *4 ndeg, iflag, iswitch
      real *8 pot(36), coeffs(0:ndeg,0:ndeg), t(3), tlog(36)
      real *8 w(12,64,36)
c-----local variables
      integer *4 i, j
      real *8 c(36), clog(36) 

      if(iswitch .eq. 0)then
        return
      endif
       
       c(1)  = coeffs(0,0)/t(1)
       c(2)  = coeffs(1,0)/t(1)
       c(3)  = coeffs(0,1)/t(1)
       c(4)  = coeffs(2,0)/t(1)
       c(5)  = coeffs(1,1)/t(1)
       c(6)  = coeffs(0,2)/t(1)
       c(7)  = coeffs(3,0)/t(1)
       c(8)  = coeffs(2,1)/t(1)
       c(9)  = coeffs(1,2)/t(1)
       c(10) = coeffs(0,3)/t(1)
       c(11) = coeffs(4,0)/t(1)
       c(12) = coeffs(3,1)/t(1)
       c(13) = coeffs(2,2)/t(1)
       c(14) = coeffs(1,3)/t(1)
       c(15) = coeffs(0,4)/t(1)
       c(16) = coeffs(5,0)/t(1)
       c(17) = coeffs(4,1)/t(1)
       c(18) = coeffs(3,2)/t(1)
       c(19) = coeffs(2,3)/t(1)
       c(20) = coeffs(1,4)/t(1)
       c(21) = coeffs(0,5)/t(1)
       c(22) = coeffs(6,0)/t(1)
       c(23) = coeffs(5,1)/t(1)
       c(24) = coeffs(4,2)/t(1)
       c(25) = coeffs(3,3)/t(1)
       c(26) = coeffs(2,4)/t(1)
       c(27) = coeffs(1,5)/t(1)
       c(28) = coeffs(0,6)/t(1)
       c(29) = coeffs(7,0)/t(1)
       c(30) = coeffs(6,1)/t(1)
       c(31) = coeffs(5,2)/t(1)
       c(32) = coeffs(4,3)/t(1)
       c(33) = coeffs(3,4)/t(1)
       c(34) = coeffs(2,5)/t(1)
       c(35) = coeffs(1,6)/t(1)
       c(36) = coeffs(0,7)/t(1)

       clog(1)  = c(1) * tlog(1)
       clog(2)  = 0.0d0
       clog(3)  = 0.0d0
       clog(4)  = c(4) * tlog(4)
       clog(5)  = 0.0d0
       clog(6)  = c(6) * tlog(6)
       clog(7)  = 0.0d0
       clog(8)  = 0.0d0
       clog(9)  = 0.0d0
       clog(10) = 0.0d0
       clog(11) = c(11) * tlog(11)
       clog(12) = 0.0d0
       clog(13) = c(13) * tlog(13)
       clog(14) = 0.0d0
       clog(15) = c(15) * tlog(15)
       clog(16) = 0.0d0
       clog(17) = 0.0d0
       clog(18) = 0.0d0
       clog(19) = 0.0d0
       clog(20) = 0.0d0
       clog(21) = 0.0d0
       clog(22) = c(22) * tlog(22)
       clog(23) = 0.0d0
       clog(24) = c(24) * tlog(24)
       clog(25) = 0.0d0
       clog(26) = c(26) * tlog(26)
       clog(27) = 0.0d0
       clog(28) = c(28) * tlog(28)
       clog(29) = 0.0d0
       clog(30) = 0.0d0
       clog(31) = 0.0d0
       clog(32) = 0.0d0
       clog(33) = 0.0d0
       clog(34) = 0.0d0
       clog(35) = 0.0d0
       clog(36) = 0.0d0
 
      do i = 1, 64
        do j = 1, 36
           pot(i) = pot(i) + c(j)*w(iflag,i,j) + clog(j)
        end do
      end do
      return
      end


c************************************************************************
c     the following subroutine takes a specified version of the multipole
c     expansion for box b and converts it to a local expansion about
c     one of the * boxes.  the specified multipole expansion is the
c     one obtained by interpolating down on b to four 'ghost' boxes.
c     this is explained in detail in mkexpbtos.
c           -------------------------
c           | * | * | * | * | * | * |
c           |___|___|___|___|___|___|
c           |   |   |   |   |   |   |
c           | * |   |   |   |   | * |
c           -------------------------
c           | * |   |       |   | * |
c           |___|___|   b   |___|___|
c           |   |   |       |   | * |
c           | * |   |       |   |   |
c           -------------------------
c           | * |   |   |   |   | * |
c           |___|___|___|___|___|___|
c           |   |   |   |   |   |   |
c           | * | * | * | * | * | * |
c           -------------------------
c
c     input:
c
c     expon is the smaller boxes exponential expansion
c     
c     z is the distance from the center of box b to the center
c     * target box,  where the scaling units are that the side of
c     a * box is of length one.
c     
c     nterms is the order of the multipole expansion
c
c     xnodes is a blank array that is set to
c            the nodes in the plane wave expansions
c
c     comp is a precomputed term involving the ratio of
c          the weights and factorial terms
c
c     betall is the larger boxes exponential expansion that
c           has been precomputed
c     
c     spin is a parameter that determines what direction the exponential
c          expansions decay in
c
c     output:
c
c     expon is altered to account for contribution of the larger box
c
c************************************************************************
      subroutine btosfar_ps(expon, z, nterms, xnodes,
     1                 comp, betall, spin,iswitch)
      implicit none
c-----global variables
      integer *4  nterms, iswitch
      real *8  xnodes(1), comp(nterms,0:nterms)
      complex *16  expon(0:nterms), betall(0:nterms)
      complex *16  spin, z
c-----local variables
      integer *4  i
      complex *16  expall(0:60)

      if(iswitch .eq. 0)then
        return
      endif

        do i = 0, nterms
          expall(i) = betall(i)
        end do

        call expshift_ps(expall, nterms, xnodes, spin, z)
        call addexp(expall, expon, nterms)

      return
      end


c*******************************************************************
c     the following subroutine precomputes some stuff that is needed
c     in the 'big to small far' routine later on.  let's suppose
c     that we have the following picture:
c           -------------------------
c           | * | * | * | * | * | * |
c           |___|___|___|___|___|___|
c           |   |   |   |   |   |   |
c           | * |   |   |   |   | * |
c           -------------------------
c           | * |   |       |   | * |
c           |___|___|   b   |___|___|
c           |   |   |       |   | * |
c           | * |   |       |   |   |
c           -------------------------
c           | * |   |   |   |   | * |
c           |___|___|___|___|___|___|
c           |   |   |   |   |   |   |
c           | * | * | * | * | * | * |
c           -------------------------
c     and we need to get compute the interaction from box b to
c     any one of the * boxes.  note that from the perspective
c     of b, the * boxes are not well separated, and also note
c     that there is no reason that one of the * boxes could
c     not be divided.  the expansions are obtained using a matrix
c     that was precomputed earlier.
c
c     input:
c
c     coeffs are the basis function coefficients for the larger box
c
c     mapsouth maps from the basis function coefficients to the 
c              south exponential coefficients
c
c     mapnorth maps from the basis function coefficients to the 
c              north exponential coefficients
c
c     mapeast maps from the basis function coefficients to the 
c              east exponential coefficients
c
c     mapwest maps from the basis function coefficients to the 
c              west exponential coefficients
c
c     sum2 is a precomputed terms needed in the exponential expansions
c
c     xlength is the length of the larger box
c
c     nnodes is the number of nodes in the exponential expansion
c
c     output:
c
c     expnall is north expansion for the larger box
c
c     expsall is south expansion for the larger box
c
c     expeall is east expansion for the larger box
c
c     expwall is west expansion for the larger box
c
c*******************************************************************
      subroutine mkexpbtos8(coeffs, expnall,expsall,expeall,expwall,
     1      mapsouth,mapnorth,mapeast,mapwest,sum2, xlength, nnodes)
      implicit none
c-----global variables
      integer *4  nnodes
      real *8  coeffs(0:7,0:7), sum2,  xlength
      complex *16  expnall(0:nnodes), expsall(0:nnodes)
      complex *16  expeall(0:nnodes), expwall(0:nnodes)
      complex *16  mapwest(0:nnodes,36), mapsouth(0:nnodes,36)
      complex *16  mapeast(0:nnodes,36), mapnorth(0:nnodes,36)
c-----local variables
      integer *4  i, k
      real *8  tcoeff(36)
      real *8  temp1, temp2
      complex *16  sum

c     now initialize all of the output
c     arrays of coefficients to zero:
      do i = 0, nnodes
        expeall(i) = 0.0d0
        expwall(i) = 0.0d0
        expnall(i) = 0.0d0
        expsall(i) = 0.0d0
      end do

c     now let's place the coefficients in a
c     one dimensional array that is indexed 
c     correctly:
      temp1 = xlength**2

      tcoeff(1) = coeffs(0,0) * temp1
      tcoeff(2) = coeffs(1,0) * temp1
      tcoeff(3) = coeffs(0,1) * temp1
      tcoeff(4) = coeffs(2,0) * temp1
      tcoeff(5) = coeffs(1,1) * temp1
      tcoeff(6) = coeffs(0,2) * temp1
      tcoeff(7) = coeffs(3,0) * temp1
      tcoeff(8) = coeffs(2,1) * temp1
      tcoeff(9) = coeffs(1,2) * temp1
      tcoeff(10)= coeffs(0,3) * temp1
      tcoeff(11)= coeffs(4,0) * temp1
      tcoeff(12)= coeffs(3,1) * temp1
      tcoeff(13)= coeffs(2,2) * temp1
      tcoeff(14)= coeffs(1,3) * temp1
      tcoeff(15)= coeffs(0,4) * temp1
      tcoeff(16)= coeffs(5,0) * temp1
      tcoeff(17)= coeffs(4,1) * temp1
      tcoeff(18)= coeffs(3,2) * temp1
      tcoeff(19)= coeffs(2,3) * temp1
      tcoeff(20)= coeffs(1,4) * temp1
      tcoeff(21)= coeffs(0,5) * temp1
      tcoeff(22)= coeffs(6,0) * temp1
      tcoeff(23)= coeffs(5,1) * temp1
      tcoeff(24)= coeffs(4,2) * temp1
      tcoeff(25)= coeffs(3,3) * temp1
      tcoeff(26)= coeffs(2,4) * temp1
      tcoeff(27)= coeffs(1,5) * temp1
      tcoeff(28)= coeffs(0,6) * temp1
      tcoeff(29)= coeffs(7,0) * temp1
      tcoeff(30)= coeffs(6,1) * temp1
      tcoeff(31)= coeffs(5,2) * temp1
      tcoeff(32)= coeffs(4,3) * temp1
      tcoeff(33)= coeffs(3,4) * temp1
      tcoeff(34)= coeffs(2,5) * temp1
      tcoeff(35)= coeffs(1,6) * temp1
      tcoeff(36)= coeffs(0,7) * temp1

c     now just multiply by a precomputed matrix that
c     will map from the ten polynomial coefficients
c     to the four expansions (they are centered 
c     around the 'child' box in the lower left
c     hand corner of the parent box)
      do k = 0, nnodes
       sum = 0.0d0
       do i = 1, 36
            sum = sum + mapnorth(k,i)*tcoeff(i)
       end do
       expnall(k) = sum
      end do

      do k = 0, nnodes
       sum = 0.0d0
       do i = 1, 36
            sum = sum + mapwest(k,i)*tcoeff(i)
       end do
       expwall(k) = sum
      end do

      do k = 0, nnodes
       sum = 0.0d0
       do i = 1, 36
            sum = sum + mapeast(k,i)*tcoeff(i)
       end do
       expeall(k) = sum
      end do

      do k = 0, nnodes
       sum = 0.0d0
       do i = 1, 36
            sum = sum + mapsouth(k,i)*tcoeff(i)
       end do
       expsall(k) = sum
      end do

      temp2 = sum2 + dlog(xlength/2.0d0)
      expeall(0) = expeall(0) * temp2
      expwall(0) = expwall(0) * temp2
      expnall(0) = expnall(0) * temp2
      expsall(0) = expsall(0) * temp2
      return
      end


c**********************************************************************
c     the following subroutine is designed to evaluate the
c     multipole expansion given an array of multipole coefficients.
c
c     input:
c
c     mpole is the array of multipole coefficients being evaluated
c
c     nterms is the order of the multipole expansion
c
c     z is the target point (scaled from the standpoint of the side of
c                       the above box being one)
c
c     xlength is the length of a side of the box being considered
c
c     output:
c
c     phi is value of the multipole expansion evaluated at z
c
c**********************************************************************
      subroutine multeval(mpole, nterms, z, phi, xlength)
      implicit none
c-----global variables
      integer *4  nterms
      real *8  xlength 
      complex *16  mpole(0:nterms), phi, z
c-----local variables
      integer *4  k
      complex *16  zk

c      the basic formula for phi is:
c      phi = mpole(0)*log(z) - sum(k=1 to nterms)(mpole(k)*(1/z^k)
c      the scaling factor is included below.
 
c      zk is just set to be z^k within the loop below.
        phi = mpole(0) * (cdlog(z) + dlog(xlength))
        zk = z
        do k = 1, nterms
          phi = phi  +  mpole(k) / zk
          zk = zk * z
        end do

      return
      end


c***********************************************************************
c     the following subroutine is designed to shift an exponential
c     expansion to a new target point, where it can then be converted
c     to a local expansion.
c
c     input:
c
c     beta is the array of plane-wave coefficients being shifted
c
c     nnodes is the order of the plane-wave expansion
c
c     xnodes are the weights associated with the plane wave expansion
c
c     spin is a parameter that determines what direction the exponential
c          expansions decay in
c
c     zshift is the distance by which the expansion is to be shifted 
c                                      (scaled)
c
c     output:
c
c     beta is altered to account for the shift
c
c***********************************************************************
      subroutine expshift_ps(beta, nnodes, xnodes, spin, zshift)
      implicit none
c-----global variables
      integer *4 nnodes
      real *8 xnodes(nnodes)
      complex *16 beta(0:nnodes), spin, zshift
c-----local variables
      integer *4 i
      complex *16 zspin
 
c      now adjust the coefficients to account for the shift.
c      leave the first one unchanged.
 
        zspin = spin*zshift
        do i = 1, nnodes
          beta(i) = beta(i) * cdexp(zspin*xnodes(i))
        end do

      return
      end


c**********************************************************************
      subroutine psppta(a,b,nterms)
c**********************************************************************
      implicit none
      complex *16 a(0:1),b(0:1)
      real *8  c(100,100)
      real *8  zero,pi,zpow(25)
      real *8  conpot(0:50,0:50)
      integer *4  nterms
      integer *4  i,j,ipow,ier
      data  zero/0.0d0/
      save
c
c        this entry converts the given multipole expansion (a) into
c        a power series expansion (b) about the origin which describes
c        the potential due to all well-separated image boxes.
c
c     a      - the original multipole decomposition
c
c     note:    we ignore a(0) = net charge since the resulting
c              potential is not meaningful.
c
c     b      - the evaluated power series coefficients
c
c     nterms - the order of the decompositions a and b
c     ____________________________________________________________
c
c
      pi = dacos(-1.0d0)
      do i=0,nterms-1
         b(i) = zero
         do j=1,nterms-1
            b(i) = b(i) + conpot(i,j)*a(j)
         end do
      end do

         b(0) = b(0) + pi*conjg(a(2))
         b(1) = b(1) - pi*conjg(a(1))
      return
c
c*****************************************************************
c
      entry psppin(ier)
c
c     this is the initialization entry point.
c     it precomputes the binomial coefficients and the potential
c     conversion matrices in full storage format.
c
c     create the binomial coefficients
c                c(m,k) = (m-1) choose (k-1)
c
      c(1,1) = 1.0d0
      do i = 2,100
         c(1,i) = 0.0d0
         c(i,1) = 1.0d0
      end do

      do i = 2,100
        do j = 2,100
         c(i,j)=c(i-1,j-1)+c(i-1,j)
        end do
      end do
c
c----- create zpow, the scaled sums over s of 1/z^k. (the periodic
c      structure has width 64 instead of 1.)
c

      zpow(1) = 0.151212002153897d+00
      zpow(2) = 0.577303536518952d-02
      zpow(3) = 0.134901282797037d-02
      zpow(4) = 0.700330250248559d-04
      zpow(5) = 0.300317628955958d-05
      zpow(6) = 0.242803838628101d-06
      zpow(7) = 0.160994099657098d-07
      zpow(8) = 0.897580666668933d-09
      zpow(9) = 0.570451512710591d-10
      zpow(10)= 0.371803105861787d-11
      zpow(11)= 0.227440251455375d-12
      zpow(12)= 0.140812857755456d-13
      zpow(13)= 0.890974169335439d-15
      zpow(14)= 0.556558380974353d-16
      zpow(15)= 0.346173274816875d-17
      zpow(16)= 0.216781734001496d-18
      zpow(17)= 0.135661847686001d-19
      zpow(18)= 0.846820937604322d-21
      zpow(19)= 0.529224560383524d-22
      zpow(20)= 0.330944477656778d-23
      zpow(21)= 0.206806338091673d-24
      zpow(22)= 0.129232908060200d-25
      zpow(23)= 0.807807171359492d-27
      zpow(24)= 0.504890432194844d-28
      zpow(25)= 0.315537827943134d-29

c
c-----generate the conversion matrices
c
      do i=0,50
       do j=1,50
        conpot(i,j) = 0.0d0
         do ipow = 1,25
           if (i+j .eq. 4*ipow) then
            conpot(i,j) = zpow(ipow)*c(i+j,j)*(-1)**j
           endif
         end do
       end do
      end do
      return
      end



c***********************************************************************
c     the following subroutine is designed to copy the initial tree
c     structure.  in the cases where we need to alter the tree structure
c     and work on a different tree (i am referring here to cases where the
c     boundary conditions are being imposed)  the original structure is 
c     copied and the work is done on the copy.  the original structure is
c     returned to the used, unchanged, on output.
c
c     input:
c
c     levelbox is an array determining the level of each box
c
c     nboxes is the total number of boxes
c
c     irowbox denotes the row of each box
c
c     icolbox denotes the column of each box
c
c     iparentbox denotes the parent of each box
c
c     nlev is the finest level
c
c     ichildbox denotes the four children of each box
c
c     iboxlev is the array in which the boxes are arranged
c
c     nblevel is the total number of boxes per level
c
c     istartlev is the pointer to where each level begins in the
c               iboxlev array
c
c
c     output: (all things in the output refer to the copy)
c
c     levelbox1 is an array determining the level of each box
c
c     nboxes1 is the total number of boxes
c
c     irowbox1 denotes the row of each box
c
c     icolbox1 denotes the column of each box
c
c     iparentbox1 denotes the parent of each box
c
c     nlev1 is the finest level
c
c     ichildbox1 denotes the four children of each box
c
c     iboxlev1 is the array in which the boxes are arranged
c
c     nblevel1 is the total number of boxes per level
c
c     istartlev1 is the pointer to where each level begins in the
c               iboxlev array
c
c***********************************************************************
      subroutine copy(levelbox,levelbox1,icolbox,icolbox1,
     1    irowbox,irowbox1,iparentbox, iparentbox1,
     2    ichildbox,ichildbox1,nboxes,istartlev,istartlev1,
     3    iboxlev, iboxlev1, nblevel, nblevel1, nlev, nlev1,
     4    nboxes1)
      implicit none
c-----global variables
      integer *4  levelbox(1), levelbox1(1)
      integer *4  icolbox(1), icolbox1(1)
      integer *4  irowbox(1), irowbox1(1)
      integer *4  iparentbox(1), iparentbox1(1)
      integer *4  ichildbox(4,1), ichildbox1(4,1)
      integer *4  istartlev(0:1), nblevel(0:1)
      integer *4  iboxlev(1)
      integer *4  istartlev1(0:1), nblevel1(0:1)
      integer *4  iboxlev1(1)
      integer *4  nboxes, nboxes1, nlev, nlev1
c-----local variables
      integer *4  i, j

         nboxes1 =  nboxes
         nlev1   =  nlev
c     for the special cases, let's copy the workspace over
c     to a new one:
      do i = 1, nboxes
        levelbox1(i)   = levelbox(i)
        iparentbox1(i) = iparentbox(i)
        icolbox1(i) = icolbox(i)
        irowbox1(i) = irowbox(i)
        iboxlev1(i) = iboxlev(i)
        do j = 1, 4
         ichildbox1(j,i) = ichildbox(j,i)
        end do
      end do

       do i = 0, nlev
         istartlev1(i) =   istartlev(i)
         nblevel1(i)   =   nblevel(i)
       end do

      return
      end

c***********************************************************************
c     the following subroutine takes one of the boundary conditions 
c     options as input and makes the necessary changes to the initial
c     box so that one of the boundary condition cases can be solved.
c     also inputed are the tree structure and the right hand side,
c     fright.
c
c     input:
c
c     levelbox is an array determining the level of each box
c
c     icolbox denotes the column of each box
c
c     irowbox denotes the row of each box
c
c     iparentbox denotes the parent of each box
c
c     ichildbox denotes the four children of each box
c
c     nboxes is the total number of boxes
c
c     iperiod denotes which of the boundary cases we are in
c
c     nlev is the finest level
c
c     fright is the right hand side defined on the old grid
c
c     output:
c
c     all of the above is altered to reflect the new,
c     four fold tree
c
c***********************************************************************
      subroutine foldover(levelbox,icolbox,irowbox,iparentbox,
     1            ichildbox,nboxes, iperiod, nlev, fright, istartlev,
     2            iboxlev, nblevel)
      implicit none
c-----global variables
      integer *4  levelbox(1)
      integer *4  icolbox(1), irowbox(1)
      integer *4  iparentbox(1), ichildbox(4,1)
      integer *4  nboxes, nlev, iperiod
      integer *4  nblevel(0:1), iboxlev(1)
      integer *4  istartlev(0:1)
      real *8  fright(64,1)
c-----local variables
      integer *4  iplus

      if(iperiod .eq. 2 .or. iperiod .eq. 7)then

        iplus = -1
        call flipup8(levelbox, nboxes,
     1    icolbox, irowbox, nlev,iparentbox,
     2    ichildbox, fright, iplus, istartlev, iboxlev, nblevel)

        iplus = -1
        call flipright8(levelbox, nboxes,
     1    icolbox, irowbox, nlev, iparentbox,
     2    ichildbox, fright, iplus)

      elseif(iperiod .eq. 3 .or. iperiod .eq. 8)then

        iplus = 1
        call flipup8(levelbox, nboxes,
     1    icolbox, irowbox, nlev,iparentbox,
     2    ichildbox, fright, iplus, istartlev, iboxlev, nblevel)

        iplus = 1
        call flipright8(levelbox, nboxes,
     1    icolbox, irowbox, nlev, iparentbox,
     2    ichildbox, fright, iplus)

      elseif(iperiod .eq. 4 .or. iperiod .eq. 9)then

        call shiftup8(levelbox, nboxes,
     1   icolbox, irowbox, nlev, iparentbox,
     2   ichildbox, fright, istartlev, iboxlev, nblevel)

        iplus = -1
        call flipright8(levelbox, nboxes,
     1    icolbox, irowbox, nlev, iparentbox,
     2    ichildbox, fright, iplus)

      elseif(iperiod .eq. 5 .or. iperiod .eq. 10)then

        iplus = 1
        call flipup8(levelbox, nboxes,
     1    icolbox, irowbox, nlev,iparentbox,
     2    ichildbox, fright, iplus, istartlev, iboxlev, nblevel)

        iplus = -1
        call flipright8(levelbox, nboxes,
     1    icolbox, irowbox, nlev, iparentbox,
     2    ichildbox, fright, iplus)


      elseif(iperiod .eq. 6 .or. iperiod .eq. 11)then

        call shiftup8(levelbox, nboxes,
     1   icolbox, irowbox, nlev, iparentbox,
     2   ichildbox, fright, istartlev, iboxlev, nblevel)

        iplus = 1
        call flipright8(levelbox, nboxes,
     1    icolbox, irowbox, nlev, iparentbox,
     2    ichildbox, fright, iplus)

      endif
      return 
      end




c**********************************************************************
c     the following subroutine takes a tree structure and right hand
c     side as input and reflects it over the top, doubling
c     the size of the total number of boxes. 
c
c     input:
c
c     levelbox is an array determining the level of each box
c
c     nboxes is the total number of boxes
c
c     icolbox denotes the column of each box
c
c     irowbox denotes the row of each box
c
c     nlev is the finest level
c
c     iparentbox denotes the parent of each box
c
c     ichildbox denotes the four children of each box
c
c     fright represents the right hand side defined on the leaf nodes
c
c     iplus is a parameter that determines whether or not the sign 
c     of the right hand side is reversed or stays the
c     same in its reflection
c
c     output:
c
c     fright is altered to account for the flipping
c
c**********************************************************************
      subroutine flipup8(levelbox, nboxes, icolbox, irowbox, nlev,
     1      iparentbox, ichildbox, fright, iplus,
     2      istartlev, iboxlev, nblevel)
      implicit none
c-----global variables
      integer *4  nboxes, nlev
      integer *4  icolbox(1), irowbox(1)
      integer *4  ichildbox(4,1), iparentbox(1)
      integer *4  levelbox(1), iplus
      integer *4  nblevel(0:1), iboxlev(1)
      integer *4  istartlev(0:1)
      real *8  fright(64,1)
c-----local variables
      integer *4  i, ibox, ii, j, nside

        nside = 1
        do i = 0, nlev
          do ii = istartlev(i), istartlev(i) + nblevel(i) - 1
           j = iboxlev(ii)
           ibox = j + nboxes
           levelbox(ibox) = levelbox(j)
           icolbox(ibox)  = icolbox(j)

           if (i  .eq. 0)then
            irowbox(ibox) = irowbox(j)
           elseif (i  .ne. 0)then
            irowbox(ibox) = irowbox(j) + 2*(nside/2 - irowbox(j)) + 1
           endif

           irowbox(ibox)  = irowbox(ibox) + nside

           iparentbox(ibox) = iparentbox(j) + nboxes
           if(ichildbox(1,j) .eq. -1)then
              ichildbox(1,ibox) = -1
              ichildbox(2,ibox) = -1
              ichildbox(3,ibox) = -1
              ichildbox(4,ibox) = -1
           else
              ichildbox(4,ibox) = ichildbox(1,j) + nboxes
              ichildbox(3,ibox) = ichildbox(2,j) + nboxes
              ichildbox(2,ibox) = ichildbox(3,j) + nboxes
              ichildbox(1,ibox) = ichildbox(4,j) + nboxes
           endif


           fright(57,ibox)  = dble(iplus)*fright(1,j)
           fright(58,ibox)  = dble(iplus)*fright(2,j)
           fright(59,ibox)  = dble(iplus)*fright(3,j)
           fright(60,ibox)  = dble(iplus)*fright(4,j)
           fright(61,ibox)  = dble(iplus)*fright(5,j)
           fright(62,ibox)  = dble(iplus)*fright(6,j)
           fright(63,ibox)  = dble(iplus)*fright(7,j)
           fright(64,ibox)  = dble(iplus)*fright(8,j)

           fright(49,ibox)  = dble(iplus)*fright(9,j)
           fright(50,ibox)  = dble(iplus)*fright(10,j)
           fright(51,ibox)  = dble(iplus)*fright(11,j)
           fright(52,ibox)  = dble(iplus)*fright(12,j)
           fright(53,ibox)  = dble(iplus)*fright(13,j)
           fright(54,ibox)  = dble(iplus)*fright(14,j)
           fright(55,ibox)  = dble(iplus)*fright(15,j)
           fright(56,ibox)  = dble(iplus)*fright(16,j)

           fright(41,ibox)  = dble(iplus)*fright(17,j)
           fright(42,ibox)  = dble(iplus)*fright(18,j)
           fright(43,ibox)  = dble(iplus)*fright(19,j)
           fright(44,ibox)  = dble(iplus)*fright(20,j)
           fright(45,ibox)  = dble(iplus)*fright(21,j)
           fright(46,ibox)  = dble(iplus)*fright(22,j)
           fright(47,ibox)  = dble(iplus)*fright(23,j)
           fright(48,ibox)  = dble(iplus)*fright(24,j)

           fright(33,ibox)  = dble(iplus)*fright(25,j)
           fright(34,ibox)  = dble(iplus)*fright(26,j)
           fright(35,ibox)  = dble(iplus)*fright(27,j)
           fright(36,ibox)  = dble(iplus)*fright(28,j)
           fright(37,ibox)  = dble(iplus)*fright(29,j)
           fright(38,ibox)  = dble(iplus)*fright(30,j)
           fright(39,ibox)  = dble(iplus)*fright(31,j)
           fright(40,ibox)  = dble(iplus)*fright(32,j)

           fright(25,ibox)  = dble(iplus)*fright(33,j)
           fright(26,ibox)  = dble(iplus)*fright(34,j)
           fright(27,ibox)  = dble(iplus)*fright(35,j)
           fright(28,ibox)  = dble(iplus)*fright(36,j)
           fright(29,ibox)  = dble(iplus)*fright(37,j)
           fright(30,ibox)  = dble(iplus)*fright(38,j)
           fright(31,ibox)  = dble(iplus)*fright(39,j)
           fright(32,ibox)  = dble(iplus)*fright(40,j)

           fright(17,ibox)  = dble(iplus)*fright(41,j)
           fright(18,ibox)  = dble(iplus)*fright(42,j)
           fright(19,ibox)  = dble(iplus)*fright(43,j)
           fright(20,ibox)  = dble(iplus)*fright(44,j)
           fright(21,ibox)  = dble(iplus)*fright(45,j)
           fright(22,ibox)  = dble(iplus)*fright(46,j)
           fright(23,ibox)  = dble(iplus)*fright(47,j)
           fright(24,ibox)  = dble(iplus)*fright(48,j)

           fright(9,ibox)   = dble(iplus)*fright(49,j)
           fright(10,ibox)  = dble(iplus)*fright(50,j)
           fright(11,ibox)  = dble(iplus)*fright(51,j)
           fright(12,ibox)  = dble(iplus)*fright(52,j)
           fright(13,ibox)  = dble(iplus)*fright(53,j)
           fright(14,ibox)  = dble(iplus)*fright(54,j)
           fright(15,ibox)  = dble(iplus)*fright(55,j)
           fright(16,ibox)  = dble(iplus)*fright(56,j)

           fright(1,ibox)  = dble(iplus)*fright(57,j)
           fright(2,ibox)  = dble(iplus)*fright(58,j)
           fright(3,ibox)  = dble(iplus)*fright(59,j)
           fright(4,ibox)  = dble(iplus)*fright(60,j)
           fright(5,ibox)  = dble(iplus)*fright(61,j)
           fright(6,ibox)  = dble(iplus)*fright(62,j)
           fright(7,ibox)  = dble(iplus)*fright(63,j)
           fright(8,ibox)  = dble(iplus)*fright(64,j)

         end do
         nside = 2*nside
        end do
        nboxes = nboxes + nboxes

      return
      end


c**************************************************************************
c     the following subroutine takes a tree structure and right hand
c     side as input and shifts it up, doubling the size of the total 
c     number of boxes.
c
c     input:
c
c     levelbox is an array determining the level of each box
c
c     nboxes is the total number of boxes
c
c     icolbox denotes the column of each box
c
c     irowbox denotes the row of each box
c
c     nlev is the finest level
c
c     iparentbox denotes the parent of each box
c
c     ichildbox denotes the four children of each box
c
c     fright represents the right hand side defined on the leaf nodes
c
c     iplus is a parameter that determines whether or not the sign 
c     of the right hand side is reversed or stays the
c     same upon being shifted
c
c     output:
c
c     fright is altered to account for the flipping
c
c**************************************************************************
      subroutine shiftup8(levelbox, nboxes, icolbox, irowbox, nlev,
     1                     iparentbox, ichildbox,fright,
     2                     istartlev, iboxlev, nblevel)
      implicit none
c-----global variables
      integer *4  nboxes, nlev
      integer *4  icolbox(1), irowbox(1)
      integer *4  ichildbox(4,1), iparentbox(1)
      integer *4  levelbox(1)
      integer *4  nblevel(0:1), iboxlev(1)
      integer *4  istartlev(0:1)
      real *8  fright(64,1)
c-----local variables
      integer *4  i, ibox, ii, j, nside, jj

        nside = 1
        do i = 0, nlev
         do jj = istartlev(i), istartlev(i) + nblevel(i) - 1
           j = iboxlev(jj)
           ibox = j + nboxes
           levelbox(ibox) = levelbox(j)
           icolbox(ibox)  = icolbox(j)
           irowbox(ibox)  = irowbox(j) + nside

           iparentbox(ibox) = iparentbox(j) + nboxes
           if(ichildbox(1,j) .eq. -1)then
              ichildbox(1,ibox) = -1
              ichildbox(2,ibox) = -1
              ichildbox(3,ibox) = -1
              ichildbox(4,ibox) = -1
           else
              ichildbox(1,ibox) = ichildbox(1,j) + nboxes
              ichildbox(2,ibox) = ichildbox(2,j) + nboxes
              ichildbox(3,ibox) = ichildbox(3,j) + nboxes
              ichildbox(4,ibox) = ichildbox(4,j) + nboxes
           endif


           do ii = 1, 64
             fright(ii,ibox) = fright(ii,j)
           end do

         end do
        nside = 2*nside
        end do
        nboxes = nboxes + nboxes

      return
      end


c**************************************************************************
c     the following subroutine takes a tree structure and right hand
c     side as input and reflects it over the right side, doubling
c     the size of the total number of boxes.  iplus determines whether
c     or not the sign of the right hand side is reversed or stays the
c     same in its reflection.
c     input:
c
c     levelbox is an array determining the level of each box
c
c     nboxes is the total number of boxes
c
c     icolbox denotes the column of each box
c
c     irowbox denotes the row of each box
c
c     nlev is the finest level
c
c     iparentbox denotes the parent of each box
c
c     ichildbox denotes the four children of each box
c
c     fright represents the right hand side defined on the leaf nodes
c
c     iplus is a parameter that determines whether or not the sign 
c     of the right hand side is reversed or stays the same in its
c     reflection
c
c     output:
c
c     fright is altered to account for the flipping
c
c**************************************************************************
      subroutine flipright8(levelbox, nboxes, icolbox, irowbox, nlev,
     1                    iparentbox, ichildbox, fright, iplus)
      implicit none
c-----global variables
      integer *4  nboxes, nlev
      integer *4  icolbox(1), irowbox(1)
      integer *4  levelbox(1), iparentbox(1)
      integer *4  ichildbox(4,1), iplus
      real *8  fright(64,1)
c-----local variables
      integer *4  i, ibox, j, nside

        do i = 0, nlev
         nside = 2**i
         do 100 j = 1, nboxes
           if(levelbox(j) .ne. i)goto 100
           ibox = j + nboxes
           levelbox(ibox) = levelbox(j)
           irowbox(ibox)  = irowbox(j)

           if (i  .eq. 0)then
            icolbox(ibox) = icolbox(j)
           elseif (i  .ne. 0)then
            icolbox(ibox) = icolbox(j) + 2*(nside/2 - icolbox(j)) + 1
           endif

           icolbox(ibox)  = icolbox(ibox) + nside

           iparentbox(ibox) = iparentbox(j) + nboxes
           if(ichildbox(1,j) .eq. -1)then
              ichildbox(1,ibox) = -1
              ichildbox(2,ibox) = -1
              ichildbox(3,ibox) = -1
              ichildbox(4,ibox) = -1
           else
              ichildbox(2,ibox) = ichildbox(1,j) + nboxes
              ichildbox(1,ibox) = ichildbox(2,j) + nboxes
              ichildbox(4,ibox) = ichildbox(3,j) + nboxes
              ichildbox(3,ibox) = ichildbox(4,j) + nboxes
           endif


           fright(8,ibox)  = dble(iplus)* fright(1,j)
           fright(7,ibox)  = dble(iplus)* fright(2,j)
           fright(6,ibox)  = dble(iplus)* fright(3,j)
           fright(5,ibox)  = dble(iplus)* fright(4,j)
           fright(4,ibox)  = dble(iplus)* fright(5,j)
           fright(3,ibox)  = dble(iplus)* fright(6,j)
           fright(2,ibox)  = dble(iplus)* fright(7,j)
           fright(1,ibox)  = dble(iplus)* fright(8,j)

           fright(16,ibox)  = dble(iplus)*fright(9,j)
           fright(15,ibox)  = dble(iplus)*fright(10,j)
           fright(14,ibox)  = dble(iplus)*fright(11,j)
           fright(13,ibox)  = dble(iplus)*fright(12,j)
           fright(12,ibox)  = dble(iplus)*fright(13,j)
           fright(11,ibox)  = dble(iplus)*fright(14,j)
           fright(10,ibox)  = dble(iplus)*fright(15,j)
           fright(9,ibox)   = dble(iplus)*fright(16,j)

           fright(24,ibox)  = dble(iplus)*fright(17,j)
           fright(23,ibox)  = dble(iplus)*fright(18,j)
           fright(22,ibox)  = dble(iplus)*fright(19,j)
           fright(21,ibox)  = dble(iplus)*fright(20,j)
           fright(20,ibox)  = dble(iplus)*fright(21,j)
           fright(19,ibox)  = dble(iplus)*fright(22,j)
           fright(18,ibox)  = dble(iplus)*fright(23,j)
           fright(17,ibox)  = dble(iplus)*fright(24,j)

           fright(32,ibox)  = dble(iplus)*fright(25,j)
           fright(31,ibox)  = dble(iplus)*fright(26,j)
           fright(30,ibox)  = dble(iplus)*fright(27,j)
           fright(29,ibox)  = dble(iplus)*fright(28,j)
           fright(28,ibox)  = dble(iplus)*fright(29,j)
           fright(27,ibox)  = dble(iplus)*fright(30,j)
           fright(26,ibox)  = dble(iplus)*fright(31,j)
           fright(25,ibox)  = dble(iplus)*fright(32,j)

           fright(40,ibox)  = dble(iplus)*fright(33,j)
           fright(39,ibox)  = dble(iplus)*fright(34,j)
           fright(38,ibox)  = dble(iplus)*fright(35,j)
           fright(37,ibox)  = dble(iplus)*fright(36,j)
           fright(36,ibox)  = dble(iplus)*fright(37,j)
           fright(35,ibox)  = dble(iplus)*fright(38,j)
           fright(34,ibox)  = dble(iplus)*fright(39,j)
           fright(33,ibox)  = dble(iplus)*fright(40,j)

           fright(48,ibox)  = dble(iplus)*fright(41,j)
           fright(47,ibox)  = dble(iplus)*fright(42,j)
           fright(46,ibox)  = dble(iplus)*fright(43,j)
           fright(45,ibox)  = dble(iplus)*fright(44,j)
           fright(44,ibox)  = dble(iplus)*fright(45,j)
           fright(43,ibox)  = dble(iplus)*fright(46,j)
           fright(42,ibox)  = dble(iplus)*fright(47,j)
           fright(41,ibox)  = dble(iplus)*fright(48,j)

           fright(56,ibox)  = dble(iplus)*fright(49,j)
           fright(55,ibox)  = dble(iplus)*fright(50,j)
           fright(54,ibox)  = dble(iplus)*fright(51,j)
           fright(53,ibox)  = dble(iplus)*fright(52,j)
           fright(52,ibox)  = dble(iplus)*fright(53,j)
           fright(51,ibox)  = dble(iplus)*fright(54,j)
           fright(50,ibox)  = dble(iplus)*fright(55,j)
           fright(49,ibox)  = dble(iplus)*fright(56,j)

           fright(64,ibox)  = dble(iplus)*fright(57,j)
           fright(63,ibox)  = dble(iplus)*fright(58,j)
           fright(62,ibox)  = dble(iplus)*fright(59,j)
           fright(61,ibox)  = dble(iplus)*fright(60,j)
           fright(60,ibox)  = dble(iplus)*fright(61,j)
           fright(59,ibox)  = dble(iplus)*fright(62,j)
           fright(58,ibox)  = dble(iplus)*fright(63,j)
           fright(57,ibox)  = dble(iplus)*fright(64,j)

100      continue
        end do
        nboxes = nboxes + nboxes

      return
      end
 
c**************************************************************************
c     the following subroutine is set up to merge all of the boxes (after
c     the data structure has been copied over) into one new tree.
c
c     input:
c
c     levelbox is an array determining the level of each box
c
c     nboxes is the total number of boxes
c
c     nlev is the finest level
c
c     icolbox denotes the column of each box
c
c     irowbox denotes the row of each box
c
c     ichildbox denotes the four children of each box
c
c     iparentbox denotes the parent of each box
c
c     nblevel is the total number of boxes per level
c
c     iboxlev is the array in which the boxes are arranged
c
c     istartlev is the pointer to where each level begins in the
c               iboxlev array
c
c     output:
c
c     all inputs may be altered to reflect the new tree
c
c**************************************************************************
      subroutine merge(levelbox,nboxes,nlev,icolbox,irowbox,
     1           ichildbox, iparentbox,nblevel,iboxlev,istartlev)
      implicit none
c-----global variables
      integer *4  levelbox(1), nboxes, nlev
      integer *4  icolbox(1), irowbox(1)
      integer *4  ichildbox(4,1), iparentbox(1)
      integer *4  nblevel(0:1), iboxlev(1), istartlev(0:1)
c-----local variables
      integer *4  ibox, ncntr, i, j
      integer *4  icol, irow, l, ll
      integer *4  icoltest, irowtest
      integer *4  istart, ii

c       because the reflection routines consist of shifting the
c       parent box (at level 0) everything must be shifted down
c       by one level.
        do ibox = 1, nboxes
          levelbox(ibox) = levelbox(ibox) + 1
        end do
        nlev = nlev + 1

c       now everybody has been moved down by a level, so
c       let's place a box at level zero that is the parent
c       box of the boxes that are now at level one.  by making
c       the boxes fit inside a square of side one, we can
c       just feed this into the old periodic subroutine and
c       then recover the correct function values at the end
c       by inverting the process performed in this subroutine:
        ibox = nboxes + 1
        levelbox(ibox) = 0
        icolbox(ibox) = 1
        irowbox(ibox) = 1
        iparentbox(ibox) = -1

        nboxes = nboxes + 1

c       now resort all of the boxes
        do i = nlev, 1, -1
          nblevel(i) = nblevel(i-1)
          istartlev(i) = istartlev(i-1) + 1
        end do
        nblevel(0) = 1
        istartlev(0) = 1

        ncntr = 1
        do i = 0, nlev
          do j = 1, nboxes
            if(levelbox(j) .eq. i)then
             iboxlev(ncntr) = j
             ncntr = ncntr + 1
            endif
          end do
        end do


         
          istart = istartlev(0)
          do ii = istart, istart + nblevel(0) - 1
           j = iboxlev(ii)
           ichildbox(1,j) = -1
           ichildbox(2,j) = -1
           ichildbox(3,j) = -1
           ichildbox(4,j) = -1

            icoltest = 2*(icolbox(j) - 1) + 1
            irowtest = 2*(irowbox(j) - 1) + 1

            do ll = istartlev(1),istartlev(1) + nblevel(1) - 1
              l = iboxlev(ll)

              icol = icolbox(l)
              irow = irowbox(l)
              if(icol .eq. icoltest .and. irow .eq. irowtest)then
                ichildbox(4,j) = l
              elseif(icol .eq. icoltest + 1
     1                        .and. irow .eq. irowtest + 1)then
                ichildbox(2,j) = l
              elseif(icol .eq. icoltest
     1                        .and. irow .eq. irowtest + 1)then
                ichildbox(1,j) = l
              elseif(icol .eq. icoltest + 1
     1                        .and. irow .eq. irowtest)then
                ichildbox(3,j) = l
             endif
           end do
          end do


        istart = istartlev(1)
        do ii = istart, istart + nblevel(1) - 1
          j = iboxlev(ii)
          iparentbox(j) = iboxlev(istartlev(0))
        end do

      return
      end





c**************************************************************************
c     the following subroutine maps from four function values on the side
c     of the box (representing charge densities) to four polynomial 
c     coefficients approximating the polynomial.
c
c     input:
c
c     f is the array of 6 function values arranged from left to right
c     
c     xlength is the length of a side of the box being considered
c
c     b is a precomputed matrix that maps from the 6 function values to
c       the 6 coefficients values
c
c     output:
c
c     coeffs contains the coefficients approximating the polynomial
c
c**************************************************************************
      subroutine interpolate8side(f ,coeff)
      implicit none
c-----global variables
      real *8  f(8), coeff(0:7)
c-----local variables
      integer *4  j
      real *8  ftemp(8,1)
      real *8  coefftemp(8,1)
      real *8  wsave2(1000)

c       initialize wsave2 array (needed by getcoeff).
        call chxcin(8,wsave2)

        do j = 1, 8
            ftemp(j,1) = f(j)
        end do

c       compute chebyshev transforms
        call getcoeff1(ftemp,coefftemp,wsave2)

c       now set these values to out coefficients
        do j = 0, 7
            coeff(j) = coefftemp(j+1,1)
          end do
      return
      end



c**************************************************************************
c     periodization module
c**************************************************************************
c     the following subroutine maps from four polynomial coefficients to
c     the multipole expansion caused by a single layer potential on the
c     top of the box.  the multipole expansion is shifted within this 
c     routine so that it lies at the center of the box.
c
c     input:
c
c     scoeffs is are the coefficients approximating the charge density
c     
c     nterms is the order of the multipole expansion
c
c     xlength is the length of a side of the box being considered
c
c     c is an array of binomial coefficients
c
c     output:
c
c     mpole contains the approximating multipole expansion
c
c**************************************************************************
      subroutine singletoptomult8(scoeffs,nterms,c,xlength,mpole) 
      implicit none
c-----global variables
      integer *4  nterms
      real *8  scoeffs(0:7)
      real *8  c(2*nterms,2*nterms)
      real *8  xlength
      complex *16  mpole(0:nterms)
c-----local variables
      integer *4  i, k, m
      real *8  scoeffscaled(0:7)
      complex *16  z0, z00, z0p(0:60), z0p2(0:60)
      complex *16  cd, cdd
      complex *16  anew(0:60), imag
      complex *16  sum
      data imag/(0.0,1.0)/

      scoeffscaled(0) = scoeffs(0) * xlength
      scoeffscaled(1) = scoeffs(1) * xlength
      scoeffscaled(2) = scoeffs(2) * xlength
      scoeffscaled(3) = scoeffs(3) * xlength
      scoeffscaled(4) = scoeffs(4) * xlength
      scoeffscaled(5) = scoeffs(5) * xlength
      scoeffscaled(6) = scoeffs(6) * xlength
      scoeffscaled(7) = scoeffs(7) * xlength

      do i = 0, nterms
         mpole(i) = 0.0d0
      end do

      z0 = imag * .5d0
      z0p(0) = 1.0d0
      z00 = z0
      cd = 1.0d0 / z0
      cdd = cd
      z0p2(0) = 1.0d0
      do i = 1, nterms
         z0p(i)  = z00
         z0p2(i) = cdd
         cdd     = cdd*cd
         z00     = z00*z0
      end do

c     first, get the zero term in the multipole expansion
c     (note that even though we are summing over all of the
c     coefficients, that we only need be concerned with 0 and
c     2, because the integrals in the other two cases are zero.)
      mpole(0) = scoeffscaled(0) 
     1         + scoeffscaled(2) * (2.0d0/3.0d0 - 1.0d0)
     2         + scoeffscaled(4) * (8.0d0/5.0d0 - 8.0d0/3.0d0 + 1.0d0)
     3         + scoeffscaled(6) * (32.0d0/7.0d0 - 48.0d0/5.0d0 
     4                           +  18.0d0/3.0d0 - 1.0d0)


      do k = 1, nterms
        sum = 0.0d0
c         we only need to concern ourselves with the cases
c         where i+k is even:
          if(mod(k,2) .eq. 0)then

           sum = sum + scoeffscaled(0) / dble(k+1)

           sum = sum + scoeffscaled(2) * 
     1         (2.0d0/dble(k+3) - 1.0d0/dble(k+1))

           sum = sum + scoeffscaled(4) *
     1       (8.0d0/dble(k+5) - 8.0d0/dble(k+3) + 1.0d0/dble(k+1))

           sum = sum + scoeffscaled(6) *
     1       (32.0d0/dble(k+7) - 48.0d0/dble(k+5) 
     2      + 18.0d0/dble(k+3) -  1.0d0/dble(k+1))

          else

           sum = sum + scoeffscaled(1) / dble(k+2)

           sum = sum + scoeffscaled(3) *
     1         (4.0d0/dble(k+4) - 3.0d0/dble(k+2))

           sum = sum + scoeffscaled(5) *
     1      (16.0d0/dble(k+6) - 20.0d0/dble(k+4) + 5.0d0/dble(k+2))

           sum = sum + scoeffscaled(7) *
     1      (64.0d0/dble(k+8) - 112.0d0/dble(k+6) 
     2     + 56.0d0/dble(k+4) -   7.0d0/dble(k+2))

          endif
        anew(k) = -sum * z0p2(k) / (dble(k)*2.0d0**k)
      end do

      do m=1, nterms
         do k=1,m
            mpole(m) = mpole(m) + anew(k)*c(m,k)
         end do
      end do

      do m = 1, nterms
        mpole(m) = mpole(m)*z0p(m) - mpole(0)*z0p(m)/dble(m)
      end do
      return
      end


c**************************************************************************
c     the following subroutine maps from four polynomial coefficients to
c     the multipole expansion caused by a single layer potential on the
c     side of the box.  the multipole expansion is shifted within this 
c     routine so that it lies at the center of the box.
c
c     input:
c
c     scoeffs is are the coefficients approximating the charge density
c     
c     nterms is the order of the multipole expansion 
c
c     xlength is the length of a side of the box being considered
c
c     c is an array of binomial coefficients
c
c     output:
c
c     mpole contains the approximating multipole expansion
c
c**************************************************************************
      subroutine singlesidetomult8(scoeffs,nterms,c,xlength,mpole) 
      implicit none
c-----global variables
      integer *4  nterms
      real *8  scoeffs(0:7)
      real *8  c(2*nterms,2*nterms)
      real *8  xlength
      complex *16  mpole(0:nterms)
c-----local variables
      integer *4  i, k, m
      real *8  scoeffscaled(0:7)
      complex *16  z0, z00, z0p(0:60), z0p2(0:60)
      complex *16  cd, cdd
      complex *16  anew(0:60), imag
      complex *16  sum
      data imag/(0.0,1.0)/

      scoeffscaled(0) = scoeffs(0) * xlength
      scoeffscaled(1) = scoeffs(1) * xlength
      scoeffscaled(2) = scoeffs(2) * xlength
      scoeffscaled(3) = scoeffs(3) * xlength
      scoeffscaled(4) = scoeffs(4) * xlength
      scoeffscaled(5) = scoeffs(5) * xlength
      scoeffscaled(6) = scoeffs(6) * xlength
      scoeffscaled(7) = scoeffs(7) * xlength

      do i = 0, nterms
         mpole(i) = 0.0d0
      end do

      z0 = .5d0
      z0p(0) = 1.0d0
      z00 = z0
      cd = 1.0d0 / z0
      cdd = cd
      z0p2(0) = 1.0d0
      do i = 1, nterms
         z0p(i)  = z00
         z0p2(i) = cdd
         cdd     = cdd*cd
         z00     = z00*z0
      end do

c     first, get the zero term in the multipole expansion
c     (note that even though we are summing over all of the
c     coefficients, that we only need be concerned with 0 and
c     2, because the integrals in the other two cases are zero.)
      mpole(0) = scoeffscaled(0) 
     1         + scoeffscaled(2) * (2.0d0/3.0d0 - 1.0d0)
     2         + scoeffscaled(4) * (8.0d0/5.0d0 - 8.0d0/3.0d0 + 1.0d0)
     3         + scoeffscaled(6) * (32.0d0/7.0d0 - 48.0d0/5.0d0 
     4                           +  18.0d0/3.0d0 - 1.0d0)


      do k = 1, nterms
        sum = 0.0d0
c         we only need to concern ourselves with the cases
c         where i+k is even:
          if(mod(k,2) .eq. 0)then

           sum = sum + scoeffscaled(0) / dble(k+1)

           sum = sum + scoeffscaled(2) * 
     1         (2.0d0/dble(k+3) - 1.0d0/dble(k+1))

           sum = sum + scoeffscaled(4) *
     1       (8.0d0/dble(k+5) - 8.0d0/dble(k+3) + 1.0d0/dble(k+1))

           sum = sum + scoeffscaled(6) *
     1       (32.0d0/dble(k+7) - 48.0d0/dble(k+5) 
     2      + 18.0d0/dble(k+3) -  1.0d0/dble(k+1))

          else

           sum = sum + scoeffscaled(1) / dble(k+2)

           sum = sum + scoeffscaled(3) *
     1         (4.0d0/dble(k+4) - 3.0d0/dble(k+2))

           sum = sum + scoeffscaled(5) *
     1      (16.0d0/dble(k+6) - 20.0d0/dble(k+4) + 5.0d0/dble(k+2))

           sum = sum + scoeffscaled(7) *
     1      (64.0d0/dble(k+8) - 112.0d0/dble(k+6) 
     2     + 56.0d0/dble(k+4) -   7.0d0/dble(k+2))

          endif
        anew(k) = -sum * z0p2(k) * imag**k / (dble(k)*2.0d0**k)
      end do

      do m=1, nterms
         do k=1,m
            mpole(m) = mpole(m) + anew(k)*c(m,k)
         end do
      end do

      do m = 1, nterms
        mpole(m) = mpole(m)*z0p(m) - mpole(0)*z0p(m)/dble(m)
      end do
      return
      end


c**************************************************************************
c     the following subroutine maps from four polynomial coefficients to
c     the multipole expansion caused by a double potential on the side of
c     the box.  the multipole expansion is shifted within this routine so
c     that it lies at the center of the box.
c
c     input:
c
c     dcoeffs is are the coefficients approximating the charge density
c     
c     nterms is the order of the multipole expansion 
c
c     xlength is the length of a side of the box being considered
c
c     c is an array of binomial coefficients
c
c     output:
c
c     mpole contains the approximating multipole expansion
c
c**************************************************************************
      subroutine doublesidetomult8(dcoeffs,nterms,c,mpole)
      implicit none
c-----global variables
      integer *4  nterms
      real *8  dcoeffs(0:7)
      real *8  c(2*nterms,2*nterms)
      complex *16  mpole(0:nterms)
c-----local variables
      integer *4  i, k, m
      complex *16  z0, z00, z0p(0:60), z0p2(0:60)
      complex *16  cd, cdd
      complex *16  anew(0:60), imag
      complex *16  sum
      data imag/(0.0,1.0)/

      do i = 0, nterms
         mpole(i) = 0.0d0
      end do

      z0 = .5d0
      z0p(0) = 1.0d0
      z00 = z0
      cd = 1.0d0 / z0
      cdd = cd
      z0p2(0) = 1.0d0
      do i = 1, nterms
         z0p(i)  = z00
         z0p2(i) = cdd
         cdd     = cdd*cd
         z00     = z00*z0
      end do

c     first, get the zero term in the multipole expansion
c     (note that even though we are summing over all of the
c     coefficients, that we only need be concerned with 0 and
c     2, because the integrals in the other two cases are zero.)
      mpole(0) = dcoeffs(0) 
     1         + dcoeffs(2) * (2.0d0/3.0d0 - 1.0d0)
     2         + dcoeffs(4) * (8.0d0/5.0d0 - 8.0d0/3.0d0 + 1.0d0)
     3         + dcoeffs(6) * (32.0d0/7.0d0 - 48.0d0/5.0d0 
     4                           +  18.0d0/3.0d0 - 1.0d0)


      do k = 1, nterms
        sum = 0.0d0
c         we only need to concern ourselves with the cases
c         where i+k is even:
          if(mod(k,2) .eq. 0)then

           sum = sum + dcoeffs(0) / dble(k+1)

           sum = sum + dcoeffs(2) * 
     1         (2.0d0/dble(k+3) - 1.0d0/dble(k+1))

           sum = sum + dcoeffs(4) *
     1       (8.0d0/dble(k+5) - 8.0d0/dble(k+3) + 1.0d0/dble(k+1))

           sum = sum + dcoeffs(6) *
     1       (32.0d0/dble(k+7) - 48.0d0/dble(k+5) 
     2      + 18.0d0/dble(k+3) -  1.0d0/dble(k+1))

          else

           sum = sum + dcoeffs(1) / dble(k+2)

           sum = sum + dcoeffs(3) *
     1         (4.0d0/dble(k+4) - 3.0d0/dble(k+2))

           sum = sum + dcoeffs(5) *
     1      (16.0d0/dble(k+6) - 20.0d0/dble(k+4) + 5.0d0/dble(k+2))

           sum = sum + dcoeffs(7) *
     1      (64.0d0/dble(k+8) - 112.0d0/dble(k+6) 
     2     + 56.0d0/dble(k+4) -   7.0d0/dble(k+2))

          endif
        anew(k) = -sum * z0p2(k) * imag**k / (dble(k)*2.0d0**k)
      end do

      do m=1, nterms
         do k=1,m
            mpole(m) = mpole(m) + anew(k)*c(m,k)
         end do
      end do

      do m = 1, nterms
        mpole(m) = mpole(m)*z0p(m) - mpole(0)*z0p(m)/dble(m)
      end do

      do i = nterms, 2, -1
        mpole(i) = -dble(i-1)*mpole(i-1)
      end do
      mpole(1) = mpole(0)
      mpole(0) = 0.0d0
      return
      end


c**************************************************************************
c     the following subroutine maps from four polynomial coefficients to
c     the multipole expansion caused by a double potential on the top of
c     the box.  the multipole expansion is shifted within this routine so
c     that it lies at the center of the box.
c
c     input:
c
c     dcoeffs is are the coefficients approximating the charge density
c
c     nterms is the order of the multipole expansion
c
c     xlength is the length of a side of the box being considered
c
c     c is an array of binomial coefficients
c
c     output:
c
c     mpole contains the approximating multipole expansion
c
c**************************************************************************
      subroutine doubletoptomult8(dcoeffs,nterms,c,mpole)
      implicit none
c-----global variables
      integer *4  nterms
      real *8  dcoeffs(0:7)
      real *8  c(2*nterms,2*nterms)
      complex *16  mpole(0:nterms)
c-----local variables
      integer *4  i, k, m
      complex *16  z0, z00, z0p(0:60), z0p2(0:60)
      complex *16  cd, cdd
      complex *16  anew(0:60), imag
      complex *16  sum
      data imag/(0.0,1.0)/

      do i = 0, nterms
         mpole(i) = 0.0d0
      end do

      z0 = imag * .5d0
      z0p(0) = 1.0d0
      z00=z0
      cd = 1.0d0 / z0
      cdd = cd
      z0p2(0) = 1.0d0
      do i = 1, nterms
         z0p(i)  = z00
         z0p2(i) = cdd
         cdd     = cdd*cd
         z00     = z00*z0
      end do

c     first, get the zero term in the multipole expansion
c     (note that even though we are summing over all of the
c     coefficients, that we only need be concerned with 0 and
c     2, because the integrals in the other two cases are zero.)
      mpole(0) = dcoeffs(0) 
     1         + dcoeffs(2) * (2.0d0/3.0d0 - 1.0d0)
     2         + dcoeffs(4) * (8.0d0/5.0d0 - 8.0d0/3.0d0 + 1.0d0)
     3         + dcoeffs(6) * (32.0d0/7.0d0 - 48.0d0/5.0d0 
     4                           +  18.0d0/3.0d0 - 1.0d0)


      do k = 1, nterms
        sum = 0.0d0
c         we only need to concern ourselves with the cases
c         where i+k is even:
          if(mod(k,2) .eq. 0)then

           sum = sum + dcoeffs(0) / dble(k+1)

           sum = sum + dcoeffs(2) * 
     1         (2.0d0/dble(k+3) - 1.0d0/dble(k+1))

           sum = sum + dcoeffs(4) *
     1       (8.0d0/dble(k+5) - 8.0d0/dble(k+3) + 1.0d0/dble(k+1))

           sum = sum + dcoeffs(6) *
     1       (32.0d0/dble(k+7) - 48.0d0/dble(k+5) 
     2      + 18.0d0/dble(k+3) -  1.0d0/dble(k+1))

          else

           sum = sum + dcoeffs(1) / dble(k+2)

           sum = sum + dcoeffs(3) *
     1         (4.0d0/dble(k+4) - 3.0d0/dble(k+2))

           sum = sum + dcoeffs(5) *
     1      (16.0d0/dble(k+6) - 20.0d0/dble(k+4) + 5.0d0/dble(k+2))

           sum = sum + dcoeffs(7) *
     1      (64.0d0/dble(k+8) - 112.0d0/dble(k+6) 
     2     + 56.0d0/dble(k+4) -   7.0d0/dble(k+2))

          endif
        anew(k) = -sum * z0p2(k) / (dble(k)*2.0d0**k)
      end do

      do m=1, nterms
         do k=1,m
            mpole(m) = mpole(m) + anew(k)*c(m,k)
         end do
      end do

      do m = 1, nterms
        mpole(m) = mpole(m)*z0p(m) - mpole(0)*z0p(m)/dble(m)
      end do

      do i = nterms, 2, -1
        mpole(i) = -dble(i-1)*imag*mpole(i-1)
      end do
      mpole(1) = imag*mpole(0)
      mpole(0) = 0.0d0
      return
      end


c*****************************************************************************
c     the following subroutine does the local work (just outgoing) involving
c     layer potentials between two boxes that are of the same size.  
c
c     input:
c
c     coeffstop is the array of  basis functions coefficients for the single
c               layer potential on the top
c
c     coeffsside is the array of  basis functions coefficients for the single
c               layer potential on the side
c
c     coeffdtop is the array of  basis functions coefficients for the double
c               layer potential on the top
c
c     coeffdside is the array of  basis functions coefficients for the double
c               layer potential on the side
c
c     ndeg is the degree of the approximating polynomial
c
c     nb denotes the colleague number being considered
c
c     t is a set of precomputed scalings (set in the adapfmm6 routine)
c
c     wdtop is the table of weights needed to determine the local 
c           contributions for the double layer on the top
c
c     wdside is the table of weights needed to determine the local 
c           contributions for the double layer on the side
c
c     wsside is the table of weights needed to determine the local 
c           contributions for the single layer on the side
c
c     wstop is the table of weights needed to determine the local 
c           contributions for the single layer on the top
c
c     output:
c
c     pot is altered to account for the local contribution
c
c*****************************************************************************
      subroutine colloclay8(pot,coeffstop, 
     1           coeffdtop,coeffsside,coeffdside,
     2           ndeg,nb,t,wdtop,wdside,
     3           wsside,wstop,iswitch,
     3           dsideswitch,dtopswitch,
     4           ssideswitch,stopswitch)
      implicit none
c-----global variables
      integer *4  ndeg, nb, iswitch
      integer *4  dsideswitch, dtopswitch
      integer *4  ssideswitch, stopswitch
      real *8  pot(64)
      real *8  coeffstop(0:ndeg)
      real *8  coeffdtop(0:ndeg)
      real *8  coeffsside(0:ndeg)
      real *8  coeffdside(0:ndeg)
      real *8  t(10)
      real *8  wdtop(9,64,8)
      real *8  wdside(9,64,8)
      real *8  wsside(9,64,8)
      real *8  wstop(9,64,8)
c-----local variables
      integer *4  i, j
      real *8  c(8), clog(8)

      if(iswitch .eq. 0)then
        return
      endif


        if(stopswitch .eq. 1)then
         c(1) = coeffstop(0) / t(1)
         c(2) = coeffstop(1) / t(1)
         c(3) = coeffstop(2) / t(1)
         c(4) = coeffstop(3) / t(1)
         c(5) = coeffstop(4) / t(1)
         c(6) = coeffstop(5) / t(1)
         c(7) = coeffstop(6) / t(1)
         c(8) = coeffstop(7) / t(1)

         clog(1)  = c(1) * t(3)
         clog(2)  = 0.0d0
         clog(3)  = c(3) * t(4)
         clog(4)  = 0.0d0
         clog(5)  = c(5) * t(5)
         clog(6)  = 0.0d0
         clog(7)  = c(7) * t(6)
         clog(8)  = 0.0d0
       
         do i = 1,64
          do j = 1,8
            pot(i) = pot(i) + c(j)*wstop(nb,i,j) + clog(j)
          enddo
         enddo
        endif


        if(ssideswitch .eq. 1)then
         c(1) = coeffsside(0) / t(1)
         c(2) = coeffsside(1) / t(1)
         c(3) = coeffsside(2) / t(1)
         c(4) = coeffsside(3) / t(1)
         c(5) = coeffsside(4) / t(1)
         c(6) = coeffsside(5) / t(1)
         c(7) = coeffsside(6) / t(1)
         c(8) = coeffsside(7) / t(1)

         clog(1)  = c(1) * t(3)
         clog(2)  = 0.0d0
         clog(3)  = c(3) * t(4)
         clog(4)  = 0.0d0
         clog(5)  = c(5) * t(5)
         clog(6)  = 0.0d0
         clog(7)  = c(7) * t(6)
         clog(8)  = 0.0d0

         do i = 1,64
          do j = 1,8
            pot(i) = pot(i) + c(j)*wsside(nb,i,j) + clog(j)
          enddo
         enddo
        endif


        if(dtopswitch .eq. 1)then
         c(1) = coeffdtop(0)
         c(2) = coeffdtop(1)
         c(3) = coeffdtop(2)
         c(4) = coeffdtop(3)
         c(5) = coeffdtop(4)
         c(6) = coeffdtop(5)
         c(7) = coeffdtop(6)
         c(8) = coeffdtop(7)

         do i = 1,64
          do j = 1,8
            pot(i) = pot(i) + c(j)*wdtop(nb,i,j)
          enddo
         enddo
        endif


        if(dsideswitch .eq. 1)then
         c(1) = coeffdside(0)
         c(2) = coeffdside(1)
         c(3) = coeffdside(2)
         c(4) = coeffdside(3)
         c(5) = coeffdside(4)
         c(6) = coeffdside(5)
         c(7) = coeffdside(6)
         c(8) = coeffdside(7)

         do i = 1,64
          do j = 1,8
            pot(i) = pot(i) + c(j)*wdside(nb,i,j)
          enddo
         enddo
        endif
       return
       end



c*****************************************************************************
c     the following subroutine does the local work involving layer potentials
c     that figures the potential in a smaller box due to a big box that is
c     touching it.
c     the boxes are only allowed to be one level apart.
c
c     input:
c
c     coeffstop is the array of  basis functions coefficients for the single
c                 layer potential on the top
c
c     coeffsside is the array of  basis functions coefficients for the single
c                 layer potential on the side
c
c     coeffdtop is the array of  basis functions coefficients for the double
c                 layer potential on the top
c
c     coeffdside is the array of  basis functions coefficients for the double
c                 layer potential on the side
c
c     ndeg is the degree of the approximating polynomial
c
c     iflag denotes the colleague number being considered
c
c     t is a set of precomputed scalings (set in the boundfmm6 routine)
c
c     wdtop is the table of weights needed to determine the local 
c           contributions from the double layer on top
c
c     wdside is the table of weights needed to determine the local 
c           contributions from the double layer on side
c
c     wstop is the table of weights needed to determine the local 
c           contributions from the single layer on top
c
c     wsside is the table of weights needed to determine the local 
c           contributions from the single layer on side
c
c     output:
c
c     pot is altered to account for the local contribution
c
c*****************************************************************************
      subroutine btosloclay8(pot,coeffstop,
     1            coeffsside,coeffdtop,coeffdside,
     2            ndeg,iflag,t,wdtop,wdside,
     3            wstop,wsside,iswitch)
      implicit none
c-----global variables
      integer *4 ndeg, iflag, iswitch
      real *8 pot(64), t(10)
      real *8 coeffstop(0:ndeg), coeffsside(0:ndeg)
      real *8 coeffdtop(0:ndeg), coeffdside(0:ndeg)
      real *8 wdtop(12,64,8), wdside(12,64,8)
      real *8 wstop(12,64,8), wsside(12,64,8)
c-----local variables
      integer *4 i, j
      real *8 c(8), clog(8)

      if(iswitch .eq. 0)then
        return
      endif

      c(1)  = coeffdtop(0)
      c(2)  = coeffdtop(1)
      c(3)  = coeffdtop(2)
      c(4)  = coeffdtop(3)
      c(5)  = coeffdtop(4)
      c(6)  = coeffdtop(5)
      c(7)  = coeffdtop(6)
      c(8)  = coeffdtop(7)

      do i = 1, 64
        do j = 1, 8
           pot(i) = pot(i) + c(j)*wdtop(iflag,i,j)
        end do
      end do


      c(1)  = coeffdside(0)
      c(2)  = coeffdside(1)
      c(3)  = coeffdside(2)
      c(4)  = coeffdside(3)
      c(5)  = coeffdside(4)
      c(6)  = coeffdside(5)
      c(7)  = coeffdside(6)
      c(8)  = coeffdside(7)

      do i = 1, 64
        do j = 1, 8
           pot(i) = pot(i) + c(j)*wdside(iflag,i,j)
        end do
      end do


      c(1) = coeffstop(0)  / t(1)
      c(2) = coeffstop(1)  / t(1)
      c(3) = coeffstop(2)  / t(1)
      c(4) = coeffstop(3)  / t(1)
      c(5) = coeffstop(4)  / t(1)
      c(6) = coeffstop(5)  / t(1)
      c(7) = coeffstop(6)  / t(1)
      c(8) = coeffstop(7)  / t(1)

      clog(1)  = c(1) * t(3)
      clog(2)  = 0.0d0
      clog(3)  = c(3) * t(4)
      clog(4)  = 0.0d0
      clog(5)  = c(5) * t(5)
      clog(6)  = 0.0d0
      clog(7)  = c(7) * t(6)
      clog(8)  = 0.0d0

      do i = 1, 64
        do j = 1, 8
           pot(i) = pot(i) + c(j)* wstop(iflag,i,j) + clog(j)
        end do
      end do


      c(1) = coeffsside(0) / t(1)
      c(2) = coeffsside(1) / t(1)
      c(3) = coeffsside(2) / t(1)
      c(4) = coeffsside(3) / t(1)
      c(5) = coeffsside(4) / t(1)
      c(6) = coeffsside(5) / t(1)
      c(7) = coeffsside(6) / t(1)
      c(8) = coeffsside(7) / t(1)

      clog(1)  = c(1) * t(3)
      clog(2)  = 0.0d0
      clog(3)  = c(3) * t(4)
      clog(4)  = 0.0d0
      clog(5)  = c(5) * t(5)
      clog(6)  = 0.0d0
      clog(7)  = c(7) * t(6)
      clog(8)  = 0.0d0

      do i = 1, 64
        do j = 1, 8
           pot(i) = pot(i) + c(j) * wsside(iflag,i,j) + clog(j)
        end do
      end do
      return
      end


c**************************************************************************
c     the following subroutine does the local work involving layer potentials
c     that figures the potential in a bigger box due to a small box that is
c     touching it.
c     the boxes are only allowed to be one level apart.
c
c     input:
c
c     coeffstop is the array of  basis functions coefficients for the single
c                 layer potential on the top
c
c     coeffsside is the array of  basis functions coefficients for the single
c                 layer potential on the side
c
c     coeffdtop is the array of  basis functions coefficients for the double
c                 layer potential on the top
c
c     coeffdside is the array of  basis functions coefficients for the double
c                 layer potential on the side
c
c     ndeg is the degree of the approximating polynomial
c
c     iflag denotes the colleague number being considered
c
c     t is a set of precomputed scalings (set in the boundfmm6 routine)
c
c     wdtop is the table of weights needed to determine the local 
c           contributions from the double layer on top
c
c     wdside is the table of weights needed to determine the local 
c           contributions from the double layer on side
c
c     wstop is the table of weights needed to determine the local
c           contributions from the single layer on top
c
c     wsside is the table of weights needed to determine the local
c           contributions from the single layer on side
c
c     output:
c
c     pot is altered to account for the local contribution
c
c**************************************************************************
      subroutine stobloclay8(pot,coeffstop, 
     1            coeffsside,coeffdtop,coeffdside, 
     2            ndeg,iflag,t,wdtop,wdside,
     3            wstop,wsside,iswitch)
      implicit none
c-----global variables
      integer *4 ndeg, iflag, iswitch
      real *8 pot(64), t(10)
      real *8 coeffstop(0:ndeg), coeffsside(0:ndeg)
      real *8 coeffdtop(0:ndeg), coeffdside(0:ndeg)
      real *8 wdtop(12,64,8), wdside(12,64,8)
      real *8 wstop(12,64,8), wsside(12,64,8)
c-----local variables
      integer *4 i, j
      real *8 c(8), clog(8)

      if(iswitch .eq. 0)then
        return
      endif
    
      c(1)  = coeffdtop(0)
      c(2)  = coeffdtop(1)
      c(3)  = coeffdtop(2)
      c(4)  = coeffdtop(3)
      c(5)  = coeffdtop(4)
      c(6)  = coeffdtop(5)
      c(7)  = coeffdtop(6)
      c(8)  = coeffdtop(7)

      do i = 1, 64
        do j = 1, 8
           pot(i) = pot(i) + c(j)*wdtop(iflag,i,j)
        end do
      end do


      c(1)  = coeffdside(0)
      c(2)  = coeffdside(1)
      c(3)  = coeffdside(2)
      c(4)  = coeffdside(3)
      c(5)  = coeffdside(4)
      c(6)  = coeffdside(5)
      c(7)  = coeffdside(6)
      c(8)  = coeffdside(7)

      do i = 1, 64
        do j = 1, 8
           pot(i) = pot(i) + c(j)*wdside(iflag,i,j)
        end do
      end do


      c(1) = coeffstop(0)  / t(2)
      c(2) = coeffstop(1)  / t(2)
      c(3) = coeffstop(2)  / t(2)
      c(4) = coeffstop(3)  / t(2)
      c(5) = coeffstop(4)  / t(2)
      c(6) = coeffstop(5)  / t(2)
      c(7) = coeffstop(6)  / t(2)
      c(8) = coeffstop(7)  / t(2)

      clog(1)  = c(1) * t(7)
      clog(2)  = 0.0d0
      clog(3)  = c(3) * t(8)
      clog(4)  = 0.0d0
      clog(5)  = c(5) * t(9)
      clog(6)  = 0.0d0
      clog(7)  = c(7) * t(10)
      clog(8)  = 0.0d0

      do i = 1, 64
        do j = 1, 8
           pot(i) = pot(i) + c(j)*wstop(iflag,i,j) + clog(j)
        end do
      end do


      c(1) = coeffsside(0) / t(2)
      c(2) = coeffsside(1) / t(2)
      c(3) = coeffsside(2) / t(2)
      c(4) = coeffsside(3) / t(2)
      c(5) = coeffsside(4) / t(2)
      c(6) = coeffsside(5) / t(2)
      c(7) = coeffsside(6) / t(2)
      c(8) = coeffsside(7) / t(2)

      clog(1)  = c(1) * t(7)
      clog(2)  = 0.0d0
      clog(3)  = c(3) * t(8)
      clog(4)  = 0.0d0
      clog(5)  = c(5) * t(9)
      clog(6)  = 0.0d0
      clog(7)  = c(7) * t(10)
      clog(8)  = 0.0d0

      do i = 1, 64
        do j = 1, 8
           pot(i) = pot(i) + c(j)*wsside(iflag,i,j) + clog(j)
        end do
      end do
      return
      end


c*****************************************************************************
c     the following subroutine generates four exponential expansions, north,
c     south, east, and west respectively, that are use when passing from a 
c     larger box to a smaller box.  these expansions represent the layer
c     potentials of the given box.  the exponential expansions are obtained
c     by using a set of precomputed matrices that map from the polynomial
c     coefficients (that approximate the charge density) to the exponential
c     coefficients.
c     
c     input:
c
c     coeffdtop is the array of  basis functions coefficients for the double
c               layer potential on the top
c
c     coeffstop is the array of  basis functions coefficients for the single
c               layer potential on the top
c
c     coeffdside is the array of  basis functions coefficients for the double
c               layer potential on the side
c
c     coeffsside is the array of  basis functions coefficients for the single
c               layer potential on the side
c
c     level is the level of the larger box
c
c     nnodes is the order of the exponential expansion
c
c     sum1 is a precomputed terms needed in the exponential expansions
c
c     the other input entries are just the precomputed maps from the
c     polynomial coefficients to the exponential coefficients.  the 
c     are written as direction, double or single, and top or side.
c     so eastdbletop would be the map from the four polynomial
c     coefficients for the double layer on top to the expansion going in the
c     east direction.  the other matrices are defined similarly.
c
c     output:
c
c     expnall is the outgoing north expansion for the big box
c
c     expsall is the outgoing south expansion for the big box
c
c     expeall is the outgoing east expansion for the big box
c
c     expwall is the outgoing west expansion for the big box
c
c*****************************************************************************
      subroutine mkexpbtoslay8(coeffdtop,coeffstop, 
     1      coeffdside, coeffsside,xlength,sum1,
     2      expnall,expsall,expeall,expwall,
     4      eastdbletopmat,   westdbletopmat,
     5      northdbletopmat,  southdbletopmat,
     5      eastdblesidemat,  westdblesidemat,
     5      northdblesidemat, southdblesidemat,
     6      eastsngletopmat,  westsngletopmat,
     5      southsngletopmat, northsngletopmat,
     7      eastsnglesidemat, westsnglesidemat, 
     8      southsnglesidemat,northsnglesidemat,nnodes,
     9      dsideswitch,dtopswitch,
     1      ssideswitch,stopswitch)
      implicit none
c-----global variables
      integer *4  i, ii, nnodes
      integer *4  dsideswitch, dtopswitch
      integer *4  ssideswitch, stopswitch
      real *8  coeffdtop(0:7), coeffstop(0:7)
      real *8  coeffdside(0:7), coeffsside(0:7)
      real *8  sum1
      complex *16  expnall(0:60), expsall(0:60)
      complex *16  expeall(0:60), expwall(0:60)
c-----local variables
      real *8  xlength, sum2
      real *8  coeffstopcaled(0:7)
      real *8  coeffssidecaled(0:7)
      complex *16  expntest(0:60), expstest(0:60)
      complex *16  expetest(0:60), expwtest(0:60)
      complex *16  eastdbletopmat(0:nnodes,0:7)
      complex *16  eastdblesidemat(0:nnodes,0:7)
      complex *16  eastsngletopmat(0:nnodes,0:7)
      complex *16  eastsnglesidemat(0:nnodes,0:7)
      complex *16  westdbletopmat(0:nnodes,0:7)
      complex *16  westdblesidemat(0:nnodes,0:7)
      complex *16  westsngletopmat(0:nnodes,0:7)
      complex *16  westsnglesidemat(0:nnodes,0:7)
      complex *16  northdbletopmat(0:nnodes,0:7)
      complex *16  northdblesidemat(0:nnodes,0:7)
      complex *16  northsngletopmat(0:nnodes,0:7)
      complex *16  northsnglesidemat(0:nnodes,0:7)
      complex *16  southdbletopmat(0:nnodes,0:7)
      complex *16  southdblesidemat(0:nnodes,0:7)
      complex *16  southsngletopmat(0:nnodes,0:7)
      complex *16  southsnglesidemat(0:nnodes,0:7)

      sum2 = sum1 + dlog(xlength)

      do i = 0, nnodes
        expeall(i) = 0.0d0
        expwall(i) = 0.0d0
        expnall(i) = 0.0d0
        expsall(i) = 0.0d0
      end do

      if(dtopswitch .eq. 1)then
        do i = 0, nnodes
        expetest(i) = 0.0d0
        do ii = 0, 7
         expetest(i) = expetest(i)
     1        + eastdbletopmat(i,ii) * coeffdtop(ii)
        end do
        end do

        do i = 0, nnodes
        expwtest(i) = 0.0d0
        do ii = 0, 7
         expwtest(i) = expwtest(i)
     1        + westdbletopmat(i,ii) * coeffdtop(ii)
        end do
        end do

        do i = 0, nnodes
        expntest(i) = 0.0d0
        do ii = 0, 7
         expntest(i) = expntest(i)
     1        + northdbletopmat(i,ii) * coeffdtop(ii)
        end do
        end do

        do i = 0, nnodes
        expstest(i) = 0.0d0
        do ii = 0, 7
         expstest(i) = expstest(i)
     1        + southdbletopmat(i,ii) * coeffdtop(ii)
        end do
        end do

        do i = 0, nnodes
          expeall(i) = expeall(i) + expetest(i)
          expwall(i) = expwall(i) + expwtest(i)
          expnall(i) = expnall(i) + expntest(i)
          expsall(i) = expsall(i) + expstest(i)
        end do
      endif


      if(dsideswitch .eq. 1)then
        do i = 0, nnodes
        expetest(i) = 0.0d0
        do ii = 0, 7
         expetest(i) = expetest(i)
     1        + eastdblesidemat(i,ii) * coeffdside(ii)
        end do
        end do

        do i = 0, nnodes
        expwtest(i) = 0.0d0
        do ii = 0, 7
         expwtest(i) = expwtest(i)
     1        + westdblesidemat(i,ii) * coeffdside(ii)
        end do
        end do

        do i = 0, nnodes
        expntest(i) = 0.0d0
        do ii = 0, 7
         expntest(i) = expntest(i)
     1        + northdblesidemat(i,ii) * coeffdside(ii)
        end do
        end do

        do i = 0, nnodes
        expstest(i) = 0.0d0
        do ii = 0, 7
         expstest(i) = expstest(i)
     1        + southdblesidemat(i,ii) * coeffdside(ii)
        end do
        end do

        do i = 0, nnodes
          expeall(i) = expeall(i) + expetest(i)
          expwall(i) = expwall(i) + expwtest(i)
          expnall(i) = expnall(i) + expntest(i)
          expsall(i) = expsall(i) + expstest(i)
        end do
      endif


      if(stopswitch .eq. 1)then
        coeffstopcaled(0) = coeffstop(0) * xlength
        coeffstopcaled(1) = coeffstop(1) * xlength
        coeffstopcaled(2) = coeffstop(2) * xlength
        coeffstopcaled(3) = coeffstop(3) * xlength
        coeffstopcaled(4) = coeffstop(4) * xlength
        coeffstopcaled(5) = coeffstop(5) * xlength
        coeffstopcaled(6) = coeffstop(6) * xlength
        coeffstopcaled(7) = coeffstop(7) * xlength

        do i = 0, nnodes
        expetest(i) = 0.0d0
        do ii = 0, 7
         expetest(i) = expetest(i)
     1        + eastsngletopmat(i,ii) * coeffstopcaled(ii)
        end do
        end do

        do i = 0, nnodes
        expwtest(i) = 0.0d0
        do ii = 0, 7
         expwtest(i) = expwtest(i)
     1        + westsngletopmat(i,ii) * coeffstopcaled(ii)
        end do
        end do

        do i = 0, nnodes
        expstest(i) = 0.0d0
        do ii = 0, 7
         expstest(i) = expstest(i)
     1        + southsngletopmat(i,ii) * coeffstopcaled(ii)
        end do
        end do

        do i = 0, nnodes
        expntest(i) = 0.0d0
        do ii = 0, 7
         expntest(i) = expntest(i)
     1        + northsngletopmat(i,ii) * coeffstopcaled(ii)
        end do
        end do

        do i = 0, nnodes
          expeall(i) = expeall(i) + expetest(i)
          expwall(i) = expwall(i) + expwtest(i)
          expsall(i) = expsall(i) + expstest(i)
          expnall(i) = expnall(i) + expntest(i)
        end do
      endif


      if(ssideswitch .eq. 1)then
        coeffssidecaled(0) = coeffsside(0) * xlength
        coeffssidecaled(1) = coeffsside(1) * xlength
        coeffssidecaled(2) = coeffsside(2) * xlength
        coeffssidecaled(3) = coeffsside(3) * xlength
        coeffssidecaled(4) = coeffsside(4) * xlength
        coeffssidecaled(5) = coeffsside(5) * xlength
        coeffssidecaled(6) = coeffsside(6) * xlength
        coeffssidecaled(7) = coeffsside(7) * xlength

        do i = 0, nnodes
        expetest(i) = 0.0d0
        do ii = 0, 7
         expetest(i) = expetest(i)
     1        + eastsnglesidemat(i,ii) * coeffssidecaled(ii)
        end do
        end do

        do i = 0, nnodes
        expwtest(i) = 0.0d0
        do ii = 0, 7
         expwtest(i) = expwtest(i)
     1        + westsnglesidemat(i,ii) * coeffssidecaled(ii)
        end do
        end do

        do i = 0, nnodes
        expstest(i) = 0.0d0
        do ii = 0, 7
         expstest(i) = expstest(i)
     1        + southsnglesidemat(i,ii) * coeffssidecaled(ii)
        end do
        end do

        do i = 0, nnodes
        expntest(i) = 0.0d0
        do ii = 0, 7
         expntest(i) = expntest(i)
     1        + northsnglesidemat(i,ii) * coeffssidecaled(ii)
        end do
        end do

        do i = 0, nnodes
          expeall(i) = expeall(i) + expetest(i)
          expwall(i) = expwall(i) + expwtest(i)
          expnall(i) = expnall(i) + expntest(i)
          expsall(i) = expsall(i) + expstest(i)
        end do
      endif

      expeall(0) = expeall(0) * sum2
      expwall(0) = expwall(0) * sum2
      expnall(0) = expnall(0) * sum2
      expsall(0) = expsall(0) * sum2
      return
      end



c**************************************************************************
c     the following subroutine sets up boundary conditions for the
c     case with purely dirichlet conditions. 
c
c     input:
c
c     levelbox is an array determining the level of each box
c
c     nboxes is the total number of boxes
c
c     nlev is the finest level
c
c     icolbox denotes the column of each box
c
c     irowbox denotes the row of each box
c
c     ichildbox denotes the four children of each box
c
c     iparentbox denotes the parent of each box
c
c     nblevel is the total number of boxes per level
c
c     iboxlev is the array in which the boxes are arranged
c
c     istartlev is the pointer to where each level begins in the
c               iboxlev array
c    
c     hl is the function representing the boundary condition imposed
c        on the left
c    
c     hr is the function representing the boundary condition imposed
c        on the right
c    
c     hb is the function representing the boundary condition imposed
c        on the bottom
c    
c     ht is the function representing the boundary condition imposed
c        on the top
c
c     output:
c
c     ftop is the array of function values representing the single layer
c          densities on the top of each box
c
c     fside is the array of function values representing the single layer
c          densities on the side of each box
c
c     ftopd is the array of function values representing the double layer
c          densities on the top of each box
c
c     fsided is the array of function values representing the double layer
c          densities on the side of each box
c
c**************************************************************************
      subroutine setbcs8dir(nboxes, nlev, icolbox, irowbox,
     1           ichildbox, nblevel, iboxlev, istartlev,
     2           ftop, fside, ftopd, fsided, 
     5           doubletoponoff, doublesideonoff,
     6           singletoponoff, singlesideonoff,
     7           hl, hr, hb, ht)
      implicit none
c-----global variables
      integer *4  nboxes, nlev
      integer *4  icolbox(1), irowbox(1)
      integer *4  ichildbox(4,1)
      integer *4  nblevel(0:1), iboxlev(1), istartlev(0:1)
      integer *4  doubletoponoff(1), doublesideonoff(1)
      integer *4  singletoponoff(1), singlesideonoff(1)
      real *8  xshift
      real *8  ftop(8,1), fside(8,1)
      real *8  ftopd(8,1), fsided(8,1)
      real *8  hl, hr, hb, ht
c-----local variables
      integer *4  i, jj, ii, ilev
      integer *4  nside
      integer *4  istart, iend
      real *8  temp3
      real *8  xuse, pi, pi16, x, xx(8)

      pi = dacos(-1.0d0)

      pi16 = pi / 16.0d0
      xx(1) = dcos(15.0d0*pi16) / 2.0d0
      xx(2) = dcos(13.0d0*pi16) / 2.0d0
      xx(3) = dcos(11.0d0*pi16) / 2.0d0
      xx(4) = dcos( 9.0d0*pi16) / 2.0d0
      xx(5) = dcos( 7.0d0*pi16) / 2.0d0
      xx(6) = dcos( 5.0d0*pi16) / 2.0d0
      xx(7) = dcos( 3.0d0*pi16) / 2.0d0
      xx(8) = dcos( 1.0d0*pi16) / 2.0d0


      do ii = 1, nboxes
        doubletoponoff(ii) = 0
        doublesideonoff(ii) = 0
        singletoponoff(ii) = 0
        singlesideonoff(ii) = 0
        do jj = 1, 8
           ftop(jj,ii) = 0.0d0
           ftopd(jj,ii) = 0.0d0
           fside(jj,ii) = 0.0d0
           fsided(jj,ii) = 0.0d0
        end do
      end do

      temp3 = .5d0
      nside = 1
      do ilev = 0, nlev
      istart = istartlev(ilev)
      iend = istart + nblevel(ilev) - 1
      do i = istart, iend
      ii = iboxlev(i)
      if(ichildbox(1,ii) .lt. 0)then

       if(icolbox(ii) .eq. nside/2)then
       if(irowbox(ii) .le. nside/2)then
         doublesideonoff(ii) = 1
         xshift  = dble(irowbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj) / temp3
           fsided(jj,ii) = hr(x)
         end do
       endif
       endif

       if(icolbox(ii) .eq. nside/2)then
       if(irowbox(ii) .gt. nside/2)then
         doublesideonoff(ii) = 1
         xshift  = dble(irowbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           xuse = x - 1.0d0
           xuse = -xuse
           fsided(jj,ii) = -hr(xuse)
         end do
       endif
       endif

       if(icolbox(ii) .eq. nside)then
       if(irowbox(ii) .le. nside/2)then
         doublesideonoff(ii) = 1
         xshift  = dble(irowbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           fsided(jj,ii) = -hl(x)
         end do
       endif
       endif

       if(icolbox(ii) .eq. nside)then
       if(irowbox(ii) .gt. nside/2)then
         doublesideonoff(ii) = 1
         xshift  = dble(irowbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           xuse = x - 1.0d0
           xuse = -xuse
           fsided(jj,ii) = hl(xuse)
         end do
       endif
       endif


c     now set the top bcs
       if(irowbox(ii) .eq. nside/2)then
       if(icolbox(ii) .le. nside/2)then
         doubletoponoff(ii) = 1
         xshift  = dble(icolbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3  
           ftopd(jj,ii) = ht(x)
         end do
       endif
       endif

       if(irowbox(ii) .eq. nside/2)then
       if(icolbox(ii) .gt. nside/2)then
         doubletoponoff(ii) = 1
         xshift  = dble(icolbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           xuse = x - 1.0d0
           xuse = -xuse
           ftopd(jj,ii) = -ht(xuse)
         end do
       endif
       endif

       if(irowbox(ii) .eq. nside)then
       if(icolbox(ii) .le. nside/2)then
         doubletoponoff(ii) = 1
         xshift  = dble(icolbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           ftopd(jj,ii) = -hb(x)
         end do
       endif
       endif

       if(irowbox(ii) .eq. nside)then
       if(icolbox(ii) .gt. nside/2)then
         doubletoponoff(ii) = 1
         xshift  = dble(icolbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           xuse = x - 1.0d0
           xuse = -xuse
           ftopd(jj,ii) = hb(xuse)
         end do
       endif
       endif

      endif
      end do
      temp3 = 2.0d0*temp3
      nside = 2*nside
      end do

c     now scale everything appropriately
c     and account for the sign changes:
      do ii = 1, nboxes
        do jj = 1, 8
           ftopd(jj,ii)  = -ftopd(jj,ii) / pi
           fsided(jj,ii) = -fsided(jj,ii) / pi
        end do
      end do
      return
      end


c**************************************************************************
c     the following subroutine sets up boundary conditions for the
c     case with purely neumann conditions.
c
c     input:
c
c     levelbox is an array determining the level of each box
c
c     nboxes is the total number of boxes
c
c     nlev is the finest level
c
c     icolbox denotes the column of each box
c
c     irowbox denotes the row of each box
c
c     ichildbox denotes the four children of each box
c
c     iparentbox denotes the parent of each box
c
c     nblevel is the total number of boxes per level
c
c     iboxlev is the array in which the boxes are arranged
c
c     istartlev is the pointer to where each level begins in the
c               iboxlev array
c
c     hl is the function representing the boundary condition imposed
c        on the left
c
c     hr is the function representing the boundary condition imposed
c        on the right
c
c     hb is the function representing the boundary condition imposed
c        on the bottom
c
c     ht is the function representing the boundary condition imposed
c        on the top
c
c     output:
c
c     ftop is the array of function values representing the single layer
c          densities on the top of each box
c
c     fside is the array of function values representing the single layer
c          densities on the side of each box
c
c     ftopd is the array of function values representing the double layer
c          densities on the top of each box
c
c     fsided is the array of function values representing the double layer
c          densities on the side of each box
c
c**************************************************************************
      subroutine setbcs8neu(nboxes, nlev, icolbox, irowbox,
     1           ichildbox, nblevel, iboxlev, istartlev,
     2           ftop, fside, ftopd, fsided, 
     5           doubletoponoff, doublesideonoff,
     6           singletoponoff, singlesideonoff,
     7           hl, hr, hb, ht)
      implicit none
c-----global variables
      integer *4  nboxes, nlev
      integer *4  icolbox(1), irowbox(1)
      integer *4  ichildbox(4,1)
      integer *4  nblevel(0:1), iboxlev(1), istartlev(0:1)
      integer *4  doubletoponoff(1), doublesideonoff(1)
      integer *4  singletoponoff(1), singlesideonoff(1)
      real *8  xshift
      real *8  ftop(8,1), fside(8,1)
      real *8  ftopd(8,1), fsided(8,1)
      real *8  hl, hr, hb, ht
c-----local variables
      integer *4  i, jj, ii, ilev
      integer *4  nside
      integer *4  istart, iend
      real *8  temp3
      real *8  xuse, pi, pi16, x, xx(8)

      pi = dacos(-1.0d0)

      pi16 = pi / 16.0d0
      xx(1) = dcos(15.0d0*pi16) / 2.0d0
      xx(2) = dcos(13.0d0*pi16) / 2.0d0
      xx(3) = dcos(11.0d0*pi16) / 2.0d0
      xx(4) = dcos( 9.0d0*pi16) / 2.0d0
      xx(5) = dcos( 7.0d0*pi16) / 2.0d0
      xx(6) = dcos( 5.0d0*pi16) / 2.0d0
      xx(7) = dcos( 3.0d0*pi16) / 2.0d0
      xx(8) = dcos( 1.0d0*pi16) / 2.0d0


      do ii = 1, nboxes
        doubletoponoff(ii) = 0
        doublesideonoff(ii) = 0
        singletoponoff(ii) = 0
        singlesideonoff(ii) = 0
        do jj = 1, 8
           ftop(jj,ii) = 0.0d0
           ftopd(jj,ii) = 0.0d0
           fside(jj,ii) = 0.0d0
           fsided(jj,ii) = 0.0d0
        end do
      end do

      temp3 = .5d0
      nside = 1
      do ilev = 0, nlev
      istart = istartlev(ilev)
      iend = istart + nblevel(ilev) - 1
      do i = istart, iend
      ii = iboxlev(i)
      if(ichildbox(1,ii) .lt. 0)then

       if(icolbox(ii) .eq. nside/2)then
       if(irowbox(ii) .le. nside/2)then
         singlesideonoff(ii) = 1
         xshift  = dble(irowbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj) / temp3
           fside(jj,ii) = hr(x)
         end do
       endif
       endif

       if(icolbox(ii) .eq. nside/2)then
       if(irowbox(ii) .gt. nside/2)then
         singlesideonoff(ii) = 1
         xshift  = dble(irowbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           xuse = x - 1.0d0
           xuse = -xuse
           fside(jj,ii) = hr(xuse)
         end do
       endif
       endif

       if(icolbox(ii) .eq. nside)then
       if(irowbox(ii) .le. nside/2)then
         singlesideonoff(ii) = 1
         xshift  = dble(irowbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           fside(jj,ii) = hl(x)
         end do
       endif
       endif

       if(icolbox(ii) .eq. nside)then
       if(irowbox(ii) .gt. nside/2)then
         singlesideonoff(ii) = 1
         xshift  = dble(irowbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           xuse = x - 1.0d0
           xuse = -xuse
           fside(jj,ii) = hl(xuse)
         end do
       endif
       endif


c     now set the top bcs
       if(irowbox(ii) .eq. nside/2)then
       if(icolbox(ii) .le. nside/2)then
         singletoponoff(ii) = 1
         xshift  = dble(icolbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3  
           ftop(jj,ii) = ht(x)
         end do
       endif
       endif

       if(irowbox(ii) .eq. nside/2)then
       if(icolbox(ii) .gt. nside/2)then
         singletoponoff(ii) = 1
         xshift  = dble(icolbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           xuse = x - 1.0d0
           xuse = -xuse
           ftop(jj,ii) = ht(xuse)
         end do
       endif
       endif

       if(irowbox(ii) .eq. nside)then
       if(icolbox(ii) .le. nside/2)then
         singletoponoff(ii) = 1
         xshift  = dble(icolbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           ftop(jj,ii) = hb(x)
         end do
       endif
       endif

       if(irowbox(ii) .eq. nside)then
       if(icolbox(ii) .gt. nside/2)then
         singletoponoff(ii) = 1
         xshift  = dble(icolbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           xuse = x - 1.0d0
           xuse = -xuse
           ftop(jj,ii) = hb(xuse)
         end do
       endif
       endif

      endif
      end do
      temp3 = 2.0d0*temp3
      nside = 2*nside
      end do

c     now scale everything appropriately
c     and account for the sign changes:
      do ii = 1, nboxes
        do jj = 1, 8
           ftop(jj,ii)  = -ftop(jj,ii) / pi
           fside(jj,ii) = -fside(jj,ii) / pi
        end do
      end do
      return
      end

c**************************************************************************
c     the following subroutine sets up boundary conditions for the
c     case with neumann conditions on the top and bottom and dirichlet
c     conditions on the sides.
c
c     input:
c
c     levelbox is an array determining the level of each box
c
c     nboxes is the total number of boxes
c
c     nlev is the finest level
c
c     icolbox denotes the column of each box
c
c     irowbox denotes the row of each box
c
c     ichildbox denotes the four children of each box
c
c     iparentbox denotes the parent of each box
c
c     nblevel is the total number of boxes per level
c
c     iboxlev is the array in which the boxes are arranged
c
c     istartlev is the pointer to where each level begins in the
c               iboxlev array
c
c     hl is the function representing the boundary condition imposed
c        on the left
c
c     hr is the function representing the boundary condition imposed
c        on the right
c
c     hb is the function representing the boundary condition imposed
c        on the bottom
c
c     ht is the function representing the boundary condition imposed
c        on the top
c
c     output:
c
c     ftop is the array of function values representing the single layer
c          densities on the top of each box
c
c     fside is the array of function values representing the single layer
c          densities on the side of each box
c
c     ftopd is the array of function values representing the double layer
c          densities on the top of each box
c
c     fsided is the array of function values representing the double layer
c          densities on the side of each box
c
c**************************************************************************
      subroutine setbcs8dirneu(nboxes, nlev, icolbox, irowbox,
     1           ichildbox, nblevel, iboxlev, istartlev,
     2           ftop, fside, ftopd, fsided,
     5           doubletoponoff, doublesideonoff,
     6           singletoponoff, singlesideonoff,
     7           hl, hr, hb, ht)
      implicit none
c-----global variables
      integer *4  nboxes, nlev
      integer *4  icolbox(1), irowbox(1)
      integer *4  ichildbox(4,1)
      integer *4  nblevel(0:1), iboxlev(1), istartlev(0:1)
      integer *4  doubletoponoff(1), doublesideonoff(1)
      integer *4  singletoponoff(1), singlesideonoff(1)
      real *8  xshift
      real *8  ftop(8,1), fside(8,1)
      real *8  ftopd(8,1), fsided(8,1)
      real *8  hl, hr, hb, ht
c-----local variables
      integer *4  i, jj, ii, ilev
      integer *4  nside
      integer *4  istart, iend
      real *8  temp3
      real *8  xuse, pi, pi16, x, xx(8)

      pi = dacos(-1.0d0)

      pi16 = pi / 16.0d0
      xx(1) = dcos(15.0d0*pi16) / 2.0d0
      xx(2) = dcos(13.0d0*pi16) / 2.0d0
      xx(3) = dcos(11.0d0*pi16) / 2.0d0
      xx(4) = dcos( 9.0d0*pi16) / 2.0d0
      xx(5) = dcos( 7.0d0*pi16) / 2.0d0
      xx(6) = dcos( 5.0d0*pi16) / 2.0d0
      xx(7) = dcos( 3.0d0*pi16) / 2.0d0
      xx(8) = dcos( 1.0d0*pi16) / 2.0d0


      do ii = 1, nboxes
        doubletoponoff(ii) = 0
        doublesideonoff(ii) = 0
        singletoponoff(ii) = 0
        singlesideonoff(ii) = 0
        do jj = 1, 8
           ftop(jj,ii) = 0.0d0
           ftopd(jj,ii) = 0.0d0
           fside(jj,ii) = 0.0d0
           fsided(jj,ii) = 0.0d0
        end do
      end do

      temp3 = .5d0
      nside = 1
      do ilev = 0, nlev
      istart = istartlev(ilev)
      iend = istart + nblevel(ilev) - 1
      do i = istart, iend
      ii = iboxlev(i)
      if(ichildbox(1,ii) .lt. 0)then

       if(icolbox(ii) .eq. nside/2)then
       if(irowbox(ii) .le. nside/2)then
         doublesideonoff(ii) = 1
         xshift  = dble(irowbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj) / temp3
           fsided(jj,ii) = hr(x)
         end do
       endif
       endif

       if(icolbox(ii) .eq. nside/2)then
       if(irowbox(ii) .gt. nside/2)then
         doublesideonoff(ii) = 1
         xshift  = dble(irowbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           xuse = x - 1.0d0
           xuse = -xuse
           fsided(jj,ii) = hr(xuse)
         end do
       endif
       endif

       if(icolbox(ii) .eq. nside)then
       if(irowbox(ii) .le. nside/2)then
         doublesideonoff(ii) = 1
         xshift  = dble(irowbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           fsided(jj,ii) = -hl(x)
         end do
       endif
       endif

       if(icolbox(ii) .eq. nside)then
       if(irowbox(ii) .gt. nside/2)then
         doublesideonoff(ii) = 1
         xshift  = dble(irowbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           xuse = x - 1.0d0
           xuse = -xuse
           fsided(jj,ii) = -hl(xuse)
         end do
       endif
       endif


c     now set the top bcs
       if(irowbox(ii) .eq. nside/2)then
       if(icolbox(ii) .le. nside/2)then
         singletoponoff(ii) = 1
         xshift  = dble(icolbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3  
           ftop(jj,ii) = ht(x)
         end do
       endif
       endif

       if(irowbox(ii) .eq. nside/2)then
       if(icolbox(ii) .gt. nside/2)then
         singletoponoff(ii) = 1
         xshift  = dble(icolbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           xuse = x - 1.0d0
           xuse = -xuse
           ftop(jj,ii) = -ht(xuse)
         end do
       endif
       endif

       if(irowbox(ii) .eq. nside)then
       if(icolbox(ii) .le. nside/2)then
         singletoponoff(ii) = 1
         xshift  = dble(icolbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           ftop(jj,ii) = hb(x)
         end do
       endif
       endif

       if(irowbox(ii) .eq. nside)then
       if(icolbox(ii) .gt. nside/2)then
         singletoponoff(ii) = 1
         xshift  = dble(icolbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           xuse = x - 1.0d0
           xuse = -xuse
           ftop(jj,ii) = -hb(xuse)
         end do
       endif
       endif

      endif
      end do
      temp3 = 2.0d0*temp3
      nside = 2*nside
      end do

c     now scale everything appropriately
c     and account for the sign changes:
      do ii = 1, nboxes
        do jj = 1, 8
           ftop(jj,ii)  = -ftop(jj,ii) / pi
           fsided(jj,ii) = -fsided(jj,ii) / pi
        end do
      end do
      return
      end

c**************************************************************************
c     the following subroutine sets up boundary conditions for the
c     case with dirichlet conditions on the side and periodic top bottom.
c
c     input:
c
c     levelbox is an array determining the level of each box
c
c     nboxes is the total number of boxes
c
c     nlev is the finest level
c
c     icolbox denotes the column of each box
c
c     irowbox denotes the row of each box
c
c     ichildbox denotes the four children of each box
c
c     iparentbox denotes the parent of each box
c
c     nblevel is the total number of boxes per level
c
c     iboxlev is the array in which the boxes are arranged
c
c     istartlev is the pointer to where each level begins in the
c               iboxlev array
c
c     hl is the function representing the boundary condition imposed
c        on the left
c
c     hr is the function representing the boundary condition imposed
c        on the right
c
c     output:
c
c     ftop is the array of function values representing the single layer
c          densities on the top of each box
c
c     fside is the array of function values representing the single layer
c          densities on the side of each box
c
c     ftopd is the array of function values representing the double layer
c          densities on the top of each box
c
c     fsided is the array of function values representing the double layer
c          densities on the side of each box
c
c**************************************************************************
      subroutine setbcs8dirper(nboxes, nlev, icolbox, irowbox,
     1           ichildbox, nblevel, iboxlev, istartlev,
     2           ftop, fside, ftopd, fsided,
     5           doubletoponoff, doublesideonoff,
     6           singletoponoff, singlesideonoff,
     7           hl, hr)
      implicit none
c-----global variables
      integer *4  nboxes, nlev
      integer *4  icolbox(1), irowbox(1)
      integer *4  ichildbox(4,1)
      integer *4  nblevel(0:1), iboxlev(1), istartlev(0:1)
      integer *4  doubletoponoff(1), doublesideonoff(1)
      integer *4  singletoponoff(1), singlesideonoff(1)
      real *8  xshift
      real *8  ftop(8,1), fside(8,1)
      real *8  ftopd(8,1), fsided(8,1)
      real *8  hl, hr
c-----local variables
      integer *4  i, jj, ii, ilev
      integer *4  nside
      integer *4  istart, iend
      real *8  temp3
      real *8  xuse, pi, pi16, x, xx(8)

      pi = dacos(-1.0d0)

      pi16 = pi / 16.0d0
      xx(1) = dcos(15.0d0*pi16) / 2.0d0
      xx(2) = dcos(13.0d0*pi16) / 2.0d0
      xx(3) = dcos(11.0d0*pi16) / 2.0d0
      xx(4) = dcos( 9.0d0*pi16) / 2.0d0
      xx(5) = dcos( 7.0d0*pi16) / 2.0d0
      xx(6) = dcos( 5.0d0*pi16) / 2.0d0
      xx(7) = dcos( 3.0d0*pi16) / 2.0d0
      xx(8) = dcos( 1.0d0*pi16) / 2.0d0


      do ii = 1, nboxes
        doubletoponoff(ii) = 0
        doublesideonoff(ii) = 0
        singletoponoff(ii) = 0
        singlesideonoff(ii) = 0
        do jj = 1, 8
           ftop(jj,ii) = 0.0d0
           ftopd(jj,ii) = 0.0d0
           fside(jj,ii) = 0.0d0
           fsided(jj,ii) = 0.0d0
        end do
      end do

      temp3 = .5d0
      nside = 1
      do ilev = 0, nlev
      istart = istartlev(ilev)
      iend = istart + nblevel(ilev) - 1
      do i = istart, iend
      ii = iboxlev(i)
      if(ichildbox(1,ii) .lt. 0)then

       if(icolbox(ii) .eq. nside/2)then
       if(irowbox(ii) .le. nside/2)then
         doublesideonoff(ii) = 1
         xshift  = dble(irowbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj) / temp3
           fsided(jj,ii) = hr(x)
         end do
       endif
       endif

       if(icolbox(ii) .eq. nside/2)then
       if(irowbox(ii) .gt. nside/2)then
         doublesideonoff(ii) = 1
         xshift  = dble(irowbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           xuse = x - 1.0d0
           fsided(jj,ii) = hr(xuse)
         end do
       endif
       endif

       if(icolbox(ii) .eq. nside)then
       if(irowbox(ii) .le. nside/2)then
         doublesideonoff(ii) = 1
         xshift  = dble(irowbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           fsided(jj,ii) = -hl(x)
         end do
       endif
       endif

       if(icolbox(ii) .eq. nside)then
       if(irowbox(ii) .gt. nside/2)then
         doublesideonoff(ii) = 1
         xshift  = dble(irowbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           xuse = x - 1.0d0
           fsided(jj,ii) = -hl(xuse)
         end do
       endif
       endif

      endif
      end do
      temp3 = 2.0d0*temp3
      nside = 2*nside
      end do

c     now scale everything appropriately
c     and account for the sign changes:
      do ii = 1, nboxes
        do jj = 1, 8
           fsided(jj,ii) = -fsided(jj,ii) / pi
        end do
      end do
      return
      end


c**************************************************************************
c     the following subroutine sets up boundary conditions for the
c     case with neumann conditions on the side and periodic top bottom.
c
c     input:
c
c     levelbox is an array determining the level of each box
c
c     nboxes is the total number of boxes
c
c     nlev is the finest level
c
c     icolbox denotes the column of each box
c
c     irowbox denotes the row of each box
c
c     ichildbox denotes the four children of each box
c
c     iparentbox denotes the parent of each box
c
c     nblevel is the total number of boxes per level
c
c     iboxlev is the array in which the boxes are arranged
c
c     istartlev is the pointer to where each level begins in the
c               iboxlev array
c
c     hl is the function representing the boundary condition imposed
c        on the left
c
c     hr is the function representing the boundary condition imposed
c        on the right
c
c     output:
c
c     ftop is the array of function values representing the single layer
c          densities on the top of each box
c
c     fside is the array of function values representing the single layer
c          densities on the side of each box
c
c     ftopd is the array of function values representing the double layer
c          densities on the top of each box
c
c     fsided is the array of function values representing the double layer
c          densities on the side of each box
c
c**************************************************************************
      subroutine setbcs8neuper(nboxes, nlev, icolbox, irowbox,
     1           ichildbox,  nblevel, iboxlev, istartlev,
     2           ftop, fside, ftopd, fsided, 
     5           doubletoponoff, doublesideonoff,
     6           singletoponoff, singlesideonoff,
     7           hl, hr)
      implicit none
c-----global variables
      integer *4  nboxes, nlev
      integer *4  icolbox(1), irowbox(1)
      integer *4  ichildbox(4,1)
      integer *4  nblevel(0:1), iboxlev(1), istartlev(0:1)
      integer *4  doubletoponoff(1), doublesideonoff(1)
      integer *4  singletoponoff(1), singlesideonoff(1)
      real *8  xshift
      real *8  ftop(8,1), fside(8,1)
      real *8  ftopd(8,1), fsided(8,1)
      real *8  hl, hr
c-----local variables
      integer *4  i, jj, ii, ilev
      integer *4  nside
      integer *4  istart, iend
      real *8  temp3
      real *8  xuse, pi, pi16, x, xx(8)

      pi = dacos(-1.0d0)

      pi16 = pi / 16.0d0
      xx(1) = dcos(15.0d0*pi16) / 2.0d0
      xx(2) = dcos(13.0d0*pi16) / 2.0d0
      xx(3) = dcos(11.0d0*pi16) / 2.0d0
      xx(4) = dcos( 9.0d0*pi16) / 2.0d0
      xx(5) = dcos( 7.0d0*pi16) / 2.0d0
      xx(6) = dcos( 5.0d0*pi16) / 2.0d0
      xx(7) = dcos( 3.0d0*pi16) / 2.0d0
      xx(8) = dcos( 1.0d0*pi16) / 2.0d0


      do ii = 1, nboxes
        doubletoponoff(ii) = 0
        doublesideonoff(ii) = 0
        singletoponoff(ii) = 0
        singlesideonoff(ii) = 0
        do jj = 1, 8
           ftop(jj,ii) = 0.0d0
           ftopd(jj,ii) = 0.0d0
           fside(jj,ii) = 0.0d0
           fsided(jj,ii) = 0.0d0
        end do
      end do

      temp3 = .5d0
      nside = 1
      do ilev = 0, nlev
      istart = istartlev(ilev)
      iend = istart + nblevel(ilev) - 1
      do i = istart, iend
      ii = iboxlev(i)
      if(ichildbox(1,ii) .lt. 0)then

       if(icolbox(ii) .eq. nside/2)then
       if(irowbox(ii) .le. nside/2)then
         singlesideonoff(ii) = 1
         xshift  = dble(irowbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj) / temp3
           fside(jj,ii) = hr(x)
         end do
       endif
       endif

       if(icolbox(ii) .eq. nside/2)then
       if(irowbox(ii) .gt. nside/2)then
         singlesideonoff(ii) = 1
         xshift  = dble(irowbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           xuse = x - 1.0d0
           fside(jj,ii) = hr(xuse)
         end do
       endif
       endif

       if(icolbox(ii) .eq. nside)then
       if(irowbox(ii) .le. nside/2)then
         singlesideonoff(ii) = 1
         xshift  = dble(irowbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           fside(jj,ii) = hl(x)
         end do
       endif
       endif

       if(icolbox(ii) .eq. nside)then
       if(irowbox(ii) .gt. nside/2)then
         singlesideonoff(ii) = 1
         xshift  = dble(irowbox(ii) - .50d0) / temp3 - .50d0
         do jj = 1, 8
           x = xshift + xx(jj)/temp3
           xuse = x - 1.0d0
           fside(jj,ii) = hl(xuse)
         end do
       endif
       endif

      endif
      end do
      temp3 = 2.0d0*temp3
      nside = 2*nside
      end do

c     now scale everything appropriately
c     and account for the sign changes:
      do ii = 1, nboxes
        do jj = 1, 8
           fside(jj,ii) = -fside(jj,ii) / pi
        end do
      end do
      return
      end




c**************************************************************************
c--------------------------------------------------------------------------
c
c     differentiation module
c
c--------------------------------------------------------------------------
c**************************************************************************

c**********************************************************************
c     the following subroutine is set up to calculate both the x and y
c     derivatives of a given function. 
c
c     input:
c
c     pot(16,nboxes) is the quantity being differentiated 
c         defined on the leaf nodes
c
c     levelbox is an array determining the level of each box
c
c     nboxes is the total number of boxes
c
c     nlev is the finest level
c
c     ichildbox denotes the four children of each box
c
c     nblevel is the total number of boxes per level
c
c     iboxlev is the array in which the boxes are arranged
c
c     istartlev is the pointer to where each level begins in the
c               iboxlev array
c
c     coeffs is a dummy array needed to store the polynomial coefficients
c            that approximate pot
c
c     output:
c
c     potx(16,nboxes) is the x derivative defined on the leaf nodes
c
c     poty(16,nboxes) is the y derivative defined on the leaf nodes
c
c**********************************************************************
      subroutine getderivatives8(pot,
     1      nlev, ichildbox, nblevel, iboxlev, istartlev,
     2      potx, poty, potxy, potxx, potyy, lap, coeffs)
      implicit none
c-----global variables
      integer *4  nlev
      integer *4  ichildbox(4,1)
      integer *4  nblevel(0:1), iboxlev(1), istartlev(0:1)
      real *8  coeffs(0:7,0:7,1)
      real *8  pot(64,1)
      real *8  potx(64,1), poty(64,1), potxy(64,1)
      real *8  potxx(64,1),potyy(64,1)
      real *8  lap(64,1) 

c       call a routine to get the matrix a, which will be 
c       needed to calculate the interpolating polynomial
c       for pot.

c       now call a routine that will approximate pot with
c       a polynomial. 
        call mkcoeffs8(coeffs,pot,nlev,
     1    ichildbox, nblevel, iboxlev, istartlev)

c       now call a routine that will calculate the x and y
c       derivatives of pot, given the approximating polynomial
c       coeffs as input.
        call differentiate8(coeffs,potx,poty,potxy,potxx,
     1           potyy, lap, nlev, ichildbox,
     2           nblevel, iboxlev, istartlev)

       return
       end


      subroutine getpotpractical(pot, potx, poty,
     1                potpractical, potpracticalx, 
     2                potpracticaly,
     3                nlev, ichildbox, nblevel,
     4                iboxlev, istartlev, coeffs)
      implicit none
      integer *4  nlev
      integer *4  ichildbox(4,1)
      integer *4  nblevel(0:1), iboxlev(1), istartlev(0:1)
      real *8  pot(64,1), potx(64,1), poty(64,1)
      real *8  potpractical(81,1)
      real *8  potpracticalx(81,1), potpracticaly(81,1)
      real *8  coeffs(0:7,0:7,1)

c       now call a routine that will approximate pot with
c       a polynomial.
        call mkcoeffs8(coeffs,pot,nlev,
     1    ichildbox, nblevel, iboxlev, istartlev)


        call practical(coeffs, potpractical,
     1              nlev, ichildbox,
     2              nblevel, iboxlev, istartlev)

c       now do the same for the x derivative
        call mkcoeffs8(coeffs,potx,nlev,
     1    ichildbox, nblevel, iboxlev, istartlev)


        call practical(coeffs, potpracticalx,
     1              nlev, ichildbox,
     2              nblevel, iboxlev, istartlev)

c       finally, do the same for the y derivative
        call mkcoeffs8(coeffs,poty,nlev,
     1    ichildbox, nblevel, iboxlev, istartlev)


        call practical(coeffs, potpracticaly,
     1              nlev, ichildbox,
     2              nblevel, iboxlev, istartlev)


      return
      end


      subroutine practical(coeffs,potpractical,
     1               nlev, ichildbox,
     2               nblevel, iboxlev, istartlev)
      implicit none
c-----global variables
      integer *4  nlev
      integer *4  ichildbox(4,1)
      integer *4  nblevel(0:1), iboxlev(1), istartlev(0:1)
      real *8  coeffs(0:7,0:7,1)
      real *8  potpractical(81,1)
      real *8  cheb
c-----local variables
      integer *4  i
      integer *4  j, ii, jj, ibox, iii, jjj
      real *8  pi16
      real *8  xf(81), yf(81), x(9), tx(0:7), ty(0:7)

      pi16 = dacos(-1.0d0) / 16.0d0
      x(1) = -1.0d0
      x(2) = dcos(14.0d0*pi16)
      x(3) = dcos(12.0d0*pi16)
      x(4) = dcos(10.0d0*pi16)
      x(5) = 0.0d0
      x(6) = dcos( 6.0d0*pi16)
      x(7) = dcos( 4.0d0*pi16)
      x(8) = dcos( 2.0d0*pi16)
      x(9) = 1.0d0


      do jj = 0,nlev
       do i = 1, 9
       do j = 1, 9
         xf(i+9*(j-1)) = x(i)
         yf(j+9*(i-1)) = x(i)
       end do
       end do


        do 100 ii = istartlev(jj), istartlev(jj) + nblevel(jj) - 1
         ibox = iboxlev(ii)
c        only find the derivative for childless boxes:
         if(ichildbox(1,ibox) .gt. 0)goto 100

         do i = 1, 81

         do iii = 0, 7
         do jjj = 0, 7
           tx(iii) = cheb(xf(i),iii)
           ty(jjj) = cheb(yf(i),jjj)
         end do
         end do

         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(0,0,ibox)*tx(0)* ty(0)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(1,0,ibox)*tx(1)* ty(0)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(0,1,ibox)*tx(0)* ty(1)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(2,0,ibox)*tx(2)* ty(0)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(1,1,ibox)*tx(1)* ty(1)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(0,2,ibox)*tx(0)* ty(2)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(3,0,ibox)*tx(3)* ty(0)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(2,1,ibox)*tx(2)* ty(1)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(1,2,ibox)*tx(1)* ty(2)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(0,3,ibox)*tx(0)* ty(3)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(4,0,ibox)*tx(4)* ty(0)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(3,1,ibox)*tx(3)* ty(1)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(2,2,ibox)*tx(2)* ty(2)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(1,3,ibox)*tx(1)* ty(3)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(0,4,ibox)*tx(0)* ty(4)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(5,0,ibox)*tx(5)* ty(0)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(4,1,ibox)*tx(4)* ty(1)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(3,1,ibox)*tx(3)* ty(2)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(2,3,ibox)*tx(2)* ty(3)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(1,4,ibox)*tx(1)* ty(4)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(0,5,ibox)*tx(0)* ty(5)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(6,0,ibox)*tx(6)* ty(0)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(5,1,ibox)*tx(5)* ty(1)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(4,2,ibox)*tx(4)* ty(2)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(3,3,ibox)*tx(3)* ty(3)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(2,4,ibox)*tx(2)* ty(4)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(1,5,ibox)*tx(1)* ty(5)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(0,6,ibox)*tx(0)* ty(6)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(7,0,ibox)*tx(7)* ty(0)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(6,1,ibox)*tx(6)* ty(1)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(5,2,ibox)*tx(5)* ty(2)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(4,3,ibox)*tx(4)* ty(3)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(3,4,ibox)*tx(3)* ty(4)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(2,5,ibox)*tx(2)* ty(5)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(1,6,ibox)*tx(1)* ty(6)
         potpractical(i,ibox) = potpractical(i,ibox) 
     1                        + coeffs(0,7,ibox)*tx(0)* ty(7)

        end do
100     continue
      end do
      return
      end


c**********************************************************************
c     the following subroutine computes the x and y derivatives of the 
c     solution, given the approximating polynomial and tree as input.
c     the algorithm works just by differentiating the approximating
c     polynomial.
c
c
c     input:
c
c     coeffs is a the array containing polynomial coefficients
c            that approximate pot
c
c     nlev is the finest level
c
c     nboxes is the total number of boxes
c
c     ichildbox denotes the four children of each box
c
c     nblevel is the total number of boxes per level
c
c     iboxlev is the array in which the boxes are arranged
c
c     istartlev is the pointer to where each level begins in the
c               iboxlev array
c
c     output:
c
c     potx(64,nboxes) is the x derivative defined on the leaf nodes
c
c     poty(64,nboxes) is the y derivative defined on the leaf nodes
c
c**********************************************************************
      subroutine differentiate8(coeffs,potx,poty,potxy,potxx,potyy,
     1               lap,nlev, ichildbox,
     2               nblevel, iboxlev, istartlev)
      implicit none
c-----global variables
      integer *4  nlev
      integer *4  ichildbox(4,1)
      integer *4  nblevel(0:1), iboxlev(1), istartlev(0:1)
      real *8  coeffs(0:7,0:7,1)
      real *8  potx(64,1), poty(64,1), potxy(64,1)
      real *8  potxx(64,1),potyy(64,1)
      real *8  lap(64,1)
      real *8  cheb
c-----local variables
      integer *4  i
      integer *4  j, ii, jj, ibox, iii, jjj
      integer *4 l, istart, iend, k
      real *8  pi16, xlength
      real *8  xf(64), yf(64), x(8), tx(0:7), ty(0:7)
      real *8  txdiff(7), tydiff(7)
      real *8  txlap(2:7), tylap(2:7)
c
      do l=0,nlev
        istart=istartlev(l) 
        iend=istart+nblevel(l)-1
        do ii=istart,iend
          i=iboxlev(ii)
          do k=1,64
            potx(k,i)=0.0d0
            poty(k,i)=0.0d0
            potxy(k,i)=0.0d0
            potxx(k,i)=0.0d0
            potyy(k,i)=0.0d0
            lap(k,i)=0.0d0
          enddo
        enddo
      enddo
c     NEED TO initialize to zero, since everything's
c     incremental below here!!! -J.W.

      pi16 = dacos(-1.0d0) / 16.0d0
      x(1) = dcos(15.0d0*pi16)
      x(2) = dcos(13.0d0*pi16)
      x(3) = dcos(11.0d0*pi16)
      x(4) = dcos( 9.0d0*pi16)
      x(5) = dcos( 7.0d0*pi16)
      x(6) = dcos( 5.0d0*pi16)
      x(7) = dcos( 3.0d0*pi16)
      x(8) = dcos( 1.0d0*pi16)


      xlength = 0.5d0
      do jj = 0,nlev
       do i = 1, 8
       do j = 1, 8
         xf(i+8*(j-1)) = x(i)
         yf(j+8*(i-1)) = x(i)
       end do
       end do


        do 100 ii = istartlev(jj), istartlev(jj) + nblevel(jj) - 1
         ibox = iboxlev(ii)
c        only find the derivative for childless boxes:
         if(ichildbox(1,ibox) .gt. 0)goto 100

         do i = 1, 64

         do iii = 0, 7
         do jjj = 0, 7
           tx(iii) = cheb(xf(i),iii)
           ty(jjj) = cheb(yf(i),jjj)
         end do
         end do

         txdiff(1) =        tx(0)
         txdiff(2) =  4.0d0*tx(1)
         txdiff(3) =  6.0d0*tx(2) +  3.0d0*tx(0)
         txdiff(4) =  8.0d0*tx(3) +  8.0d0*tx(1)
         txdiff(5) = 10.0d0*tx(4) + 10.0d0*tx(2) +  5.0d0*tx(0)
         txdiff(6) = 12.0d0*tx(5) + 12.0d0*tx(3) + 12.0d0*tx(1)
         txdiff(7) = 14.0d0*tx(6) + 14.0d0*tx(4) 
     1             + 14.0d0*tx(2) +  7.0d0*tx(0)

         tydiff(1) =        ty(0)
         tydiff(2) =  4.0d0*ty(1)
         tydiff(3) =  6.0d0*ty(2) +  3.0d0*ty(0)
         tydiff(4) =  8.0d0*ty(3) +  8.0d0*ty(1)
         tydiff(5) = 10.0d0*ty(4) + 10.0d0*ty(2) +  5.0d0*ty(0)
         tydiff(6) = 12.0d0*ty(5) + 12.0d0*ty(3) + 12.0d0*ty(1)
         tydiff(7) = 14.0d0*ty(6) + 14.0d0*ty(4) 
     1             + 14.0d0*ty(2) +  7.0d0*ty(0)

         do iii = 1, 7
           txdiff(iii) = txdiff(iii) / xlength
           tydiff(iii) = tydiff(iii) / xlength
         end do

         potx(i,ibox) = potx(i,ibox) + coeffs(1,0,ibox)*txdiff(1)
         potx(i,ibox) = potx(i,ibox) + coeffs(2,0,ibox)*txdiff(2)
         potx(i,ibox) = potx(i,ibox) + coeffs(3,0,ibox)*txdiff(3)
         potx(i,ibox) = potx(i,ibox) + coeffs(4,0,ibox)*txdiff(4)
         potx(i,ibox) = potx(i,ibox) + coeffs(5,0,ibox)*txdiff(5)
         potx(i,ibox) = potx(i,ibox) + coeffs(6,0,ibox)*txdiff(6)
         potx(i,ibox) = potx(i,ibox) + coeffs(7,0,ibox)*txdiff(7)
         potx(i,ibox) = potx(i,ibox) + coeffs(1,1,ibox)*txdiff(1)*ty(1)
         potx(i,ibox) = potx(i,ibox) + coeffs(2,1,ibox)*txdiff(2)*ty(1)
         potx(i,ibox) = potx(i,ibox) + coeffs(3,1,ibox)*txdiff(3)*ty(1)
         potx(i,ibox) = potx(i,ibox) + coeffs(4,1,ibox)*txdiff(4)*ty(1)
         potx(i,ibox) = potx(i,ibox) + coeffs(5,1,ibox)*txdiff(5)*ty(1)
         potx(i,ibox) = potx(i,ibox) + coeffs(6,1,ibox)*txdiff(6)*ty(1)
         potx(i,ibox) = potx(i,ibox) + coeffs(1,2,ibox)*txdiff(1)*ty(2)
         potx(i,ibox) = potx(i,ibox) + coeffs(2,2,ibox)*txdiff(2)*ty(2)
         potx(i,ibox) = potx(i,ibox) + coeffs(3,2,ibox)*txdiff(3)*ty(2)
         potx(i,ibox) = potx(i,ibox) + coeffs(4,2,ibox)*txdiff(4)*ty(2)
         potx(i,ibox) = potx(i,ibox) + coeffs(5,2,ibox)*txdiff(5)*ty(2)
         potx(i,ibox) = potx(i,ibox) + coeffs(1,3,ibox)*txdiff(1)*ty(3)
         potx(i,ibox) = potx(i,ibox) + coeffs(2,3,ibox)*txdiff(2)*ty(3)
         potx(i,ibox) = potx(i,ibox) + coeffs(3,3,ibox)*txdiff(3)*ty(3)
         potx(i,ibox) = potx(i,ibox) + coeffs(4,3,ibox)*txdiff(4)*ty(3)
         potx(i,ibox) = potx(i,ibox) + coeffs(1,4,ibox)*txdiff(1)*ty(4)
         potx(i,ibox) = potx(i,ibox) + coeffs(2,4,ibox)*txdiff(2)*ty(4)
         potx(i,ibox) = potx(i,ibox) + coeffs(3,4,ibox)*txdiff(3)*ty(4)
         potx(i,ibox) = potx(i,ibox) + coeffs(1,5,ibox)*txdiff(1)*ty(5)
         potx(i,ibox) = potx(i,ibox) + coeffs(2,5,ibox)*txdiff(2)*ty(5)
         potx(i,ibox) = potx(i,ibox) + coeffs(1,6,ibox)*txdiff(1)*ty(6)

         poty(i,ibox) = poty(i,ibox) + coeffs(0,1,ibox)*tydiff(1)
         poty(i,ibox) = poty(i,ibox) + coeffs(0,2,ibox)*tydiff(2)
         poty(i,ibox) = poty(i,ibox) + coeffs(0,3,ibox)*tydiff(3)
         poty(i,ibox) = poty(i,ibox) + coeffs(0,4,ibox)*tydiff(4)
         poty(i,ibox) = poty(i,ibox) + coeffs(0,5,ibox)*tydiff(5)
         poty(i,ibox) = poty(i,ibox) + coeffs(0,6,ibox)*tydiff(6)
         poty(i,ibox) = poty(i,ibox) + coeffs(0,7,ibox)*tydiff(7)
         poty(i,ibox) = poty(i,ibox) + coeffs(1,1,ibox)*tx(1)*tydiff(1)
         poty(i,ibox) = poty(i,ibox) + coeffs(1,2,ibox)*tx(1)*tydiff(2)
         poty(i,ibox) = poty(i,ibox) + coeffs(1,3,ibox)*tx(1)*tydiff(3)
         poty(i,ibox) = poty(i,ibox) + coeffs(1,4,ibox)*tx(1)*tydiff(4)
         poty(i,ibox) = poty(i,ibox) + coeffs(1,5,ibox)*tx(1)*tydiff(5)
         poty(i,ibox) = poty(i,ibox) + coeffs(1,6,ibox)*tx(1)*tydiff(6)
         poty(i,ibox) = poty(i,ibox) + coeffs(2,1,ibox)*tx(2)*tydiff(1)
         poty(i,ibox) = poty(i,ibox) + coeffs(2,2,ibox)*tx(2)*tydiff(2)
         poty(i,ibox) = poty(i,ibox) + coeffs(2,3,ibox)*tx(2)*tydiff(3)
         poty(i,ibox) = poty(i,ibox) + coeffs(2,4,ibox)*tx(2)*tydiff(4)
         poty(i,ibox) = poty(i,ibox) + coeffs(2,5,ibox)*tx(2)*tydiff(5)
         poty(i,ibox) = poty(i,ibox) + coeffs(3,1,ibox)*tx(3)*tydiff(1)
         poty(i,ibox) = poty(i,ibox) + coeffs(3,2,ibox)*tx(3)*tydiff(2)
         poty(i,ibox) = poty(i,ibox) + coeffs(3,3,ibox)*tx(3)*tydiff(3)
         poty(i,ibox) = poty(i,ibox) + coeffs(3,4,ibox)*tx(3)*tydiff(4)
         poty(i,ibox) = poty(i,ibox) + coeffs(4,1,ibox)*tx(4)*tydiff(1)
         poty(i,ibox) = poty(i,ibox) + coeffs(4,2,ibox)*tx(4)*tydiff(2)
         poty(i,ibox) = poty(i,ibox) + coeffs(4,3,ibox)*tx(4)*tydiff(3)
         poty(i,ibox) = poty(i,ibox) + coeffs(5,1,ibox)*tx(5)*tydiff(1)
         poty(i,ibox) = poty(i,ibox) + coeffs(5,2,ibox)*tx(5)*tydiff(2)
         poty(i,ibox) = poty(i,ibox) + coeffs(6,1,ibox)*tx(6)*tydiff(1)

         potxy(i,ibox) = potxy(i,ibox) + coeffs(1,1,ibox)*
     1                 txdiff(1)*tydiff(1)
         potxy(i,ibox) = potxy(i,ibox) + coeffs(1,2,ibox)*
     1                 txdiff(1)*tydiff(2)
         potxy(i,ibox) = potxy(i,ibox) + coeffs(1,3,ibox)*
     1                 txdiff(1)*tydiff(3)
         potxy(i,ibox) = potxy(i,ibox) + coeffs(1,4,ibox)*
     1                 txdiff(1)*tydiff(4)
         potxy(i,ibox) = potxy(i,ibox) + coeffs(1,5,ibox)*
     1                 txdiff(1)*tydiff(5)
         potxy(i,ibox) = potxy(i,ibox) + coeffs(1,6,ibox)*
     1                 txdiff(1)*tydiff(6)
         potxy(i,ibox) = potxy(i,ibox) + coeffs(2,1,ibox)*
     1                 txdiff(2)*tydiff(1)
         potxy(i,ibox) = potxy(i,ibox) + coeffs(2,2,ibox)*
     1                 txdiff(2)*tydiff(2)
         potxy(i,ibox) = potxy(i,ibox) + coeffs(2,3,ibox)*
     1                 txdiff(2)*tydiff(3)
         potxy(i,ibox) = potxy(i,ibox) + coeffs(2,4,ibox)*
     1                 txdiff(2)*tydiff(4)
         potxy(i,ibox) = potxy(i,ibox) + coeffs(2,5,ibox)*
     1                 txdiff(2)*tydiff(5)
         potxy(i,ibox) = potxy(i,ibox) + coeffs(3,1,ibox)*
     1                 txdiff(3)*tydiff(1)
         potxy(i,ibox) = potxy(i,ibox) + coeffs(3,2,ibox)*
     1                 txdiff(3)*tydiff(2)
         potxy(i,ibox) = potxy(i,ibox) + coeffs(3,3,ibox)*
     1                 txdiff(3)*tydiff(3)
         potxy(i,ibox) = potxy(i,ibox) + coeffs(3,4,ibox)*
     1                 txdiff(3)*tydiff(4)
         potxy(i,ibox) = potxy(i,ibox) + coeffs(4,1,ibox)*
     1                 txdiff(4)*tydiff(1)
         potxy(i,ibox) = potxy(i,ibox) + coeffs(4,2,ibox)*
     1                 txdiff(4)*tydiff(2)
         potxy(i,ibox) = potxy(i,ibox) + coeffs(4,3,ibox)*
     1                 txdiff(4)*tydiff(3)
         potxy(i,ibox) = potxy(i,ibox) + coeffs(5,1,ibox)*
     1                 txdiff(5)*tydiff(1)
         potxy(i,ibox) = potxy(i,ibox) + coeffs(5,2,ibox)*
     1                 txdiff(5)*tydiff(2)
         potxy(i,ibox) = potxy(i,ibox) + coeffs(6,1,ibox)*
     1                 txdiff(6)*tydiff(1)

         txlap(2) =   4.0d0*tx(0)
         txlap(3) =  24.0d0*tx(1)
         txlap(4) =  48.0d0*tx(2) +  32.0d0*tx(0)
         txlap(5) =  80.0d0*tx(3) + 120.0d0*tx(1)
         txlap(6) = 120.0d0*tx(4) + 192.0d0*tx(2) + 108.0d0*tx(0)
         txlap(7) = 168.0d0*tx(5) + 280.0d0*tx(3) + 336.0d0*tx(1)


         tylap(2) =   4.0d0*ty(0)
         tylap(3) =  24.0d0*ty(1)
         tylap(4) =  48.0d0*ty(2) +  32.0d0*ty(0)
         tylap(5) =  80.0d0*ty(3) + 120.0d0*ty(1)
         tylap(6) = 120.0d0*ty(4) + 192.0d0*ty(2) + 108.0d0*ty(0)
         tylap(7) = 168.0d0*ty(5) + 280.0d0*ty(3) + 336.0d0*ty(1)

         do iii = 2, 7
           txlap(iii) = txlap(iii) / xlength**2
           tylap(iii) = tylap(iii) / xlength**2
         end do

         potxx(i,ibox) = potxx(i,ibox) + coeffs(2,0,ibox)*txlap(2)
         potxx(i,ibox) = potxx(i,ibox) + coeffs(2,1,ibox)*txlap(2)*ty(1)
         potxx(i,ibox) = potxx(i,ibox) + coeffs(2,2,ibox)*txlap(2)*ty(2)
         potxx(i,ibox) = potxx(i,ibox) + coeffs(2,3,ibox)*txlap(2)*ty(3)
         potxx(i,ibox) = potxx(i,ibox) + coeffs(2,4,ibox)*txlap(2)*ty(4)
         potxx(i,ibox) = potxx(i,ibox) + coeffs(2,5,ibox)*txlap(2)*ty(5)
         potxx(i,ibox) = potxx(i,ibox) + coeffs(3,0,ibox)*txlap(3)
         potxx(i,ibox) = potxx(i,ibox) + coeffs(3,1,ibox)*txlap(3)*ty(1)
         potxx(i,ibox) = potxx(i,ibox) + coeffs(3,2,ibox)*txlap(3)*ty(2)
         potxx(i,ibox) = potxx(i,ibox) + coeffs(3,3,ibox)*txlap(3)*ty(3)
         potxx(i,ibox) = potxx(i,ibox) + coeffs(3,4,ibox)*txlap(3)*ty(4)
         potxx(i,ibox) = potxx(i,ibox) + coeffs(4,0,ibox)*txlap(4)
         potxx(i,ibox) = potxx(i,ibox) + coeffs(4,1,ibox)*txlap(4)*ty(1)
         potxx(i,ibox) = potxx(i,ibox) + coeffs(4,2,ibox)*txlap(4)*ty(2)
         potxx(i,ibox) = potxx(i,ibox) + coeffs(4,3,ibox)*txlap(4)*ty(3)
         potxx(i,ibox) = potxx(i,ibox) + coeffs(5,0,ibox)*txlap(5)
         potxx(i,ibox) = potxx(i,ibox) + coeffs(5,1,ibox)*txlap(5)*ty(1)
         potxx(i,ibox) = potxx(i,ibox) + coeffs(5,2,ibox)*txlap(5)*ty(2)
         potxx(i,ibox) = potxx(i,ibox) + coeffs(6,0,ibox)*txlap(6)*ty(0)
         potxx(i,ibox) = potxx(i,ibox) + coeffs(6,1,ibox)*txlap(6)*ty(1)
         potxx(i,ibox) = potxx(i,ibox) + coeffs(7,0,ibox)*txlap(7)

         potyy(i,ibox) = potyy(i,ibox) + coeffs(0,2,ibox)*tylap(2)
         potyy(i,ibox) = potyy(i,ibox) + coeffs(1,2,ibox)*tx(1)*tylap(2)
         potyy(i,ibox) = potyy(i,ibox) + coeffs(2,2,ibox)*tx(2)*tylap(2)
         potyy(i,ibox) = potyy(i,ibox) + coeffs(3,2,ibox)*tx(3)*tylap(2)
         potyy(i,ibox) = potyy(i,ibox) + coeffs(4,2,ibox)*tx(4)*tylap(2)
         potyy(i,ibox) = potyy(i,ibox) + coeffs(5,2,ibox)*tx(5)*tylap(2)
         potyy(i,ibox) = potyy(i,ibox) + coeffs(0,3,ibox)*tylap(3)
         potyy(i,ibox) = potyy(i,ibox) + coeffs(1,3,ibox)*tx(1)*tylap(3)
         potyy(i,ibox) = potyy(i,ibox) + coeffs(2,3,ibox)*tx(2)*tylap(3)
         potyy(i,ibox) = potyy(i,ibox) + coeffs(3,3,ibox)*tx(3)*tylap(3)
         potyy(i,ibox) = potyy(i,ibox) + coeffs(4,3,ibox)*tx(4)*tylap(3)
         potyy(i,ibox) = potyy(i,ibox) + coeffs(0,4,ibox)*tylap(4)
         potyy(i,ibox) = potyy(i,ibox) + coeffs(1,4,ibox)*tx(1)*tylap(4)
         potyy(i,ibox) = potyy(i,ibox) + coeffs(2,4,ibox)*tx(2)*tylap(4)
         potyy(i,ibox) = potyy(i,ibox) + coeffs(3,4,ibox)*tx(3)*tylap(4)
         potyy(i,ibox) = potyy(i,ibox) + coeffs(0,5,ibox)*tylap(5)
         potyy(i,ibox) = potyy(i,ibox) + coeffs(1,5,ibox)*tx(1)*tylap(5)
         potyy(i,ibox) = potyy(i,ibox) + coeffs(2,5,ibox)*tylap(5)
         potyy(i,ibox) = potyy(i,ibox) + coeffs(0,6,ibox)*tx(0)*tylap(6)
         potyy(i,ibox) = potyy(i,ibox) + coeffs(1,6,ibox)*tx(1)*tylap(6)
         potyy(i,ibox) = potyy(i,ibox) + coeffs(0,7,ibox)*tylap(7)


        
         lap(i,ibox) = lap(i,ibox) + coeffs(2,0,ibox)*txlap(2)
         lap(i,ibox) = lap(i,ibox) + coeffs(0,2,ibox)*tylap(2)
         lap(i,ibox) = lap(i,ibox) + coeffs(3,0,ibox)*txlap(3) 
         lap(i,ibox) = lap(i,ibox) + coeffs(2,1,ibox)*txlap(2)*ty(1) 
         lap(i,ibox) = lap(i,ibox) + coeffs(1,2,ibox)*tx(1)*tylap(2)
         lap(i,ibox) = lap(i,ibox) + coeffs(0,3,ibox)*tylap(3)
         lap(i,ibox) = lap(i,ibox) + coeffs(4,0,ibox)*txlap(4)
         lap(i,ibox) = lap(i,ibox) + coeffs(3,1,ibox)*txlap(3)*ty(1)
         lap(i,ibox) = lap(i,ibox) + coeffs(2,2,ibox)*
     1                             (tx(2)*tylap(2) + txlap(2)*ty(2))
         lap(i,ibox) = lap(i,ibox) + coeffs(1,3,ibox)*tx(1)*tylap(3)
         lap(i,ibox) = lap(i,ibox) + coeffs(0,4,ibox)*tylap(4)
         lap(i,ibox) = lap(i,ibox) + coeffs(5,0,ibox)*txlap(5)
         lap(i,ibox) = lap(i,ibox) + coeffs(4,1,ibox)*txlap(4)*ty(1)
         lap(i,ibox) = lap(i,ibox) + coeffs(3,2,ibox)*
     1                             (tx(3)*tylap(2) + txlap(3)*ty(2))
         lap(i,ibox) = lap(i,ibox) + coeffs(2,3,ibox)*
     1                             (tx(2)*tylap(3) + txlap(2)*ty(3))
         lap(i,ibox) = lap(i,ibox) + coeffs(1,4,ibox)*tx(1)*tylap(4)
         lap(i,ibox) = lap(i,ibox) + coeffs(0,5,ibox)*tylap(5)
         lap(i,ibox) = lap(i,ibox) + coeffs(6,0,ibox)*txlap(6)
         lap(i,ibox) = lap(i,ibox) + coeffs(5,1,ibox)*txlap(5)*ty(1)
         lap(i,ibox) = lap(i,ibox) + coeffs(4,2,ibox)*
     1                             (txlap(4)*ty(2) + tx(4)*tylap(2))
         lap(i,ibox) = lap(i,ibox) + coeffs(3,3,ibox)*
     1                             (txlap(3)*ty(3) + tx(3)*tylap(3))
         lap(i,ibox) = lap(i,ibox) + coeffs(2,4,ibox)*
     1                             (txlap(2)*ty(4) + tx(2)*tylap(4))
         lap(i,ibox) = lap(i,ibox) + coeffs(1,5,ibox)*tx(1)*tylap(5)
         lap(i,ibox) = lap(i,ibox) + coeffs(0,6,ibox)*tylap(6)
         lap(i,ibox) = lap(i,ibox) + coeffs(7,0,ibox)*txlap(7)
         lap(i,ibox) = lap(i,ibox) + coeffs(6,1,ibox)*txlap(6)*ty(1)
         lap(i,ibox) = lap(i,ibox) + coeffs(5,2,ibox)*
     1                             (txlap(5)*ty(2) + tx(5)*tylap(2))
         lap(i,ibox) = lap(i,ibox) + coeffs(4,3,ibox)*
     1                             (txlap(4)*ty(3) + tx(4)*tylap(3))
         lap(i,ibox) = lap(i,ibox) + coeffs(3,4,ibox)*
     1                             (txlap(3)*ty(4) + tx(3)*tylap(4))
         lap(i,ibox) = lap(i,ibox) + coeffs(2,5,ibox)*
     1                             (txlap(2)*ty(5) + tx(2)*tylap(5))
         lap(i,ibox) = lap(i,ibox) + coeffs(1,6,ibox)*tx(1)*tylap(6)
         lap(i,ibox) = lap(i,ibox) + coeffs(0,7,ibox)*tylap(7)


        end do
100     continue
       xlength = xlength / 2.0d0
      end do
      return
      end






c**************************************************************************
c--------------------------------------------------------------------------
c
c     function module
c
c--------------------------------------------------------------------------
c**************************************************************************
c
      function cheb(x,i)
      implicit none  
      integer *4  i
      real *8  cheb, x

        if(i .eq. 0)then
          cheb = 1.0d0
        elseif(i .eq. 1)then
           cheb = x
        elseif(i .eq. 2)then
           cheb = 2.0d0*x**2 - 1.0d0
        elseif(i .eq. 3)then
           cheb = 4.0d0*x**3 - 3.0d0*x
        elseif(i .eq. 4)then
           cheb = 8.0d0*x**4 - 8.0d0*x**2 + 1.0d0
        elseif(i .eq. 5)then
           cheb = 16.0d0*x**5 - 20.0d0*x**3 + 5.0d0*x
        elseif(i .eq. 6)then
           cheb = 32.0d0*x**6 - 48.0d0*x**4 + 18.0d0*x**2 - 1.0d0
        elseif(i .eq. 7)then
           cheb = 64.0d0*x**7 - 112.0d0*x**5 + 56.0d0*x**3 - 7.0d0*x
        endif

      return
      end





c----------------------------------------------------------
c     subroutines from aquad.f
c----------------------------------------------------------
c
      subroutine mksur(x,y,value,ftest)
      implicit none
      external ftest
      integer key, n, nf, ndim, mincls, maxcls, ifail, neval, nw
      parameter (ndim = 2, nw = 1000000, nf = 1)
      real *8 a(ndim), b(ndim), wrkstr(nw)
      real *8 absest(nf), finest(nf), absreq, relreq
      real *8 xtarg,ytarg
      real *8 x, y, value
      common  xtarg,ytarg
      xtarg = x
      ytarg = y

      do 10 n = 1,ndim
         a(n) = -0.5d0
         b(n) =  0.5d0
   10 continue
      mincls = 0
      maxcls = 300000
      key = 0
      absreq = 0
      relreq = 1d-14

      call dcuhre(ndim, nf, a, b, mincls, maxcls, ftest, 
     1      absreq, relreq, key, nw, 0, finest, absest, neval,
     2      ifail, wrkstr)

      value = finest(1)
      end


      subroutine ftest(ndim, z, nfun, f, h2)
      integer ndim, nfun
      real *8 z(ndim), f(nfun), rx, ry, pi
      real *8 rr,reps,dgreen
      real *8 xtarg,ytarg
      real *8 h2
      external h2
      common  xtarg,ytarg
      pi = 4.0d0*datan(1.0d0)
      rx = z(1) - xtarg
      ry = z(2) - ytarg
      rr = dsqrt(rx*rx +ry*ry)
      reps = 1.0d-10
      if (rr.gt.reps) then
         dgreen = dlog(rr)/(2.0d0*pi)
         f(1) = dgreen * h2(z(1),z(2))
      else
         f(1) = 0.0d0
      endif
      end


      subroutine d07hre(ndim,wtleng,w,g,errcof,rulpts)
c***begin prologue d07hre
c***keywords basic integration rule, degree 7
c***purpose  to initialize a degree 7 basic rule, and null rules.
c***author   alan genz, computer science department, washington
c            state university, pullman, wa 99163-1210 usa
c***last modification 88-05-31
c***description  d07hre initializes a degree 7 integration rule,
c            two degree 5 null rules, one degree 3 null rule and one
c            degree 1 null rule for the hypercube [-1,1]**ndim.
c
c   on entry
c
c   ndim   integer.
c          number of variables.
c   wtleng integer.
c          the number of weights in each of the rules.
c          wtleng must be set equal to 6.
c
c   on return
c   w      real array of dimension (5,wtleng).
c          the weights for the basic and null rules.
c          w(1,1),...,w(1,wtleng) are weights for the basic rule.
c          w(i,1),...,w(i,wtleng), for i > 1 are null rule weights.
c   g      real array of dimension (ndim, wtleng).
c          the fully symmetric sum generators for the rules.
c          g(1, j), ..., g(ndim, j) are the are the generators for the
c          points associated with the jth weights.
c   errcof real array of dimension 6.
c          heuristic error coefficients that are used in the
c          error estimation in basrul.
c   rulpts real array of dimension wtleng.
c          a work array.
c
c***references a. genz and a. malik,
c             "an imbedded family of fully symmetric numerical
c              integration rules",
c              siam j numer. anal. 20 (1983), pp. 580-588.
c***routines called-none
c***end prologue d07hre
c
c   global variables
c
      integer ndim,wtleng
      real *8 w(5,wtleng),g(ndim,wtleng),errcof(6)
      real *8 rulpts(wtleng)
c
c   local variables
c
      real *8 ratio,lam0,lam1,lam2,lamp,twondm
      integer i,j
c
c***first executable statement d07hre
c
c
c     initialize generators, weights and rulpts
c
      do 30 j = 1,wtleng
          do 10 i = 1,ndim
              g(i,j) = 0
10        continue
          do 20 i = 1,5
              w(i,j) = 0
20        continue
          rulpts(j) = 2*ndim
30    continue
      twondm = 2**ndim
      rulpts(wtleng) = twondm
      rulpts(wtleng-1) = 2*ndim* (ndim-1)
      rulpts(1) = 1
c
c     compute squared generator parameters
c
      lam0 = 0.4707
      lamp = 0.5625
      lam1 = 4/ (15-5/lam0)
      ratio = (1-lam1/lam0)/27
      lam2 = (5-7*lam1-35*ratio)/ (7-35*lam1/3-35*ratio/lam0)
c
c     compute degree 7 rule weights
c
      w(1,6) = 1/ (3*lam0)**3/twondm
      w(1,5) = (1-5*lam0/3)/ (60* (lam1-lam0)*lam1**2)
      w(1,3) = (1-5*lam2/3-5*twondm*w(1,6)*lam0* (lam0-lam2))/
     +         (10*lam1* (lam1-lam2)) - 2* (ndim-1)*w(1,5)
      w(1,2) = (1-5*lam1/3-5*twondm*w(1,6)*lam0* (lam0-lam1))/
     +         (10*lam2* (lam2-lam1))
c
c     compute weights for 2 degree 5, 1 degree 3 and 1 degree 1 rules
c
      w(2,6) = 1/ (36*lam0**3)/twondm
      w(2,5) = (1-9*twondm*w(2,6)*lam0**2)/ (36*lam1**2)
      w(2,3) = (1-5*lam2/3-5*twondm*w(2,6)*lam0* (lam0-lam2))/
     +         (10*lam1* (lam1-lam2)) - 2* (ndim-1)*w(2,5)
      w(2,2) = (1-5*lam1/3-5*twondm*w(2,6)*lam0* (lam0-lam1))/
     +         (10*lam2* (lam2-lam1))
      w(3,6) = 5/ (108*lam0**3)/twondm
      w(3,5) = (1-9*twondm*w(3,6)*lam0**2)/ (36*lam1**2)
      w(3,3) = (1-5*lamp/3-5*twondm*w(3,6)*lam0* (lam0-lamp))/
     +         (10*lam1* (lam1-lamp)) - 2* (ndim-1)*w(3,5)
      w(3,4) = (1-5*lam1/3-5*twondm*w(3,6)*lam0* (lam0-lam1))/
     +         (10*lamp* (lamp-lam1))
      w(4,6) = 1/ (54*lam0**3)/twondm
      w(4,5) = (1-18*twondm*w(4,6)*lam0**2)/ (72*lam1**2)
      w(4,3) = (1-10*lam2/3-10*twondm*w(4,6)*lam0* (lam0-lam2))/
     +         (20*lam1* (lam1-lam2)) - 2* (ndim-1)*w(4,5)
      w(4,2) = (1-10*lam1/3-10*twondm*w(4,6)*lam0* (lam0-lam1))/
     +         (20*lam2* (lam2-lam1))
c
c     set generator values
c
      lam0 = sqrt(lam0)
      lam1 = sqrt(lam1)
      lam2 = sqrt(lam2)
      lamp = sqrt(lamp)
      do 40 i = 1,ndim
          g(i,wtleng) = lam0
40    continue
      g(1,wtleng-1) = lam1
      g(2,wtleng-1) = lam1
      g(1,wtleng-4) = lam2
      g(1,wtleng-3) = lam1
      g(1,wtleng-2) = lamp
c
c     compute final weight values.
c     the null rule weights are computed from differences between
c     the degree 7 rule weights and lower degree rule weights.
c
      w(1,1) = twondm
      do 70 j = 2,5
          do 50 i = 2,wtleng
              w(j,i) = w(j,i) - w(1,i)
              w(j,1) = w(j,1) - rulpts(i)*w(j,i)
50        continue
70    continue
      do 80 i = 2,wtleng
          w(1,i) = twondm*w(1,i)
          w(1,1) = w(1,1) - rulpts(i)*w(1,i)
80    continue
c
c     set error coefficients
c
      errcof(1) = 5
      errcof(2) = 5
      errcof(3) = 1
      errcof(4) = 5
      errcof(5) = 0.5
      errcof(6) = 0.25
c
c***end d07hre
c
      end
      subroutine d09hre(ndim,wtleng,w,g,errcof,rulpts)
c***begin prologue d09hre
c***keywords basic integration rule, degree 9
c***purpose  to initialize a degree 9 basic rule and null rules.
c***author   alan genz, computer science department, washington
c            state university, pullman, wa 99163-1210 usa
c***last modification 88-05-20
c***description  d09hre initializes a degree 9 integration rule,
c            two degree 7 null rules, one degree 5 null rule and one
c            degree 3 null rule for the hypercube [-1,1]**ndim.
c
c   on entry
c
c   ndim   integer.
c          number of variables.
c   wtleng integer.
c          the number of weights in each of the rules.
c
c   on return
c   w      real array of dimension (5,wtleng).
c          the weights for the basic and null rules.
c          w(1,1),...,w(1,wtleng) are weights for the basic rule.
c          w(i,1),...,w(i,wtleng), for i > 1 are null rule weights.
c   g      real array of dimension (ndim, wtleng).
c          the fully symmetric sum generators for the rules.
c          g(1, j), ..., g(ndim, j) are the are the generators for the
c          points associated with the jth weights.
c   errcof real array of dimension 6.
c          heuristic error coefficients that are used in the
c          error estimation in basrul.
c   rulpts real array of dimension wtleng.
c          a work array.
c
c***references a. genz and a. malik,
c             "an imbedded family of fully symmetric numerical
c              integration rules",
c              siam j numer. anal. 20 (1983), pp. 580-588.
c***routines called-none
c***end prologue d09hre
c
c   global variables
c
      integer ndim,wtleng
      real *8 w(5,wtleng),g(ndim,wtleng),errcof(6)
      real *8 rulpts(wtleng)
c
c   local variables
c
      real *8 ratio,lam0,lam1,lam2,lam3,lamp,twondm
      integer i,j
c
c***first executable statement d09hre
c
c
c     initialize generators, weights and rulpts
c
      do 30 j = 1,wtleng
          do 10 i = 1,ndim
              g(i,j) = 0
10        continue
          do 20 i = 1,5
              w(i,j) = 0
20        continue
          rulpts(j) = 2*ndim
30    continue
      twondm = 2**ndim
      rulpts(wtleng) = twondm
      if (ndim.gt.2) rulpts(8) = (4*ndim* (ndim-1)* (ndim-2))/3
      rulpts(7) = 4*ndim* (ndim-1)
      rulpts(6) = 2*ndim* (ndim-1)
      rulpts(1) = 1
c
c     compute squared generator parameters
c
      lam0 = 0.4707
      lam1 = 4/ (15-5/lam0)
      ratio = (1-lam1/lam0)/27
      lam2 = (5-7*lam1-35*ratio)/ (7-35*lam1/3-35*ratio/lam0)
      ratio = ratio* (1-lam2/lam0)/3
      lam3 = (7-9* (lam2+lam1)+63*lam2*lam1/5-63*ratio)/
     +       (9-63* (lam2+lam1)/5+21*lam2*lam1-63*ratio/lam0)
      lamp = 0.0625
c
c     compute degree 9 rule weights
c
      w(1,wtleng) = 1/ (3*lam0)**4/twondm
      if (ndim.gt.2) w(1,8) = (1-1/ (3*lam0))/ (6*lam1)**3
      w(1,7) = (1-7* (lam0+lam1)/5+7*lam0*lam1/3)/
     +         (84*lam1*lam2* (lam2-lam0)* (lam2-lam1))
      w(1,6) = (1-7* (lam0+lam2)/5+7*lam0*lam2/3)/
     +         (84*lam1*lam1* (lam1-lam0)* (lam1-lam2)) -
     +         w(1,7)*lam2/lam1 - 2* (ndim-2)*w(1,8)
      w(1,4) = (1-9* ((lam0+lam1+lam2)/7- (lam0*lam1+lam0*lam2+
     +         lam1*lam2)/5)-3*lam0*lam1*lam2)/
     +         (18*lam3* (lam3-lam0)* (lam3-lam1)* (lam3-lam2))
      w(1,3) = (1-9* ((lam0+lam1+lam3)/7- (lam0*lam1+lam0*lam3+
     +         lam1*lam3)/5)-3*lam0*lam1*lam3)/
     +         (18*lam2* (lam2-lam0)* (lam2-lam1)* (lam2-lam3)) -
     +         2* (ndim-1)*w(1,7)
      w(1,2) = (1-9* ((lam0+lam2+lam3)/7- (lam0*lam2+lam0*lam3+
     +         lam2*lam3)/5)-3*lam0*lam2*lam3)/
     +         (18*lam1* (lam1-lam0)* (lam1-lam2)* (lam1-lam3)) -
     +         2* (ndim-1)* (w(1,7)+w(1,6)+ (ndim-2)*w(1,8))
c
c     compute weights for 2 degree 7, 1 degree 5 and 1 degree 3 rules
c
      w(2,wtleng) = 1/ (108*lam0**4)/twondm
      if (ndim.gt.2) w(2,8) = (1-27*twondm*w(2,9)*lam0**3)/ (6*lam1)**3
      w(2,7) = (1-5*lam1/3-15*twondm*w(2,wtleng)*lam0**2* (lam0-lam1))/
     +          (60*lam1*lam2* (lam2-lam1))
      w(2,6) = (1-9* (8*lam1*lam2*w(2,7)+twondm*w(2,wtleng)*lam0**2))/
     +         (36*lam1*lam1) - 2*w(2,8)* (ndim-2)
      w(2,4) = (1-7* ((lam1+lam2)/5-lam1*lam2/3+twondm*w(2,
     +         wtleng)*lam0* (lam0-lam1)* (lam0-lam2)))/
     +         (14*lam3* (lam3-lam1)* (lam3-lam2))
      w(2,3) = (1-7* ((lam1+lam3)/5-lam1*lam3/3+twondm*w(2,
     +         wtleng)*lam0* (lam0-lam1)* (lam0-lam3)))/
     +         (14*lam2* (lam2-lam1)* (lam2-lam3)) - 2* (ndim-1)*w(2,7)
      w(2,2) = (1-7* ((lam2+lam3)/5-lam2*lam3/3+twondm*w(2,
     +         wtleng)*lam0* (lam0-lam2)* (lam0-lam3)))/
     +         (14*lam1* (lam1-lam2)* (lam1-lam3)) -
     +         2* (ndim-1)* (w(2,7)+w(2,6)+ (ndim-2)*w(2,8))
      w(3,wtleng) = 5/ (324*lam0**4)/twondm
      if (ndim.gt.2) w(3,8) = (1-27*twondm*w(3,9)*lam0**3)/ (6*lam1)**3
      w(3,7) = (1-5*lam1/3-15*twondm*w(3,wtleng)*lam0**2* (lam0-lam1))/
     +          (60*lam1*lam2* (lam2-lam1))
      w(3,6) = (1-9* (8*lam1*lam2*w(3,7)+twondm*w(3,wtleng)*lam0**2))/
     +         (36*lam1*lam1) - 2*w(3,8)* (ndim-2)
      w(3,5) = (1-7* ((lam1+lam2)/5-lam1*lam2/3+twondm*w(3,
     +         wtleng)*lam0* (lam0-lam1)* (lam0-lam2)))/
     +         (14*lamp* (lamp-lam1)* (lamp-lam2))
      w(3,3) = (1-7* ((lam1+lamp)/5-lam1*lamp/3+twondm*w(3,
     +         wtleng)*lam0* (lam0-lam1)* (lam0-lamp)))/
     +         (14*lam2* (lam2-lam1)* (lam2-lamp)) - 2* (ndim-1)*w(3,7)
      w(3,2) = (1-7* ((lam2+lamp)/5-lam2*lamp/3+twondm*w(3,
     +         wtleng)*lam0* (lam0-lam2)* (lam0-lamp)))/
     +         (14*lam1* (lam1-lam2)* (lam1-lamp)) -
     +         2* (ndim-1)* (w(3,7)+w(3,6)+ (ndim-2)*w(3,8))
      w(4,wtleng) = 2/ (81*lam0**4)/twondm
      if (ndim.gt.2) w(4,8) = (2-27*twondm*w(4,9)*lam0**3)/ (6*lam1)**3
      w(4,7) = (2-15*lam1/9-15*twondm*w(4,wtleng)*lam0* (lam0-lam1))/
     +         (60*lam1*lam2* (lam2-lam1))
      w(4,6) = (1-9* (8*lam1*lam2*w(4,7)+twondm*w(4,wtleng)*lam0**2))/
     +         (36*lam1*lam1) - 2*w(4,8)* (ndim-2)
      w(4,4) = (2-7* ((lam1+lam2)/5-lam1*lam2/3+twondm*w(4,
     +         wtleng)*lam0* (lam0-lam1)* (lam0-lam2)))/
     +         (14*lam3* (lam3-lam1)* (lam3-lam2))
      w(4,3) = (2-7* ((lam1+lam3)/5-lam1*lam3/3+twondm*w(4,
     +         wtleng)*lam0* (lam0-lam1)* (lam0-lam3)))/
     +         (14*lam2* (lam2-lam1)* (lam2-lam3)) - 2* (ndim-1)*w(4,7)
      w(4,2) = (2-7* ((lam2+lam3)/5-lam2*lam3/3+twondm*w(4,
     +         wtleng)*lam0* (lam0-lam2)* (lam0-lam3)))/
     +         (14*lam1* (lam1-lam2)* (lam1-lam3)) -
     +         2* (ndim-1)* (w(4,7)+w(4,6)+ (ndim-2)*w(4,8))
      w(5,2) = 1/ (6*lam1)
c
c     set generator values
c
      lam0 = sqrt(lam0)
      lam1 = sqrt(lam1)
      lam2 = sqrt(lam2)
      lam3 = sqrt(lam3)
      lamp = sqrt(lamp)
      do 40 i = 1,ndim
          g(i,wtleng) = lam0
40    continue
      if (ndim.gt.2) then
          g(1,8) = lam1
          g(2,8) = lam1
          g(3,8) = lam1
      end if
      g(1,7) = lam1
      g(2,7) = lam2
      g(1,6) = lam1
      g(2,6) = lam1
      g(1,5) = lamp
      g(1,4) = lam3
      g(1,3) = lam2
      g(1,2) = lam1
c
c     compute final weight values.
c     the null rule weights are computed from differences between
c     the degree 9 rule weights and lower degree rule weights.
c
      w(1,1) = twondm
      do 70 j = 2,5
          do 50 i = 2,wtleng
              w(j,i) = w(j,i) - w(1,i)
              w(j,1) = w(j,1) - rulpts(i)*w(j,i)
50        continue
70    continue
      do 80 i = 2,wtleng
          w(1,i) = twondm*w(1,i)
          w(1,1) = w(1,1) - rulpts(i)*w(1,i)
80    continue
c
c     set error coefficients
c
      errcof(1) = 5
      errcof(2) = 5
      errcof(3) = 1.
      errcof(4) = 5
      errcof(5) = 0.5
      errcof(6) = 0.25
c
c***end d09hre
c
      end
      subroutine d113re(wtleng,w,g,errcof,rulpts)
c***begin prologue d113re
c***author   jarle berntsen, edb-senteret,
c            university of bergen, thormohlens gt. 55,
c            n-5008 bergen, norway
c***purpose d113re computes abscissas and weights of a 3 dimensional
c            integration rule of degree 11.
c            two null rules of degree 9, one null rule of degree 7
c            and one null rule of degree 5 to be used in error
c            estimation are also computed.
c***description d113re will select the correct values of the abscissas
c            and corresponding weights for different
c            integration rules and null rules and assign them to g
c            and w.
c            the heuristic error coefficients errcof
c            will also be computed.
c
c
c   on entry
c
c     wtleng integer.
c            the number of weights in each of the rules.
c
c   on return
c
c     w      real array of dimension (5,wtleng).
c            the weights for the basic and null rules.
c            w(1,1),...,w(1,wtleng) are weights for the basic rule.
c            w(i,1),...,w(i,wtleng), for i > 1 are null rule weights.
c     g      real array of dimension (ndim,wtleng).
c            the fully symmetric sum generators for the rules.
c            g(1,j),...,g(ndim,j) are the generators for the points
c            associated with the the jth weights.
c     errcof real array of dimension 6.
c            heuristic error coefficients that are used in the
c            error estimation in basrul.
c     rulpts real array of dimension wtleng.
c            the number of points used by each generator.
c
c***references  j.berntsen, cautious adaptive numerical integration
c               over the 3-cube, reports in informatics 17, dept. of
c               inf.,univ. of bergen, norway, 1985.
c               j.berntsen and t.o.espelid, on the construction of
c               higher degree three-dimensional embedded integration
c               rules, siam j. numer. anal.,vol. 25,no. 1, pp.222-234,
c               1988.
c***routines called-none
c***end prologue d113re
c
c   global variables.
c
      integer wtleng
      real *8 w(5,wtleng),g(3,wtleng),errcof(6)
      real *8 rulpts(wtleng)
c
c   local variables.
c
      integer i,j
      real *8 dim3g(14)
      real *8 dim3w(13,5)
c
      data (dim3g(i),i=1,14)/0.1900000000000000d+00,
     +     0.5000000000000000d+00,0.7500000000000000d+00,
     +     0.8000000000000000d+00,0.9949999999999999d+00,
     +     0.9987344998351400d+00,0.7793703685672423d+00,
     +     0.9999698993088767d+00,0.7902637224771788d+00,
     +     0.4403396687650737d+00,0.4378478459006862d+00,
     +     0.9549373822794593d+00,0.9661093133630748d+00,
     +     0.4577105877763134d+00/
c
      data (dim3w(i,1),i=1,13)/0.7923078151105734d-02,
     +     0.6797177392788080d-01,0.1086986538805825d-02,
     +     0.1838633662212829d+00,0.3362119777829031d-01,
     +     0.1013751123334062d-01,0.1687648683985235d-02,
     +     0.1346468564512807d+00,0.1750145884600386d-02,
     +     0.7752336383837454d-01,0.2461864902770251d+00,
     +     0.6797944868483039d-01,0.1419962823300713d-01/
c
      data (dim3w(i,2),i=1,13)/0.1715006248224684d+01,
     +     - .3755893815889209d+00,0.1488632145140549d+00,
     +     - .2497046640620823d+00,0.1792501419135204d+00,
     +     0.3446126758973890d-02, - .5140483185555825d-02,
     +     0.6536017839876425d-02, - .6513454939229700d-03,
     +     - .6304672433547204d-02,0.1266959399788263d-01,
     +     - .5454241018647931d-02,0.4826995274768427d-02/
c
      data (dim3w(i,3),i=1,13)/0.1936014978949526d+01,
     +     - .3673449403754268d+00,0.2929778657898176d-01,
     +     - .1151883520260315d+00,0.5086658220872218d-01,
     +     0.4453911087786469d-01, - .2287828257125900d-01,
     +     0.2908926216345833d-01, - .2898884350669207d-02,
     +     - .2805963413307495d-01,0.5638741361145884d-01,
     +     - .2427469611942451d-01,0.2148307034182882d-01/
c
      data (dim3w(i,4),i=1,13)/0.5170828195605760d+00,
     +     0.1445269144914044d-01, - .3601489663995932d+00,
     +     0.3628307003418485d+00,0.7148802650872729d-02,
     +     - .9222852896022966d-01,0.1719339732471725d-01,
     +     - .1021416537460350d+00, - .7504397861080493d-02,
     +     0.1648362537726711d-01,0.5234610158469334d-01,
     +     0.1445432331613066d-01,0.3019236275367777d-02/
c
      data (dim3w(i,5),i=1,13)/0.2054404503818520d+01,
     +     0.1377759988490120d-01, - .5768062917904410d+00,
     +     0.3726835047700328d-01,0.6814878939777219d-02,
     +     0.5723169733851849d-01, - .4493018743811285d-01,
     +     0.2729236573866348d-01,0.3547473950556990d-03,
     +     0.1571366799739551d-01,0.4990099219278567d-01,
     +     0.1377915552666770d-01,0.2878206423099872d-02/
c
c***first executable statement d113re
c
c   assign values to w.
c
      do 10 i = 1,13
          do 10 j = 1,5
              w(j,i) = dim3w(i,j)
10    continue
c
c   assign values to g.
c
      do 20 i = 1,3
          do 20 j = 1,13
              g(i,j) = 0
20    continue
      g(1,2) = dim3g(1)
      g(1,3) = dim3g(2)
      g(1,4) = dim3g(3)
      g(1,5) = dim3g(4)
      g(1,6) = dim3g(5)
      g(1,7) = dim3g(6)
      g(2,7) = g(1,7)
      g(1,8) = dim3g(7)
      g(2,8) = g(1,8)
      g(1,9) = dim3g(8)
      g(2,9) = g(1,9)
      g(3,9) = g(1,9)
      g(1,10) = dim3g(9)
      g(2,10) = g(1,10)
      g(3,10) = g(1,10)
      g(1,11) = dim3g(10)
      g(2,11) = g(1,11)
      g(3,11) = g(1,11)
      g(1,12) = dim3g(12)
      g(2,12) = dim3g(11)
      g(3,12) = g(2,12)
      g(1,13) = dim3g(13)
      g(2,13) = g(1,13)
      g(3,13) = dim3g(14)
c
c   assign values to rulpts.
c
      rulpts(1) = 1
      rulpts(2) = 6
      rulpts(3) = 6
      rulpts(4) = 6
      rulpts(5) = 6
      rulpts(6) = 6
      rulpts(7) = 12
      rulpts(8) = 12
      rulpts(9) = 8
      rulpts(10) = 8
      rulpts(11) = 8
      rulpts(12) = 24
      rulpts(13) = 24
c
c   assign values to errcof.
c
      errcof(1) = 4
      errcof(2) = 4.
      errcof(3) = 0.5
      errcof(4) = 3.
      errcof(5) = 0.5
      errcof(6) = 0.25
c
c***end d113re
c
      return
      end
      subroutine d132re(wtleng,w,g,errcof,rulpts)
c***begin prologue d132re
c***author   jarle berntsen, edb-senteret,
c            university of bergen, thormohlens gt. 55,
c            n-5008 bergen, norway
c***purpose d132re computes abscissas and weights of a 2 dimensional
c            integration rule of degree 13.
c            two null rules of degree 11, one null rule of degree 9
c            and one null rule of degree 7 to be used in error
c            estimation are also computed.
c ***description d132re will select the correct values of the abscissas
c            and corresponding weights for different
c            integration rules and null rules and assign them to
c            g and w. the heuristic error coefficients errcof
c            will also be assigned.
c
c
c   on entry
c
c     wtleng integer.
c            the number of weights in each of the rules.
c
c   on return
c
c     w      real array of dimension (5,wtleng).
c            the weights for the basic and null rules.
c            w(1,1),...,w(1,wtleng) are weights for the basic rule.
c            w(i,1),...,w(i,wtleng), for i > 1 are null rule weights.
c     g      real array of dimension (ndim,wtleng).
c            the fully symmetric sum generators for the rules.
c            g(1,j),...,g(ndim,j) are the generators for the points
c            associated with the the jth weights.
c     errcof real array of dimension 6.
c            heuristic error coefficients that are used in the
c            error estimation in basrul.
c     rulpts real array of dimension wtleng.
c            the number of points produced by each generator.
c***references s.eriksen,
c              thesis of the degree cand.scient, dept. of informatics,
c              univ. of bergen,norway, 1984.
c
c***routines called-none
c***end prologue d132re
c
c   global variables
c
      integer wtleng
      real *8 w(5,wtleng),g(2,wtleng),errcof(6)
      real *8 rulpts(wtleng)
c
c   local variables.
c
      integer i,j
      real *8 dim2g(16)
      real *8 dim2w(14,5)
c
      data (dim2g(i),i=1,16)/0.2517129343453109d+00,
     +     0.7013933644534266d+00,0.9590960631619962d+00,
     +     0.9956010478552127d+00,0.5000000000000000d+00,
     +     0.1594544658297559d+00,0.3808991135940188d+00,
     +     0.6582769255267192d+00,0.8761473165029315d+00,
     +     0.9982431840531980d+00,0.9790222658168462d+00,
     +     0.6492284325645389d+00,0.8727421201131239d+00,
     +     0.3582614645881228d+00,0.5666666666666666d+00,
     +     0.2077777777777778d+00/
c
      data (dim2w(i,1),i=1,14)/0.3379692360134460d-01,
     +     0.9508589607597761d-01,0.1176006468056962d+00,
     +     0.2657774586326950d-01,0.1701441770200640d-01,
     +     0.0000000000000000d+00,0.1626593098637410d-01,
     +     0.1344892658526199d+00,0.1328032165460149d+00,
     +     0.5637474769991870d-01,0.3908279081310500d-02,
     +     0.3012798777432150d-01,0.1030873234689166d+00,
     +     0.6250000000000000d-01/
c
      data (dim2w(i,2),i=1,14)/0.3213775489050763d+00,
     +     - .1767341636743844d+00,0.7347600537466072d-01,
     +     - .3638022004364754d-01,0.2125297922098712d-01,
     +     0.1460984204026913d+00,0.1747613286152099d-01,
     +     0.1444954045641582d+00,0.1307687976001325d-03,
     +     0.5380992313941161d-03,0.1042259576889814d-03,
     +     - .1401152865045733d-02,0.8041788181514763d-02,
     +     - .1420416552759383d+00/
c
      data (dim2w(i,3),i=1,14)/0.3372900883288987d+00,
     +     - .1644903060344491d+00,0.7707849911634622d-01,
     +     - .3804478358506310d-01,0.2223559940380806d-01,
     +     0.1480693879765931d+00,0.4467143702185814d-05,
     +     0.1508944767074130d+00,0.3647200107516215d-04,
     +     0.5777198999013880d-03,0.1041757313688177d-03,
     +     - .1452822267047819d-02,0.8338339968783705d-02,
     +     - .1472796329231960d+00/
c
      data (dim2w(i,4),i=1,14)/ - .8264123822525677d+00,
     +     0.3065838614094360d+00,0.2389292538329435d-02,
     +     - .1343024157997222d+00,0.8833366840533900d-01,
     +     0.0000000000000000d+00,0.9786283074168292d-03,
     +     - .1319227889147519d+00,0.7990012200150630d-02,
     +     0.3391747079760626d-02,0.2294915718283264d-02,
     +     - .1358584986119197d-01,0.4025866859057809d-01,
     +     0.3760268580063992d-02/
c
      data (dim2w(i,5),i=1,14)/0.6539094339575232d+00,
     +     - .2041614154424632d+00, - .1746981515794990d+00,
     +     0.3937939671417803d-01,0.6974520545933992d-02,
     +     0.0000000000000000d+00,0.6667702171778258d-02,
     +     0.5512960621544304d-01,0.5443846381278607d-01,
     +     0.2310903863953934d-01,0.1506937747477189d-01,
     +     - .6057021648901890d-01,0.4225737654686337d-01,
     +     0.2561989142123099d-01/
c
c***first executable statement d132re
c
c   assign values to w.
c
      do 10 i = 1,14
          do 10 j = 1,5
              w(j,i) = dim2w(i,j)
10    continue
c
c   assign values to g.
c
      do 20 i = 1,2
          do 20 j = 1,14
              g(i,j) = 0
20    continue
      g(1,2) = dim2g(1)
      g(1,3) = dim2g(2)
      g(1,4) = dim2g(3)
      g(1,5) = dim2g(4)
      g(1,6) = dim2g(5)
      g(1,7) = dim2g(6)
      g(2,7) = g(1,7)
      g(1,8) = dim2g(7)
      g(2,8) = g(1,8)
      g(1,9) = dim2g(8)
      g(2,9) = g(1,9)
      g(1,10) = dim2g(9)
      g(2,10) = g(1,10)
      g(1,11) = dim2g(10)
      g(2,11) = g(1,11)
      g(1,12) = dim2g(11)
      g(2,12) = dim2g(12)
      g(1,13) = dim2g(13)
      g(2,13) = dim2g(14)
      g(1,14) = dim2g(15)
      g(2,14) = dim2g(16)
c
c   assign values to rulpts.
c
      rulpts(1) = 1
      do 30 i = 2,11
          rulpts(i) = 4
30    continue
      rulpts(12) = 8
      rulpts(13) = 8
      rulpts(14) = 8
c
c   assign values to errcof.
c
      errcof(1) = 10
      errcof(2) = 10
      errcof(3) = 1.
      errcof(4) = 5.
      errcof(5) = 0.5
      errcof(6) = 0.25
c
c***end d132re
c
      return
      end
      subroutine dadhre(ndim,numfun,mdiv,a,b,minsub,maxsub,funsub,
     +                  epsabs,epsrel,key,restar,num,lenw,wtleng,
     +                  result,abserr,neval,nsub,ifail,values,
     +                  errors,centrs,hwidts,greate,dir,oldres,work,g,w,
     +                  rulpts,center,hwidth,x,scales,norms)
c***begin prologue dadhre
c***keywords automatic multidimensional integrator,
c            n-dimensional hyper-rectangles,
c            general purpose, global adaptive
c***purpose  the routine calculates an approximation to a given
c            vector of definite integrals, i, over a hyper-rectangular
c            region hopefully satisfying for each component of i the
c            following claim for accuracy:
c            abs(i(k)-result(k)).le.max(epsabs,epsrel*abs(i(k)))
c***description computation of integrals over hyper-rectangular
c            regions.
c            dadhre repeatedly subdivides the region
c            of integration and estimates the integrals and the
c            errors over the subregions with  greatest
c            estimated errors until the error request
c            is met or maxsub subregions are stored.
c            the regions are divided in two equally sized parts along
c            the direction with greatest absolute fourth divided
c            difference.
c
c   on entry
c
c     ndim   integer.
c            number of variables. 1 < ndim <= maxdim.
c     numfun integer.
c            number of components of the integral.
c     mdiv   integer.
c            defines the number of new subregions that are divided
c            in each subdivision step.
c     a      real array of dimension ndim.
c            lower limits of integration.
c     b      real array of dimension ndim.
c            upper limits of integration.
c     minsub integer.
c            the computations proceed until there are at least
c            minsub subregions in the data structure.
c     maxsub integer.
c            the computations proceed until there are at most
c            maxsub subregions in the data structure.
c
c     funsub externally declared subroutine for computing
c            all components of the integrand in the given
c            evaluation point.
c            it must have parameters (ndim,x,numfun,funvls)
c            input parameters:
c              ndim   integer that defines the dimension of the
c                     integral.
c              x      real array of dimension ndim
c                     that defines the evaluation point.
c              numfun integer that defines the number of
c                     components of i.
c            output parameter:
c              funvls real array of dimension numfun
c                     that defines numfun components of the integrand.
c
c     epsabs real.
c            requested absolute error.
c     epsrel real.
c            requested relative error.
c     key    integer.
c            key to selected local integration rule.
c            key = 0 is the default value.
c                  for ndim = 2 the degree 13 rule is selected.
c                  for ndim = 3 the degree 11 rule is selected.
c                  for ndim > 3 the degree  9 rule is selected.
c            key = 1 gives the user the 2 dimensional degree 13
c                  integration rule that uses 65 evaluation points.
c            key = 2 gives the user the 3 dimensional degree 11
c                  integration rule that uses 127 evaluation points.
c            key = 3 gives the user the degree 9 integration rule.
c            key = 4 gives the user the degree 7 integration rule.
c                  this is the recommended rule for problems that
c                  require great adaptivity.
c     restar integer.
c            if restar = 0, this is the first attempt to compute
c            the integral.
c            if restar = 1, then we restart a previous attempt.
c            (in this case the output parameters from dadhre
c            must not be changed since the last
c            exit from dadhre.)
c     num    integer.
c            the number of function evaluations over each subregion.
c     lenw   integer.
c            defines the length of the working array work.
c            lenw should be greater or equal to
c            16*mdiv*numfun.
c     wtleng integer.
c            the number of weights in the basic integration rule.
c     nsub   integer.
c            if restar = 1, then nsub must specify the number
c            of subregions stored in the previous call to dadhre.
c
c   on return
c
c     result real array of dimension numfun.
c            approximations to all components of the integral.
c     abserr real array of dimension numfun.
c            estimates of absolute accuracies.
c     neval  integer.
c            number of function evaluations used by dadhre.
c     nsub   integer.
c            number of stored subregions.
c     ifail  integer.
c            ifail = 0 for normal exit, when abserr(k) <=  epsabs or
c              abserr(k) <=  abs(result(k))*epsrel with maxsub or less
c              subregions processed for all values of k,
c              1 <=  k <=  numfun.
c            ifail = 1 if maxsub was too small for dadhre
c              to obtain the required accuracy. in this case dadhre
c              returns values of result with estimated absolute
c              accuracies abserr.
c     values real array of dimension (numfun,maxsub).
c            used to store estimated values of the integrals
c            over the subregions.
c     errors real array of dimension (numfun,maxsub).
c            used to store the corresponding estimated errors.
c     centrs real array of dimension (ndim,maxsub).
c            used to store the centers of the stored subregions.
c     hwidts real array of dimension (ndim,maxsub).
c            used to store the half widths of the stored subregions.
c     greate real array of dimension maxsub.
c            used to store the greatest estimated errors in
c            all subregions.
c     dir    real array of dimension maxsub.
c            dir is used to store the directions for
c            further subdivision.
c     oldres real array of dimension (numfun,mdiv).
c            used to store old estimates of the integrals over the
c            subregions.
c     work   real array of dimension lenw.
c            used  in drlhre and dtrhre.
c     g      real array of dimension (ndim,wtleng,2*mdiv).
c            the fully symmetric sum generators for the rules.
c            g(1,j,1),...,g(ndim,j,1) are the generators for the
c            points associated with the jth weights.
c            when mdiv subregions are divided in 2*mdiv
c            subregions, the subregions may be processed on different
c            processors and we must make a copy of the generators
c            for each processor.
c     w      real array of dimension (5,wtleng).
c            the weights for the basic and null rules.
c            w(1,1), ..., w(1,wtleng) are weights for the basic rule.
c            w(i,1), ..., w(i,wtleng) , for i > 1 are null rule weights.
c     rulpts real array of dimension wtleng.
c            work array used in dinhre.
c     center real array of dimension ndim.
c            work array used in dtrhre.
c     hwidth real array of dimension ndim.
c            work array used in dtrhre.
c     x      real array of dimension (ndim,2*mdiv).
c            work array used in drlhre.
c     scales real array of dimension (3,wtleng).
c            work array used by dinhre and drlhre.
c     norms  real array of dimension (3,wtleng).
c            work array used by dinhre and drlhre.
c
c***references
c
c   p. van dooren and l. de ridder, algorithm 6, an adaptive algorithm
c   for numerical integration over an n-dimensional cube, j.comput.appl.
c   math. 2(1976)207-217.
c
c   a.c.genz and a.a.malik, algorithm 019. remarks on algorithm 006:
c   an adaptive algorithm for numerical integration over an
c   n-dimensional rectangular region,j.comput.appl.math. 6(1980)295-302.
c
c***routines called dtrhre,dinhre,drlhre
c***end prologue dadhre
c
c   global variables.
c
      external funsub
      integer ndim,numfun,mdiv,minsub,maxsub,key,lenw,restar
      integer num,neval,nsub,ifail,wtleng
      real *8 a(ndim),b(ndim),epsabs,epsrel
      real *8 result(numfun),abserr(numfun)
      real *8 values(numfun,maxsub),errors(numfun,maxsub)
      real *8 centrs(ndim,maxsub)
      real *8 hwidts(ndim,maxsub)
      real *8 greate(maxsub),dir(maxsub)
      real *8 oldres(numfun,mdiv)
      real *8 work(lenw),rulpts(wtleng)
      real *8 g(ndim,wtleng,2*mdiv),w(5,wtleng)
      real *8 center(ndim),hwidth(ndim),x(ndim,2*mdiv)
      real *8 scales(3,wtleng),norms(3,wtleng)
c
c   local variables.
c
c   intsgn is used to get correct sign on the integral.
c   sbrgns is the number of stored subregions.
c   ndiv   the number of subregions to be divided in each main step.
c   pointr pointer to the position in the data structure where
c          the new subregions are to be stored.
c   direct direction of subdivision.
c   errcof heuristic error coeff. defined in dinhre and used by drlhre
c          and dadhre.
c
      integer i,j,k
      integer intsgn,sbrgns
      integer l1
      integer ndiv,pointr,direct,index
      real *8 oldcen,est1,est2,errcof(6)
c
c***first executable statement dadhre
c
c   get the correct sign on the integral.
c
      intsgn = 1
      do 10 j = 1,ndim
          if (b(j).lt.a(j)) then
              intsgn = - intsgn
          end if
10    continue
c
c   call dinhre to compute the weights and abscissas of
c   the function evaluation points.
c
      call dinhre(ndim,key,wtleng,w,g,errcof,rulpts,scales,norms)
c
c   if restar = 1, then this is a restart run.
c
      if (restar.eq.1) then
          sbrgns = nsub
          go to 110
      end if
c
c   initialize the sbrgns, centrs and hwidts.
c
      sbrgns = 1
      do 15 j = 1,ndim
          centrs(j,1) = (a(j)+b(j))/2
          hwidts(j,1) = abs(b(j)-a(j))/2
15    continue
c
c   initialize result, abserr and neval.
c
      do 20 j = 1,numfun
          result(j) = 0
          abserr(j) = 0
20    continue
      neval = 0
c
c   apply drlhre over the whole region.
c
      call drlhre(ndim,centrs(1,1),hwidts(1,1),wtleng,g,w,errcof,numfun,
     +            funsub,scales,norms,x,work,values(1,1),errors(1,1),
     +            dir(1))
      neval = neval + num
c
c   add the computed values to result and abserr.
c
      do 55 j = 1,numfun
          result(j) = result(j) + values(j,1)
55    continue
      do 65 j = 1,numfun
          abserr(j) = abserr(j) + errors(j,1)
65    continue
c
c   store results in heap.
c
      index = 1
      call dtrhre(2,ndim,numfun,index,values,errors,centrs,hwidts,
     +            greate,work(1),work(numfun+1),center,hwidth,dir)
c
c***end initialisation.
c
c***begin loop while the error is too great,
c   and sbrgns+1 is less than maxsub.
c
110   if (sbrgns+1.le.maxsub) then
c
c   if we are allowed to divide further,
c   prepare to apply basic rule over each half of the
c   ndiv subregions with greatest errors.
c   if maxsub is great enough, ndiv = mdiv
c
          if (mdiv.gt.1) then
              ndiv = maxsub - sbrgns
              ndiv = min(ndiv,mdiv,sbrgns)
          else
              ndiv = 1
          end if
c
c   divide the ndiv subregions in two halves, and compute
c   integral and error over each half.
c
          do 150 i = 1,ndiv
              pointr = sbrgns + ndiv + 1 - i
c
c   adjust result and abserr.
c
              do 115 j = 1,numfun
                  result(j) = result(j) - values(j,1)
                  abserr(j) = abserr(j) - errors(j,1)
115           continue
c
c   compute first half region.
c
              do 120 j = 1,ndim
                  centrs(j,pointr) = centrs(j,1)
                  hwidts(j,pointr) = hwidts(j,1)
120           continue
              direct = dir(1)
              dir(pointr) = direct
              hwidts(direct,pointr) = hwidts(direct,1)/2
              oldcen = centrs(direct,1)
              centrs(direct,pointr) = oldcen - hwidts(direct,pointr)
c
c   save the computed values of the integrals.
c
              do 125 j = 1,numfun
                  oldres(j,ndiv-i+1) = values(j,1)
125           continue
c
c   adjust the heap.
c
              call dtrhre(1,ndim,numfun,sbrgns,values,errors,centrs,
     +                    hwidts,greate,work(1),work(numfun+1),center,
     +                    hwidth,dir)
c
c   compute second half region.
c
              do 130 j = 1,ndim
                  centrs(j,pointr-1) = centrs(j,pointr)
                  hwidts(j,pointr-1) = hwidts(j,pointr)
130           continue
              centrs(direct,pointr-1) = oldcen + hwidts(direct,pointr)
              hwidts(direct,pointr-1) = hwidts(direct,pointr)
              dir(pointr-1) = direct
150       continue
c
c   make copies of the generators for each processor.
c
          do 190 i = 2,2*ndiv
              do 190 j = 1,ndim
                  do 190 k = 1,wtleng
                      g(j,k,i) = g(j,k,1)
190       continue
c
c   apply basic rule.
c
cvd$l cncall
          do 200 i = 1,2*ndiv
              index = sbrgns + i
              l1 = 1 + (i-1)*8*numfun
              call drlhre(ndim,centrs(1,index),hwidts(1,index),wtleng,
     +                    g(1,1,i),w,errcof,numfun,funsub,scales,norms,
     +                    x(1,i),work(l1),values(1,index),
     +                    errors(1,index),dir(index))
200       continue
          neval = neval + 2*ndiv*num
c
c   add new contributions to result.
c
          do 220 i = 1,2*ndiv
              do 210 j = 1,numfun
                  result(j) = result(j) + values(j,sbrgns+i)
210           continue
220       continue
c
c   check consistency of results and if necessary adjust
c   the estimated errors.
c
          do 240 i = 1,ndiv
              greate(sbrgns+2*i-1) = 0
              greate(sbrgns+2*i) = 0
              do 230 j = 1,numfun
                  est1 = abs(oldres(j,i)- (values(j,
     +                   sbrgns+2*i-1)+values(j,sbrgns+2*i)))
                  est2 = errors(j,sbrgns+2*i-1) + errors(j,sbrgns+2*i)
                  if (est2.gt.0) then
                      errors(j,sbrgns+2*i-1) = errors(j,sbrgns+2*i-1)*
     +                  (1+errcof(5)*est1/est2)
                      errors(j,sbrgns+2*i) = errors(j,sbrgns+2*i)*
     +                                       (1+errcof(5)*est1/est2)
                  end if
                  errors(j,sbrgns+2*i-1) = errors(j,sbrgns+2*i-1) +
     +                                     errcof(6)*est1
                  errors(j,sbrgns+2*i) = errors(j,sbrgns+2*i) +
     +                                   errcof(6)*est1
                  if (errors(j,sbrgns+2*i-1).gt.
     +                greate(sbrgns+2*i-1)) then
                      greate(sbrgns+2*i-1) = errors(j,sbrgns+2*i-1)
                  end if
                  if (errors(j,sbrgns+2*i).gt.greate(sbrgns+2*i)) then
                      greate(sbrgns+2*i) = errors(j,sbrgns+2*i)
                  end if
                  abserr(j) = abserr(j) + errors(j,sbrgns+2*i-1) +
     +                        errors(j,sbrgns+2*i)
230           continue
240       continue
c
c   store results in heap.
c
          do 250 i = 1,2*ndiv
              index = sbrgns + i
              call dtrhre(2,ndim,numfun,index,values,errors,centrs,
     +                    hwidts,greate,work(1),work(numfun+1),center,
     +                    hwidth,dir)
250       continue
          sbrgns = sbrgns + 2*ndiv
c
c   check for termination.
c
          if (sbrgns.lt.minsub) then
              go to 110
          end if
          do 255 j = 1,numfun
              if (abserr(j).gt.epsrel*abs(result(j)) .and.
     +            abserr(j).gt.epsabs) then
                  go to 110
              end if
255       continue
          ifail = 0
          go to 499
c
c   else we did not succeed with the
c   given value of maxsub.
c
      else
          ifail = 1
      end if
c
c   compute more accurate values of result and abserr.
c
499   continue
      do 500 j = 1,numfun
          result(j) = 0
          abserr(j) = 0
500   continue
      do 510 i = 1,sbrgns
          do 505 j = 1,numfun
              result(j) = result(j) + values(j,i)
              abserr(j) = abserr(j) + errors(j,i)
505       continue
510   continue
c
c   compute correct sign on the integral.
c
      do 600 j = 1,numfun
          result(j) = result(j)*intsgn
600   continue
      nsub = sbrgns
      return
c
c***end dadhre
c
      end
      subroutine dchhre(maxdim,ndim,numfun,mdiv,a,b,minpts,maxpts,
     +                  epsabs,epsrel,key,nw,restar,num,maxsub,minsub,
     +                  keyf,ifail,wtleng)
c***begin prologue dchhre
c***purpose  dchhre checks the validity of the
c            input parameters to dcuhre.
c***description
c            dchhre computes num, maxsub, minsub, keyf, wtleng and
c            ifail as functions of the input parameters to dcuhre,
c            and checks the validity of the input parameters to dcuhre.
c
c   on entry
c
c     maxdim integer.
c            the maximum allowed number of dimensions.
c     ndim   integer.
c            number of variables. 1 < ndim <= maxdim.
c     numfun integer.
c            number of components of the integral.
c     mdiv   integer.
c            mdiv is the number of subregions that are divided in
c            each subdivision step in dadhre.
c            mdiv is chosen default to 1.
c            for efficient execution on parallel computers
c            with nproc processors mdiv should be set equal to
c            the smallest integer such that mod(2*mdiv,nproc) = 0.
c     a      real array of dimension ndim.
c            lower limits of integration.
c     b      real array of dimension ndim.
c            upper limits of integration.
c     minpts integer.
c            minimum number of function evaluations.
c     maxpts integer.
c            maximum number of function evaluations.
c            the number of function evaluations over each subregion
c            is num.
c            if (key = 0 or key = 1) and (ndim = 2) then
c              num = 65
c            elseif (key = 0 or key = 2) and (ndim = 3) then
c              num = 127
c            elseif (key = 0 and ndim > 3) or (key = 3) then
c              num = 1 + 4*2*ndim + 2*ndim*(ndim-1) + 4*ndim*(ndim-1) +
c                    4*ndim*(ndim-1)*(ndim-2)/3 + 2**ndim
c            elseif (key = 4) then
c              num = 1 + 3*2*ndim + 2*ndim*(ndim-1) + 2**ndim
c            maxpts >= 3*num and maxpts >= minpts
c     epsabs real.
c            requested absolute error.
c     epsrel real.
c            requested relative error.
c     key    integer.
c            key to selected local integration rule.
c            key = 0 is the default value.
c                  for ndim = 2 the degree 13 rule is selected.
c                  for ndim = 3 the degree 11 rule is selected.
c                  for ndim > 3 the degree  9 rule is selected.
c            key = 1 gives the user the 2 dimensional degree 13
c                  integration rule that uses 65 evaluation points.
c            key = 2 gives the user the 3 dimensional degree 11
c                  integration rule that uses 127 evaluation points.
c            key = 3 gives the user the degree 9 integration rule.
c            key = 4 gives the user the degree 7 integration rule.
c                  this is the recommended rule for problems that
c                  require great adaptivity.
c     nw     integer.
c            defines the length of the working array work.
c            let maxsub denote the maximum allowed number of subregions
c            for the given values of maxpts, key and ndim.
c            maxsub = (maxpts-num)/(2*num) + 1
c            nw should be greater or equal to
c            maxsub*(2*ndim+2*numfun+2) + 17*numfun + 1
c            for efficient execution on parallel computers
c            nw should be chosen greater or equal to
c            maxsub*(2*ndim+2*numfun+2) + 17*numfun*mdiv + 1
c            where mdiv is the number of subregions that are divided in
c            each subdivision step.
c            mdiv is default set internally in dcuhre equal to 1.
c            for efficient execution on parallel computers
c            with nproc processors mdiv should be set equal to
c            the smallest integer such that mod(2*mdiv,nproc) = 0.
c     restar integer.
c            if restar = 0, this is the first attempt to compute
c            the integral.
c            if restar = 1, then we restart a previous attempt.
c
c   on return
c
c     num    integer.
c            the number of function evaluations over each subregion.
c     maxsub integer.
c            the maximum allowed number of subregions for the
c            given values of maxpts, key and ndim.
c     minsub integer.
c            the minimum allowed number of subregions for the given
c            values of minpts, key and ndim.
c     ifail  integer.
c            ifail = 0 for normal exit.
c            ifail = 2 if key is less than 0 or key greater than 4.
c            ifail = 3 if ndim is less than 2 or ndim greater than
c                      maxdim.
c            ifail = 4 if key = 1 and ndim not equal to 2.
c            ifail = 5 if key = 2 and ndim not equal to 3.
c            ifail = 6 if numfun less than 1.
c            ifail = 7 if volume of region of integration is zero.
c            ifail = 8 if maxpts is less than 3*num.
c            ifail = 9 if maxpts is less than minpts.
c            ifail = 10 if epsabs < 0 and epsrel < 0.
c            ifail = 11 if nw is too small.
c            ifail = 12 if unlegal restar.
c     keyf   integer.
c            key to selected integration rule.
c     wtleng integer.
c            the number of generators of the chosen integration rule.
c
c***routines called-none
c***end prologue dchhre
c
c   global variables.
c
      integer ndim,numfun,mdiv,minpts,maxpts,key,nw,minsub,maxsub
      integer restar,num,keyf,ifail,maxdim,wtleng
      real *8 a(ndim),b(ndim),epsabs,epsrel
c
c   local variables.
c
      integer limit,j
c
c***first executable statement dchhre
c
      ifail = 0
c
c   check on legal key.
c
      if (key.lt.0 .or. key.gt.4) then
          ifail = 2
          go to 999
      end if
c
c   check on legal ndim.
c
      if (ndim.lt.2 .or. ndim.gt.maxdim) then
          ifail = 3
          go to 999
      end if
c
c   for key = 1, ndim must be equal to 2.
c
      if (key.eq.1 .and. ndim.ne.2) then
          ifail = 4
          go to 999
      end if
c
c   for key = 2, ndim must be equal to 3.
c
      if (key.eq.2 .and. ndim.ne.3) then
          ifail = 5
          go to 999
      end if
c
c   for key = 0, we point at the selected integration rule.
c
      if (key.eq.0) then
          if (ndim.eq.2) then
              keyf = 1
          else if (ndim.eq.3) then
              keyf = 2
          else
              keyf = 3
          endif
      else
          keyf = key
      endif
c
c   compute num and wtleng as a function of keyf and ndim.
c
      if (keyf.eq.1) then
          num = 65
          wtleng = 14
      else if (keyf.eq.2) then
          num = 127
          wtleng = 13
      else if (keyf.eq.3) then
          num = 1 + 4*2*ndim + 2*ndim* (ndim-1) + 4*ndim* (ndim-1) +
     +          4*ndim* (ndim-1)* (ndim-2)/3 + 2**ndim
          wtleng = 9
          if (ndim.eq.2) wtleng = 8
      else if (keyf.eq.4) then
          num = 1 + 3*2*ndim + 2*ndim* (ndim-1) + 2**ndim
          wtleng = 6
      end if
c
c   compute maxsub.
c
      maxsub = (maxpts-num)/ (2*num) + 1
c
c   compute minsub.
c
      minsub = (minpts-num)/ (2*num) + 1
      if (mod(minpts-num,2*num).ne.0) then
          minsub = minsub + 1
      end if
      minsub = max(2,minsub)
c
c   check on positive numfun.
c
      if (numfun.lt.1) then
          ifail = 6
          go to 999
      end if
c
c   check on legal upper and lower limits of integration.
c
      do 10 j = 1,ndim
          if (a(j)-b(j).eq.0) then
              ifail = 7
              go to 999
          end if
10    continue
c
c   check on maxpts < 3*num.
c
      if (maxpts.lt.3*num) then
          ifail = 8
          go to 999
      end if
c
c   check on maxpts >= minpts.
c
      if (maxpts.lt.minpts) then
          ifail = 9
          go to 999
      end if
c
c   check on legal accuracy requests.
c
      if (epsabs.lt.0 .and. epsrel.lt.0) then
          ifail = 10
          go to 999
      end if
c
c   check on big enough double precision workspace.
c
      limit = maxsub* (2*ndim+2*numfun+2) + 17*mdiv*numfun + 1
      if (nw.lt.limit) then
          ifail = 11
          go to 999
      end if
c
c    check on legal restar.
c
      if (restar.ne.0 .and. restar.ne.1) then
          ifail = 12
          go to 999
      end if
999   return
c
c***end dchhre
c
      end
      subroutine dcuhre(ndim,numfun,a,b,minpts,maxpts,funsub,epsabs,
     +                  epsrel,key,nw,restar,result,abserr,neval,ifail,
     +                  work)
c***begin prologue dcuhre
c***date written   900116   (yymmdd)
c***revision date  900116   (yymmdd)
c***category no. h2b1a1
c***author
c            jarle berntsen, the computing centre,
c            university of bergen, thormohlens gt. 55,
c            n-5008 bergen, norway
c            phone..  47-5-544055
c            email..  jarle@eik.ii.uib.no
c            terje o. espelid, department of informatics,
c            university of bergen, thormohlens gt. 55,
c            n-5008 bergen, norway
c            phone..  47-5-544180
c            email..  terje@eik.ii.uib.no
c            alan genz, computer science department, washington state
c            university, pullman, wa 99163-1210, usa
c            phone.. 509-335-2131
c            email..  acg@cs2.cs.wsu.edu
c***keywords automatic multidimensional integrator,
c            n-dimensional hyper-rectangles,
c            general purpose, global adaptive
c***purpose  the routine calculates an approximation to a given
c            vector of definite integrals
c
c      b(1) b(2)     b(ndim)
c     i    i    ... i       (f ,f ,...,f      ) dx(ndim)...dx(2)dx(1),
c      a(1) a(2)     a(ndim)  1  2      numfun
c
c       where f = f (x ,x ,...,x    ), i = 1,2,...,numfun.
c              i   i  1  2      ndim
c
c            hopefully satisfying for each component of i the following
c            claim for accuracy:
c            abs(i(k)-result(k)).le.max(epsabs,epsrel*abs(i(k)))
c***description computation of integrals over hyper-rectangular
c            regions.
c            dcuhre is a driver for the integration routine
c            dadhre, which repeatedly subdivides the region
c            of integration and estimates the integrals and the
c            errors over the subregions with greatest
c            estimated errors until the error request
c            is met or maxpts function evaluations have been used.
c
c            for ndim = 2 the default integration rule is of
c            degree 13 and uses 65 evaluation points.
c            for ndim = 3 the default integration rule is of
c            degree 11 and uses 127 evaluation points.
c            for ndim greater then 3 the default integration rule
c            is of degree 9 and uses num evaluation points where
c              num = 1 + 4*2*ndim + 2*ndim*(ndim-1) + 4*ndim*(ndim-1) +
c                    4*ndim*(ndim-1)*(ndim-2)/3 + 2**ndim
c            the degree 9 rule may also be applied for ndim = 2
c            and ndim = 3.
c            a rule of degree 7 is available in all dimensions.
c            the number of evaluation
c            points used by the degree 7 rule is
c              num = 1 + 3*2*ndim + 2*ndim*(ndim-1) + 2**ndim
c
c            when dcuhre computes estimates to a vector of
c            integrals, all components of the vector are given
c            the same treatment. that is, i(f ) and i(f ) for
c                                            j         k
c            j not equal to k, are estimated with the same
c            subdivision of the region of integration.
c            for integrals with enough similarity, we may save
c            time by applying dcuhre to all integrands in one call.
c            for integrals that vary continuously as functions of
c            some parameter, the estimates produced by dcuhre will
c            also vary continuously when the same subdivision is
c            applied to all components. this will generally not be
c            the case when the different components are given
c            separate treatment.
c
c            on the other hand this feature should be used with
c            caution when the different components of the integrals
c            require clearly different subdivisions.
c
c   on entry
c
c     ndim   integer.
c            number of variables. 1 < ndim <=  15.
c     numfun integer.
c            number of components of the integral.
c     a      real array of dimension ndim.
c            lower limits of integration.
c     b      real array of dimension ndim.
c            upper limits of integration.
c     minpts integer.
c            minimum number of function evaluations.
c     maxpts integer.
c            maximum number of function evaluations.
c            the number of function evaluations over each subregion
c            is num.
c            if (key = 0 or key = 1) and (ndim = 2) then
c              num = 65
c            elseif (key = 0 or key = 2) and (ndim = 3) then
c              num = 127
c            elseif (key = 0 and ndim > 3) or (key = 3) then
c              num = 1 + 4*2*ndim + 2*ndim*(ndim-1) + 4*ndim*(ndim-1) +
c                    4*ndim*(ndim-1)*(ndim-2)/3 + 2**ndim
c            elseif (key = 4) then
c              num = 1 + 3*2*ndim + 2*ndim*(ndim-1) + 2**ndim
c            maxpts >= 3*num and maxpts >= minpts
c            for 3 < ndim < 13 the minimum values for maxpts are:
c             ndim =    4   5   6    7    8    9    10   11    12
c            key = 3:  459 819 1359 2151 3315 5067 7815 12351 20235
c            key = 4:  195 309  483  765 1251 2133 3795  7005 13299
c     funsub externally declared subroutine for computing
c            all components of the integrand at the given
c            evaluation point.
c            it must have parameters (ndim,x,numfun,funvls)
c            input parameters:
c              ndim   integer that defines the dimension of the
c                     integral.
c              x      real array of dimension ndim
c                     that defines the evaluation point.
c              numfun integer that defines the number of
c                     components of i.
c            output parameter:
c              funvls real array of dimension numfun
c                     that defines numfun components of the integrand.
c
c     epsabs real.
c            requested absolute error.
c     epsrel real.
c            requested relative error.
c     key    integer.
c            key to selected local integration rule.
c            key = 0 is the default value.
c                  for ndim = 2 the degree 13 rule is selected.
c                  for ndim = 3 the degree 11 rule is selected.
c                  for ndim > 3 the degree  9 rule is selected.
c            key = 1 gives the user the 2 dimensional degree 13
c                  integration rule that uses 65 evaluation points.
c            key = 2 gives the user the 3 dimensional degree 11
c                  integration rule that uses 127 evaluation points.
c            key = 3 gives the user the degree 9 integration rule.
c            key = 4 gives the user the degree 7 integration rule.
c                  this is the recommended rule for problems that
c                  require great adaptivity.
c     nw     integer.
c            defines the length of the working array work.
c            let maxsub denote the maximum allowed number of subregions
c            for the given values of maxpts, key and ndim.
c            maxsub = (maxpts-num)/(2*num) + 1
c            nw should be greater or equal to
c            maxsub*(2*ndim+2*numfun+2) + 17*numfun + 1
c            for efficient execution on parallel computers
c            nw should be chosen greater or equal to
c            maxsub*(2*ndim+2*numfun+2) + 17*numfun*mdiv + 1
c            where mdiv is the number of subregions that are divided in
c            each subdivision step.
c            mdiv is default set internally in dcuhre equal to 1.
c            for efficient execution on parallel computers
c            with nproc processors mdiv should be set equal to
c            the smallest integer such that mod(2*mdiv,nproc) = 0.
c
c     restar integer.
c            if restar = 0, this is the first attempt to compute
c            the integral.
c            if restar = 1, then we restart a previous attempt.
c            in this case the only parameters for dcuhre that may
c            be changed (with respect to the previous call of dcuhre)
c            are minpts, maxpts, epsabs, epsrel and restar.
c
c   on return
c
c     result real array of dimension numfun.
c            approximations to all components of the integral.
c     abserr real array of dimension numfun.
c            estimates of absolute errors.
c     neval  integer.
c            number of function evaluations used by dcuhre.
c     ifail  integer.
c            ifail = 0 for normal exit, when abserr(k) <=  epsabs or
c              abserr(k) <=  abs(result(k))*epsrel with maxpts or less
c              function evaluations for all values of k,
c              1 <= k <= numfun .
c            ifail = 1 if maxpts was too small for dcuhre
c              to obtain the required accuracy. in this case dcuhre
c              returns values of result with estimated absolute
c              errors abserr.
c            ifail = 2 if key is less than 0 or key greater than 4.
c            ifail = 3 if ndim is less than 2 or ndim greater than 15.
c            ifail = 4 if key = 1 and ndim not equal to 2.
c            ifail = 5 if key = 2 and ndim not equal to 3.
c            ifail = 6 if numfun is less than 1.
c            ifail = 7 if volume of region of integration is zero.
c            ifail = 8 if maxpts is less than 3*num.
c            ifail = 9 if maxpts is less than minpts.
c            ifail = 10 if epsabs < 0 and epsrel < 0.
c            ifail = 11 if nw is too small.
c            ifail = 12 if unlegal restar.
c     work   real array of dimension nw.
c            used as working storage.
c            work(nw) = nsub, the number of subregions in the data
c            structure.
c            let wrksub=(nw-1-17*numfun*mdiv)/(2*ndim+2*numfun+2)
c            work(1),...,work(numfun*wrksub) contain
c              the estimated components of the integrals over the
c              subregions.
c            work(numfun*wrksub+1),...,work(2*numfun*wrksub) contain
c              the estimated errors over the subregions.
c            work(2*numfun*wrksub+1),...,work(2*numfun*wrksub+ndim*
c              wrksub) contain the centers of the subregions.
c            work(2*numfun*wrksub+ndim*wrksub+1),...,work((2*numfun+
c              ndim)*wrksub+ndim*wrksub) contain subregion half widths.
c            work(2*numfun*wrksub+2*ndim*wrksub+1),...,work(2*numfun*
c              wrksub+2*ndim*wrksub+wrksub) contain the greatest errors
c              in each subregion.
c            work((2*numfun+2*ndim+1)*wrksub+1),...,work((2*numfun+
c              2*ndim+1)*wrksub+wrksub) contain the direction of
c              subdivision in each subregion.
c            work(2*(ndim+numfun+1)*wrksub),...,work(2*(ndim+numfun+1)*
c              wrksub+ 17*mdiv*numfun) is used as temporary
c              storage in dadhre.
c
c
c        dcuhre example test program
c
c
c   dtest1 is a simple test driver for dcuhre.
c
c   output produced on a sun 3/50.
c
c       dcuhre test results
c
c    ftest calls = 3549, ifail =  0
c   n   estimated error   integral
c   1     0.00000010     0.13850818
c   2     0.00000013     0.06369469
c   3     0.00000874     0.05861748
c   4     0.00000021     0.05407034
c   5     0.00000019     0.05005614
c   6     0.00000009     0.04654608
c
c     program dtest1
c     external ftest
c     integer key, n, nf, ndim, mincls, maxcls, ifail, neval, nw
c     parameter (ndim = 5, nw = 5000, nf = ndim+1)
c     real *8 a(ndim), b(ndim), wrkstr(nw)
c     real *8 absest(nf), finest(nf), absreq, relreq
c     do 10 n = 1,ndim
c        a(n) = 0
c        b(n) = 1
c  10 continue
c     mincls = 0
c     maxcls = 10000
c     key = 0
c     absreq = 0
c     relreq = 1e-3
c     call dcuhre(ndim, nf, a, b, mincls, maxcls, ftest, absreq, relreq,
c    * key, nw, 0, finest, absest, neval, ifail, wrkstr)
c     print 9999, neval, ifail
c9999 format (8x, 'dcuhre test results', //'     ftest calls = ', i4,
c    * ', ifail = ', i2, /'    n   estimated error   integral')
c     do 20 n = 1,nf
c        print 9998, n, absest(n), finest(n)
c9998    format (3x, i2, 2f15.8)
c  20 continue
c     end
c     subroutine ftest(ndim, z, nfun, f)
c     integer n, ndim, nfun
c     real *8 z(ndim), f(nfun), sum
c     sum = 0
c     do 10 n = 1,ndim
c        sum = sum + n*z(n)**2
c  10 continue
c     f(1) = exp(-sum/2)
c     do 20 n = 1,ndim
c        f(n+1) = z(n)*f(1)
c  20 continue
c     end
c
c***long description
c
c   the information for each subregion is contained in the
c   data structure work.
c   when passed on to dadhre, work is split into eight
c   arrays values, errors, centrs, hwidts, greate, dir,
c   oldres and work.
c   values contains the estimated values of the integrals.
c   errors contains the estimated errors.
c   centrs contains the centers of the subregions.
c   hwidts contains the half widths of the subregions.
c   greate contains the greatest estimated error for each subregion.
c   dir    contains the directions for further subdivision.
c   oldres and work are used as work arrays in dadhre.
c
c   the data structures for the subregions are in dadhre organized
c   as a heap, and the size of greate(i) defines the position of
c   region i in the heap. the heap is maintained by the program
c   dtrhre.
c
c   the subroutine dadhre is written for efficient execution on shared
c   memory parallel computer. on a computer with nproc processors we wil
c   in each subdivision step divide mdiv regions, where mdiv is
c   chosen such that mod(2*mdiv,nproc) = 0, in totally 2*mdiv new region
c   each processor will then compute estimates of the integrals and erro
c   over 2*mdiv/nproc subregions in each subdivision step.
c   the subroutine for estimating the integral and the error over
c   each subregion, drlhre, uses work2 as a work array.
c   we must make sure that each processor writes its results to
c   separate parts of the memory, and therefore the sizes of work and
c   work2 are functions of mdiv.
c   in order to achieve parallel processing of subregions, compiler
c   directives should be placed in front of the do 200
c   loop in dadhre on machines like alliant and cray.
c
c***references
c   j.berntsen, t.o.espelid and a.genz, an adaptive algorithm
c   for the approximate calculation of multiple integrals,
c   to be published.
c
c   j.berntsen, t.o.espelid and a.genz, dcuhre: an adaptive
c   multidimensional integration routine for a vector of
c   integrals, to be published.
c
c***routines called dchhre,dadhre
c***end prologue dcuhre
c
c   global variables.
c
      external funsub
      integer ndim,numfun,minpts,maxpts,key,nw,restar
      integer neval,ifail
      real *8 a(ndim),b(ndim),epsabs,epsrel
      real *8 result(numfun),abserr(numfun),work(nw)
c
c   local variables.
c
c   mdiv   integer.
c          mdiv is the number of subregions that are divided in
c          each subdivision step in dadhre.
c          mdiv is chosen default to 1.
c          for efficient execution on parallel computers
c          with nproc processors mdiv should be set equal to
c          the smallest integer such that mod(2*mdiv,nproc) = 0.
c   maxdim integer.
c          the maximum allowed value of ndim.
c   maxwt  integer. the maximum number of weights used by the
c          integration rule.
c   wtleng integer.
c          the number of generators used by the selected rule.
c   work2  real work space. the length
c          depends on the parameters mdiv,maxdim and maxwt.
c   maxsub integer.
c          the maximum allowed number of subdivisions
c          for the given values of key, ndim and maxpts.
c   minsub integer.
c          the minimum allowed number of subregions for the given
c          values of minpts, key and ndim.
c   wrksub integer.
c          the maximum allowed number of subregions as a function
c          of nw, numfun, ndim and mdiv. this determines the length
c          of the main work arrays.
c   num    integer. the number of integrand evaluations needed
c          over each subregion.
c
      integer mdiv,maxwt,wtleng,maxdim,lenw2,maxsub,minsub
      integer num,nsub,lenw,keyf
      parameter (mdiv=1)
      parameter (maxdim=15)
      parameter (maxwt=14)
      parameter (lenw2=2*mdiv*maxdim* (maxwt+1)+12*maxwt+2*maxdim)
      integer wrksub,i1,i2,i3,i4,i5,i6,i7,i8,k1,k2,k3,k4,k5,k6,k7,k8
      real *8 work2(lenw2)
c
c***first executable statement dcuhre
c
c   compute num, wtleng, maxsub and minsub,
c   and check the input parameters.
c
      call dchhre(maxdim,ndim,numfun,mdiv,a,b,minpts,maxpts,epsabs,
     +            epsrel,key,nw,restar,num,maxsub,minsub,keyf,
     +            ifail,wtleng)
      wrksub = (nw - 1 - 17*mdiv*numfun)/(2*ndim + 2*numfun + 2)
      if (ifail.ne.0) then
          go to 999
      end if
c
c   split up the work space.
c
      i1 = 1
      i2 = i1 + wrksub*numfun
      i3 = i2 + wrksub*numfun
      i4 = i3 + wrksub*ndim
      i5 = i4 + wrksub*ndim
      i6 = i5 + wrksub
      i7 = i6 + wrksub
      i8 = i7 + numfun*mdiv
      k1 = 1
      k2 = k1 + 2*mdiv*wtleng*ndim
      k3 = k2 + wtleng*5
      k4 = k3 + wtleng
      k5 = k4 + ndim
      k6 = k5 + ndim
      k7 = k6 + 2*mdiv*ndim
      k8 = k7 + 3*wtleng
c
c   on restart runs the number of subregions from the
c   previous call is assigned to nsub.
c
      if (restar.eq.1) then
          nsub = work(nw)
      end if
c
c   compute the size of the temporary work space needed in dadhre.
c
      lenw = 16*mdiv*numfun
      call dadhre(ndim,numfun,mdiv,a,b,minsub,maxsub,funsub,epsabs,
     +            epsrel,keyf,restar,num,lenw,wtleng,
     +            result,abserr,neval,nsub,ifail,work(i1),work(i2),
     +            work(i3),work(i4),work(i5),work(i6),work(i7),work(i8),
     +            work2(k1),work2(k2),work2(k3),work2(k4),work2(k5),
     +            work2(k6),work2(k7),work2(k8))
      work(nw) = nsub
999   return
c
c***end dcuhre
c
      end
      subroutine dfshre(ndim,center,hwidth,x,g,numfun,funsub,fulsms,
     +                  funvls)
c***begin prologue dfshre
c***keywords fully symmetric sum
c***purpose  to compute fully symmetric basic rule sums
c***author   alan genz, computer science department, washington
c            state university, pullman, wa 99163-1210 usa
c***last modification 88-04-08
c***description dfshre computes a fully symmetric sum for a vector
c            of integrand values over a hyper-rectangular region.
c            the sum is fully symmetric with respect to the center of
c            the region and is taken over all sign changes and
c            permutations of the generators for the sum.
c
c   on entry
c
c   ndim   integer.
c          number of variables.
c   center real array of dimension ndim.
c          the coordinates for the center of the region.
c   hwidth real array of dimension ndim.
c          hwidth(i) is half of the width of dimension i of the region.
c   x      real array of dimension ndim.
c          a work array.
c   g      real array of dimension ndim.
c          the generators for the fully symmetric sum. these must be
c          non-negative and non-increasing.
c   numfun integer.
c          number of components for the vector integrand.
c   funsub externally declared subroutine.
c          for computing the components of the integrand at a point x.
c          it must have parameters (ndim, x, numfun, funvls).
c           input parameters:
c            x      real array of dimension ndim.
c                   defines the evaluation point.
c            ndim   integer.
c                   number of variables for the integrand.
c            numfun integer.
c                   number of components for the vector integrand.
c           output parameters:
c            funvls real array of dimension numfun.
c                   the components of the integrand at the point x.
c   on return
c
c   fulsms real array of dimension numfun.
c          the values for the fully symmetric sums for each component
c          of the integrand.
c   funvls real array of dimension numfun.
c          a work array.
c
c***routines called: funsub
c
c***end prologue dfshre
c
c   global variables.
c
      external funsub
      integer ndim,numfun
      real *8 center(ndim),hwidth(ndim),x(ndim),g(ndim),
     +                 fulsms(numfun),funvls(numfun)
c
c   local variables.
c
      integer ixchng,lxchng,i,j,l
      real *8 gl,gi
c
c***first executable statement dfshre
c
      do 10 j = 1,numfun
          fulsms(j) = 0
10    continue
c
c     compute centrally symmetric sum for permutation of g
c
20    do 30 i = 1,ndim
          x(i) = center(i) + g(i)*hwidth(i)
30    continue
40    call funsub(ndim,x,numfun,funvls)
      do 50 j = 1,numfun
          fulsms(j) = fulsms(j) + funvls(j)
50    continue
      do 60 i = 1,ndim
          g(i) = - g(i)
          x(i) = center(i) + g(i)*hwidth(i)
          if (g(i).lt.0) go to 40
60    continue
c
c       find next distinct permutation of g and loop back for next sum.
c       permutations are generated in reverse lexicographic order.
c
      do 80 i = 2,ndim
          if (g(i-1).gt.g(i)) then
              gi = g(i)
              ixchng = i - 1
              do 70 l = 1, (i-1)/2
                  gl = g(l)
                  g(l) = g(i-l)
                  g(i-l) = gl
                  if (gl.le.gi) ixchng = ixchng - 1
                  if (g(l).gt.gi) lxchng = l
70            continue
              if (g(ixchng).le.gi) ixchng = lxchng
              g(i) = g(ixchng)
              g(ixchng) = gi
              go to 20
          end if
80    continue
c
c     restore original order to generators
c
      do 90 i = 1,ndim/2
          gi = g(i)
          g(i) = g(ndim-i+1)
          g(ndim-i+1) = gi
90    continue
c
c***end dfshre
c
      end
      subroutine dinhre(ndim,key,wtleng,w,g,errcof,rulpts,scales,norms)
c***begin prologue dinhre
c***purpose dinhre computes abscissas and weights of the integration
c            rule and the null rules to be used in error estimation.
c            these are computed as functions of ndim and key.
c***description dinhre will for given values of ndim and key compute or
c            select the correct values of the abscissas and
c            corresponding weights for different
c            integration rules and null rules and assign them to
c            g and w.
c            the heuristic error coefficients errcof
c            will be computed as a function of key.
c            scaling factors scales and normalization factors norms
c            used in the error estimation are computed.
c
c
c   on entry
c
c     ndim   integer.
c            number of variables.
c     key    integer.
c            key to selected local integration rule.
c     wtleng integer.
c            the number of weights in each of the rules.
c
c   on return
c
c     w      real array of dimension (5,wtleng).
c            the weights for the basic and null rules.
c            w(1,1), ...,w(1,wtleng) are weights for the basic rule.
c            w(i,1), ...,w(i,wtleng), for i > 1 are null rule weights.
c     g      real array of dimension (ndim,wtleng).
c            the fully symmetric sum generators for the rules.
c            g(1,j),...,g(ndim,j) are the generators for the points
c            associated with the the jth weights.
c     errcof real array of dimension 6.
c            heuristic error coefficients that are used in the
c            error estimation in basrul.
c            it is assumed that the error is computed using:
c             if (n1*errcof(1) < n2 and n2*errcof(2) < n3)
c               then error = errcof(3)*n1
c               else error = errcof(4)*max(n1, n2, n3)
c             error = error + ep*(errcof(5)*error/(es+error)+errcof(6))
c            where n1-n3 are the null rules, ep is the error for
c            the parent
c            subregion and es is the error for the sibling subregion.
c     rulpts real array of dimension wtleng.
c            a work array containing the number of points produced by
c            each generator of the selected rule.
c     scales real array of dimension (3,wtleng).
c            scaling factors used to construct new null rules,
c            n1, n2 and n3,
c            based on a linear combination of two successive null rules
c            in the sequence of null rules.
c     norms  real array of dimension (3,wtleng).
c            2**ndim/(1-norm of the null rule constructed by each of
c            the scaling factors.)
c
c***routines called  d132re,d113re,d07hre,d09hre
c***end prologue dinhre
c
c   global variables.
c
      integer ndim,key,wtleng
      real *8 g(ndim,wtleng),w(5,wtleng),errcof(6)
      real *8 rulpts(wtleng),scales(3,wtleng)
      real *8 norms(3,wtleng)
c
c   local variables.
c
      integer i,j,k
      real *8 we(14)
c
c***first executable statement dinhre
c
c   compute w, g and errcof.
c
      if (key.eq.1) then
          call d132re(wtleng,w,g,errcof,rulpts)
      else if (key.eq.2) then
          call d113re(wtleng,w,g,errcof,rulpts)
      else if (key.eq.3) then
          call d09hre(ndim,wtleng,w,g,errcof,rulpts)
      else if (key.eq.4) then
          call d07hre(ndim,wtleng,w,g,errcof,rulpts)
      end if
c
c   compute scales and norms.
c
      do 100 k = 1,3
          do 50 i = 1,wtleng
              if (w(k+1,i).ne.0) then
                  scales(k,i) = - w(k+2,i)/w(k+1,i)
              else
                  scales(k,i) = 100
              end if
              do 30 j = 1,wtleng
                  we(j) = w(k+2,j) + scales(k,i)*w(k+1,j)
30            continue
              norms(k,i) = 0
              do 40 j = 1,wtleng
                  norms(k,i) = norms(k,i) + rulpts(j)*abs(we(j))
40            continue
              norms(k,i) = 2**ndim/norms(k,i)
50        continue
100   continue
      return
c
c***end dinhre
c
      end
      subroutine drlhre(ndim,center,hwidth,wtleng,g,w,errcof,numfun,
     +                  funsub,scales,norms,x,null,basval,rgnerr,direct)
c***begin prologue drlhre
c***keywords basic numerical integration rule
c***purpose  to compute basic integration rule values.
c***author   alan genz, computer science department, washington
c            state university, pullman, wa 99163-1210 usa
c***last modification 90-02-06
c***description drlhre computes basic integration rule values for a
c            vector of integrands over a hyper-rectangular region.
c            these are estimates for the integrals. drlhre also computes
c            estimates for the errors and determines the coordinate axis
c            where the fourth difference for the integrands is largest.
c
c   on entry
c
c   ndim   integer.
c          number of variables.
c   center real array of dimension ndim.
c          the coordinates for the center of the region.
c   hwidth real array of dimension ndim.
c          hwidth(i) is half of the width of dimension i of the region.
c   wtleng integer.
c          the number of weights in the basic integration rule.
c   g      real array of dimension (ndim,wtleng).
c          the fully symmetric sum generators for the rules.
c          g(1,j), ..., g(ndim,j) are the are the generators for the
c          points associated with the jth weights.
c   w      real array of dimension (5,wtleng).
c          the weights for the basic and null rules.
c          w(1,1),...,w(1,wtleng) are weights for the basic rule.
c          w(i,1),...,w(i,wtleng), for i > 1 are null rule weights.
c   errcof real array of dimension 6.
c          the error coefficients for the rules.
c          it is assumed that the error is computed using:
c           if (n1*errcof(1) < n2 and n2*errcof(2) < n3)
c             then error = errcof(3)*n1
c             else error = errcof(4)*max(n1, n2, n3)
c           error = error + ep*(errcof(5)*error/(es+error)+errcof(6))
c          where n1-n4 are the null rules, ep is the error
c          for the parent
c          subregion and es is the error for the sibling subregion.
c   numfun integer.
c          number of components for the vector integrand.
c   funsub externally declared subroutine.
c          for computing the components of the integrand at a point x.
c          it must have parameters (ndim,x,numfun,funvls).
c           input parameters:
c            x      real array of dimension ndim.
c                   defines the evaluation point.
c            ndim   integer.
c                   number of variables for the integrand.
c            numfun integer.
c                   number of components for the vector integrand.
c           output parameters:
c            funvls real array of dimension numfun.
c                   the components of the integrand at the point x.
c   scales real array of dimension (3,wtleng).
c          scaling factors used to construct new null rules based
c          on a linear combination of two successive null rules
c          in the sequence of null rules.
c   norms  real array of dimension (3,wtleng).
c          2**ndim/(1-norm of the null rule constructed by each of the
c          scaling factors.)
c   x      real array of dimension ndim.
c          a work array.
c   null   real array of dimension (numfun, 8)
c          a work array.
c
c   on return
c
c   basval real array of dimension numfun.
c          the values for the basic rule for each component
c          of the integrand.
c   rgnerr real array of dimension numfun.
c          the error estimates for each component of the integrand.
c   direct real.
c          the coordinate axis where the fourth difference of the
c          integrand values is largest.
c
c***references
c   a.c.genz and a.a.malik, an adaptive algorithm for numerical
c   integration over an n-dimensional rectangular region,
c   j.comp.appl.math., 6:295-302, 1980.
c
c   t.o.espelid, integration rules, null rules and error
c   estimation, reports in informatics 33, dept. of informatics,
c   univ. of bergen, 1988.
c
c***routines called: dfshre, funsub
c
c***end prologue drlhre
c
c   global variables.
c
      external funsub
      integer wtleng,numfun,ndim
      real *8 center(ndim),x(ndim),hwidth(ndim),basval(numfun),
     +                 rgnerr(numfun),null(numfun,8),w(5,wtleng),
     +                 g(ndim,wtleng),errcof(6),direct,scales(3,wtleng),
     +                 norms(3,wtleng)
c
c   local variables.
c
      real *8 rgnvol,difsum,difmax,frthdf
      integer i,j,k,divaxn
      real *8 search,ratio
c
c***first executable statement drlhre
c
c
c       compute volume of subregion, initialize divaxn and rule sums;
c       compute fourth differences and new divaxn (rgnerr is used
c       for a work array here). the integrand values used for the
c       fourth divided differences are accumulated in rule arrays.
c
      rgnvol = 1
      divaxn = 1
      do 10 i = 1,ndim
          rgnvol = rgnvol*hwidth(i)
          x(i) = center(i)
          if (hwidth(i).gt.hwidth(divaxn)) divaxn = i
10    continue
      call funsub(ndim,x,numfun,rgnerr)
      do 30 j = 1,numfun
          basval(j) = w(1,1)*rgnerr(j)
          do 20 k = 1,4
              null(j,k) = w(k+1,1)*rgnerr(j)
20        continue
30    continue
      difmax = 0
      ratio = (g(1,3)/g(1,2))**2
      do 60 i = 1,ndim
          x(i) = center(i) - hwidth(i)*g(1,2)
          call funsub(ndim,x,numfun,null(1,5))
          x(i) = center(i) + hwidth(i)*g(1,2)
          call funsub(ndim,x,numfun,null(1,6))
          x(i) = center(i) - hwidth(i)*g(1,3)
          call funsub(ndim,x,numfun,null(1,7))
          x(i) = center(i) + hwidth(i)*g(1,3)
          call funsub(ndim,x,numfun,null(1,8))
          x(i) = center(i)
          difsum = 0
          do 50 j = 1,numfun
              frthdf = 2* (1-ratio)*rgnerr(j) - (null(j,7)+null(j,8)) +
     +                 ratio* (null(j,5)+null(j,6))
c
c           ignore differences below roundoff
c
              if (rgnerr(j)+frthdf/4.ne.rgnerr(j)) difsum = difsum +
     +            abs(frthdf)
              do 40 k = 1,4
                  null(j,k) = null(j,k) + w(k+1,2)*
     +                        (null(j,5)+null(j,6)) +
     +                        w(k+1,3)* (null(j,7)+null(j,8))
40            continue
              basval(j) = basval(j) + w(1,2)* (null(j,5)+null(j,6)) +
     +                    w(1,3)* (null(j,7)+null(j,8))
50        continue
          if (difsum.gt.difmax) then
              difmax = difsum
              divaxn = i
          end if
60    continue
      direct = divaxn
c
c    finish computing the rule values.
c
      do 90 i = 4,wtleng
          call dfshre(ndim,center,hwidth,x,g(1,i),numfun,funsub,rgnerr,
     +                null(1,5))
          do 80 j = 1,numfun
              basval(j) = basval(j) + w(1,i)*rgnerr(j)
              do 70 k = 1,4
                  null(j,k) = null(j,k) + w(k+1,i)*rgnerr(j)
70            continue
80        continue
90    continue
c
c    compute errors.
c
      do 130 j = 1,numfun
c
c    we search for the null rule, in the linear space spanned by two
c    successive null rules in our sequence, which gives the greatest
c    error estimate among all normalized (1-norm) null rules in this
c    space.
c
          do 110 i = 1,3
              search = 0
              do 100 k = 1,wtleng
                  search = max(search,abs(null(j,i+1)+scales(i,
     +                     k)*null(j,i))*norms(i,k))
100           continue
              null(j,i) = search
110       continue
          if (errcof(1)*null(j,1).le.null(j,2) .and.
     +        errcof(2)*null(j,2).le.null(j,3)) then
              rgnerr(j) = errcof(3)*null(j,1)
          else
              rgnerr(j) = errcof(4)*max(null(j,1),null(j,2),null(j,3))
          end if
          rgnerr(j) = rgnvol*rgnerr(j)
          basval(j) = rgnvol*basval(j)
130   continue
c
c***end drlhre
c
      end
      subroutine dtrhre(dvflag,ndim,numfun,sbrgns,values,errors,centrs,
     +                  hwidts,greate,error,value,center,hwidth,dir)
c***begin prologue dtrhre
c***purpose dtrhre maintains a heap of subregions.
c***description dtrhre maintains a heap of subregions.
c            the subregions are ordered according to the size
c            of the greatest error estimates of each subregion(greate).
c
c   parameters
c
c     dvflag integer.
c            if dvflag = 1, we remove the subregion with
c            greatest error from the heap.
c            if dvflag = 2, we insert a new subregion in the heap.
c     ndim   integer.
c            number of variables.
c     numfun integer.
c            number of components of the integral.
c     sbrgns integer.
c            number of subregions in the heap.
c     values real array of dimension (numfun,sbrgns).
c            used to store estimated values of the integrals
c            over the subregions.
c     errors real array of dimension (numfun,sbrgns).
c            used to store the corresponding estimated errors.
c     centrs real array of dimension (ndim,sbrgns).
c            used to store the center limits of the stored subregions.
c     hwidts real array of dimension (ndim,sbrgns).
c            used to store the hwidth limits of the stored subregions.
c     greate real array of dimension sbrgns.
c            used to store the greatest estimated errors in
c            all subregions.
c     error  real array of dimension numfun.
c            used as intermediate storage for the error of a subregion.
c     value  real array of dimension numfun.
c            used as intermediate storage for the estimate
c            of the integral over a subregion.
c     center real array of dimension ndim.
c            used as intermediate storage for the center of
c            the subregion.
c     hwidth real array of dimension ndim.
c            used as intermediate storage for the half width of
c            the subregion.
c     dir    integer array of dimension sbrgns.
c            dir is used to store the directions for
c            further subdivision.
c
c***routines called-none
c***end prologue dtrhre
c
c   global variables.
c
      integer dvflag,ndim,numfun,sbrgns
      real *8 values(numfun,*),errors(numfun,*)
      real *8 centrs(ndim,*)
      real *8 hwidts(ndim,*)
      real *8 greate(*)
      real *8 error(numfun),value(numfun)
      real *8 center(ndim),hwidth(ndim)
      real *8 dir(*)
c
c   local variables.
c
c   great  is used as intermediate storage for the greatest error of a
c          subregion.
c   direct is used as intermediate storage for the direction of further
c          subdivision.
c   subrgn position of child/parent subregion in the heap.
c   subtmp position of parent/child subregion in the heap.
c
      integer j,subrgn,subtmp
      real *8 great,direct
c
c***first executable statement dtrhre
c
c   save values to be stored in their correct place in the heap.
c
      great = greate(sbrgns)
      direct = dir(sbrgns)
      do 5 j = 1,numfun
          error(j) = errors(j,sbrgns)
          value(j) = values(j,sbrgns)
5     continue
      do 10 j = 1,ndim
          center(j) = centrs(j,sbrgns)
          hwidth(j) = hwidts(j,sbrgns)
10    continue
c
c    if dvflag = 1, we will remove the region
c    with greatest estimated error from the heap.
c
      if (dvflag.eq.1) then
          sbrgns = sbrgns - 1
          subrgn = 1
20        subtmp = 2*subrgn
          if (subtmp.le.sbrgns) then
              if (subtmp.ne.sbrgns) then
c
c   find max. of left and right child.
c
                  if (greate(subtmp).lt.greate(subtmp+1)) then
                      subtmp = subtmp + 1
                  end if
              end if
c
c   compare max.child with parent.
c   if parent is max., then done.
c
              if (great.lt.greate(subtmp)) then
c
c   move the values at position subtmp up the heap.
c
                  greate(subrgn) = greate(subtmp)
                  do 25 j = 1,numfun
                      errors(j,subrgn) = errors(j,subtmp)
                      values(j,subrgn) = values(j,subtmp)
25                continue
                  dir(subrgn) = dir(subtmp)
                  do 30 j = 1,ndim
                      centrs(j,subrgn) = centrs(j,subtmp)
                      hwidts(j,subrgn) = hwidts(j,subtmp)
30                continue
                  subrgn = subtmp
                  go to 20
              end if
          end if
      else if (dvflag.eq.2) then
c
c   if dvflag = 2, then insert new region in the heap.
c
          subrgn = sbrgns
40        subtmp = subrgn/2
          if (subtmp.ge.1) then
c
c   compare max.child with parent.
c   if parent is max, then done.
c
              if (great.gt.greate(subtmp)) then
c
c   move the values at position subtmp down the heap.
c
                  greate(subrgn) = greate(subtmp)
                  do 45 j = 1,numfun
                      errors(j,subrgn) = errors(j,subtmp)
                      values(j,subrgn) = values(j,subtmp)
45                continue
                  dir(subrgn) = dir(subtmp)
                  do 50 j = 1,ndim
                      centrs(j,subrgn) = centrs(j,subtmp)
                      hwidts(j,subrgn) = hwidts(j,subtmp)
50                continue
                  subrgn = subtmp
                  go to 40
              end if
          end if
      end if
c
c    insert the saved values in their correct places.
c
      if (sbrgns.gt.0) then
          greate(subrgn) = great
          do 55 j = 1,numfun
              errors(j,subrgn) = error(j)
              values(j,subrgn) = value(j)
55        continue
          dir(subrgn) = direct
          do 60 j = 1,ndim
              centrs(j,subrgn) = center(j)
              hwidts(j,subrgn) = hwidth(j)
60        continue
      end if
c
c***end dtrhre
c
      return
      end








